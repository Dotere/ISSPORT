#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// {Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	// {Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	Если РольДоступна("ПолныеПрава") Тогда
		 Элементы.Ответственный.Доступность = Истина;
	Иначе 	 
		 Элементы.Ответственный.Доступность = Ложь;
	КонецЕсли;
	// }Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	// }Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	
	// {Рарус adilas #10700 -Вид спорта подставлять по текущему этапу спортсмена 2020.11.05
	Если Объект.Ссылка.Пустая() Тогда
		УчетСпортсменовСервер.ЗаполнитьНастройкиПоУмолчанию(Объект);
		УправлениеУчебнымГодомСсылка = УчетСпортсменовСервер.ПолучитьДокументТекущегоУчебногоГода();
	    Объект.УчебныйГод = УправлениеУчебнымГодомСсылка.УчебныйГод;
	Иначе
		УчетСпортсменовСервер.ЗаполнитьНастройкиПоУмолчанию(Объект);
		УчетСпортсменовСервер.ЗаполнитьДокументУчебныйГод(ЭтотОбъект, Объект.УчебныйГод, Объект.Организация);
	КонецЕсли;
	// }Рарус adilas #10700 -Вид спорта подставлять по текущему этапу спортсмена 2020.11.05
	
	Если Параметры.Свойство("массивСпортсменов") Тогда
		Для Каждого ЗначениеСпортсмен Из Параметры.массивСпортсменов Цикл
			строкаСпортсмен = Объект.Спортсмены.Добавить();
			строкаСпортсмен.Спортсмен  = ЗначениеСпортсмен;
		КонецЦикла;
	ИначеЕсли Параметры.Свойство("Спортсмен") Тогда
		СтрокаСпортсмен = Объект.Спортсмены.Добавить();
		СтрокаСпортсмен.Спортсмен = Параметры.Спортсмен;	
	КонецЕсли;
	
	// {Рарус adilas #13392 -Номер документа 2021.02.12
	Если Пользователи.РолиДоступны("АдминистраторСистемы, ПолныеПрава") Тогда
		Элементы.Номер.Доступность = Истина;
		Элементы.Номер.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	// }Рарус adilas #13392 -Номер документа 2021.02.12
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если Объект.Проведен Тогда
		Оповестить("ВключениеСпортсменовВГруппу", Новый Структура("ДокументСсылка", Объект.Ссылка));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийШапки

&НаКлиенте
Процедура УчебныйГодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	УчетСпортсменовКлиент.УчебныйГодНачалоВыбора(ЭтотОбъект,СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодОткрытие(Элемент, СтандартнаяОбработка)
	УчетСпортсменовКлиент.УчебныйГодОткрытие(УправлениеУчебнымГодомСсылка,СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодСоздание(Элемент, СтандартнаяОбработка)
	УчетСпортсменовКлиент.УчебныйГодСоздание(ЭтаФорма, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УчебныйГодСозданиеЗавершение(Результат, ДопПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
	   Объект.УчебныйГод = Результат.УчебныйГодСсылка;
	   УправлениеУчебнымГодомСсылка = Результат.УчебныйГодОбъект;
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодНачалоВыбораЗавершение(Результат, ДопПараметры) Экспорт
	
	УправлениеУчебнымГодомСсылка = Результат;
    ПолучитьУчебныйГодСсылка();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьУчебныйГодСсылка()
	Объект.УчебныйГод = УправлениеУчебнымГодомСсылка.УчебныйГод;
КонецПроцедуры	

// {Рарус adilas #-7400 -Команда подбор спортменов, открыть форму выбора справочника  2020.07.26
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПодборСпискаСпортсменов" Тогда
		Для Каждого ЗначениеВыбора Из Параметр.СписокСпортсменов Цикл
			строкаСпортсмен           			    = Объект.Спортсмены.Добавить();
			строкаСпортсмен.Спортсмен  				= ЗначениеВыбора.Значение;
			строкаСпортсмен.ДатаИсключенияИзГруппы 	= Объект.Дата;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция МоментВремениДок()
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат ТекущаяДатаСеанса();
	Иначе 
		Возврат Объект.Ссылка.МоментВремени(); 
	КонецЕсли;
	
КонецФункции
// }Рарус adilas #-7400 -Команда подбор спортменов, открыть форму выбора справочника  2020.07.26

#КонецОбласти

#Область ОбработчикиКомандФормы

// {Рарус adilas #-7400 -Команда подбор спортменов, открыть форму выбора справочника  2020.07.26
// Открываем форму подбора с параметрами при закрытии формы происходит добавление в табличную часть
&НаКлиенте
Процедура ПодборСпортсменов(Команда)
	// Вставить содержимое обработчика. 
	МоментВремени = МоментВремениДок();
	
	ИдентификаторИсключениеИзГруппы = "";
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТолькоПодбор", ИСТИНА);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("МоментВремени", МоментВремени);
	ПараметрыФормы.Вставить("ДатаСреза", Объект.Дата);
	ПараметрыФормы.Вставить("Тренер", Объект.ТренерСборной);
	ПараметрыФормы.Вставить("Группа", Объект.Группа);
	ПараметрыФормы.Вставить("УчебныйГод", Объект.УчебныйГод);
	ПараметрыФормы.Вставить("ВидСпорта", Объект.ВидСпорта);
	ПараметрыФормы.Вставить("ИдентификаторИсключениеИзГруппы", ИдентификаторИсключениеИзГруппы);
	ОткрытьФорму("Справочник.Спортсмены.Форма.ФормаВыбораСпортсменаПоУчреждению", ПараметрыФормы);
КонецПроцедуры
// }Рарус adilas #-7400 -Команда подбор спортменов, открыть форму выбора справочника  2020.07.26

#КонецОбласти