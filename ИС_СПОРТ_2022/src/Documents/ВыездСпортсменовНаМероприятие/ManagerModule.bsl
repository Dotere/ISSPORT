#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"       		, ДокументСсылка);
	Запрос.УстановитьПараметр("Период"       		, ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Организация"  		, ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("Соревнование" 		, ДокументСсылка.Соревнование);
	Запрос.УстановитьПараметр("ВидСпорта"    		, ДокументСсылка.ВидСпорта);
	Запрос.УстановитьПараметр("НомерСоревнования"   , ДокументСсылка.НомерСоревнования);
	Запрос.УстановитьПараметр("МестоПроведения"		, ДокументСсылка.МестоПроведения);
	Запрос.УстановитьПараметр("ДатаВыезда"			, ДокументСсылка.ДатаВыезда);
	Запрос.УстановитьПараметр("ДатаВозвращения"		, ДокументСсылка.ДатаВозвращения);
	Запрос.Текст = 	"ВЫБРАТЬ
	               	|	""ВыездСпортсменовНаМероприятие"" КАК ИмяРегистра_,
	               	|	&Период КАК Период,
	               	|	&Организация КАК Организация,
	               	|	&ВидСпорта КАК ВидСпорта,
	               	|	&Соревнование КАК Соревнование,
					|	&НомерСоревнования КАК НомерСоревнования,
					|	&ДатаВыезда КАК ДатаВыезда,
					|	&ДатаВозвращения КАК ДатаВозвращения,
	               	|	ВыездСпортсменовСпротсмены.Спортсмен КАК Спортсмен,
	               	|	&МестоПроведения КАК МестоПроведения
	               	|ИЗ
	               	|	Документ.ВыездСпортсменовНаМероприятие.Спортсмены КАК ВыездСпортсменовСпротсмены
	               	|ГДЕ
	               	|	ВыездСпортсменовСпротсмены.Ссылка = &Ссылка";
					
	ТаблицаВыездыСпортсменов = Запрос.Выполнить().Выгрузить();				
	
	Запрос2 = Новый Запрос;
	Запрос2.УстановитьПараметр("Ссылка"       		, ДокументСсылка);
	Запрос2.УстановитьПараметр("Период"       		, ДокументСсылка.Дата);
	Запрос2.УстановитьПараметр("Организация"  		, ДокументСсылка.Организация);
	Запрос2.УстановитьПараметр("Соревнование" 		, ДокументСсылка.Соревнование);
	Запрос2.УстановитьПараметр("ВидСпорта"    		, ДокументСсылка.ВидСпорта);
	Запрос2.УстановитьПараметр("НомерСоревнования"  , ДокументСсылка.НомерСоревнования);
	Запрос2.УстановитьПараметр("МестоПроведения"	, ДокументСсылка.МестоПроведения);
	Запрос2.УстановитьПараметр("ДатаВыезда"			, ДокументСсылка.ДатаВыезда);
	Запрос2.УстановитьПараметр("ДатаВозвращения"	, ДокументСсылка.ДатаВозвращения);
	Запрос2.Текст = 	"ВЫБРАТЬ
	                	|	""ВыездСпортсменовНаМероприятие"" КАК ИмяРегистра_,
	                	|	&Период КАК Период,
	                	|	&Организация КАК Организация,
	                	|	&ВидСпорта КАК ВидСпорта,
	                	|	&Соревнование КАК Соревнование,
	                	|	&МестоПроведения КАК МестоПроведения,
	                	|	&НомерСоревнования КАК НомерСоревнования,
	                	|	&ДатаВыезда КАК ДатаВыезда,
	                	|	&ДатаВозвращения КАК ДатаВозвращения,
	                	|	ВыездСпортсменовТренеры.Тренер КАК Тренер
	                	|ИЗ
	                	|	Документ.ВыездСпортсменовНаМероприятие.Тренеры КАК ВыездСпортсменовТренеры
	                	|ГДЕ
	                	|	ВыездСпортсменовТренеры.Ссылка = &Ссылка";
					
	ТаблицаВыездыТренеров= Запрос2.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаВыездыСпортсменов" , ТаблицаВыездыСпортсменов);
	ТаблицыДляДвижений.Вставить("ТаблицаВыездыТренеров" , ТаблицаВыездыТренеров);
	
    ДополнительныеСвойства.Вставить("Отказ", Отказ);
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ВыездСпортсменовНаМероприятие.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольПередПроведением(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ВыездСпортсменовНаМероприятие.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовОтменыПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ВыездСпортсменовНаМероприятие.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВыездТабДок") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ВыездТабДок", "Выезд", СформироватьПечатнуюФормуЗаявка(МассивОбъектов));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуЗаявка(МассивОбъектовДляПечати) Экспорт
	
	Если ТипЗнч(МассивОбъектовДляПечати) = Тип("Массив") Тогда
		
		МассивОбъектов = МассивОбъектовДляПечати;
		
	Иначе
		
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(МассивОбъектовДляПечати);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВыездСпортсменовНаМероприятие.Ссылка КАК Ссылка,
		|	ВыездСпортсменовНаМероприятие.Соревнование КАК Соревнование,
		|	ВыездСпортсменовНаМероприятие.Организация КАК Организация,
		|	ВыездСпортсменовНаМероприятие.МестоПроведения КАК МестоПроведения,
		|	ВыездСпортсменовНаМероприятие.ВидСпорта КАК ВидСпорта,
		|	ВыездСпортсменовНаМероприятие.ДатаВыезда КАК ДатаВыезда,
		|	ВыездСпортсменовНаМероприятие.ДатаВозвращения КАК ДатаВозвращения,
		|	ВыездСпортсменовНаМероприятие.ОбщаяСумма КАК ОбщаяСумма,
		|	ВыездСпортсменовНаМероприятие.МОЛ КАК МОЛ,
		|	ВыездСпортсменовНаМероприятие.ОснованиеВыезда КАК ОснованиеВыезда,
		|	ВыездСпортсменовНаМероприятие.Город КАК Город,
		|	ВыездСпортсменовНаМероприятие.Отправление КАК Отправление,
		|	ВыездСпортсменовНаМероприятие.Финансирование КАК Финансирование,
		|	ВыездСпортсменовНаМероприятие.НомерПриказа КАК НомерПриказа,
		|	ВыездСпортсменовНаМероприятие.ДатаПриказа КАК ДатаПриказа
		|ИЗ
		|	Документ.ВыездСпортсменовНаМероприятие КАК ВыездСпортсменовНаМероприятие
		|ГДЕ
		|	ВыездСпортсменовНаМероприятие.Ссылка В(&МассивОбъектов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВыездСпортсменовНаМероприятиеТренеры.Тренер КАК Тренеры,
		|	ВыездСпортсменовНаМероприятиеТренеры.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ВыездСпортсменовНаМероприятие.Тренеры КАК ВыездСпортсменовНаМероприятиеТренеры
		|ГДЕ
		|	ВыездСпортсменовНаМероприятиеТренеры.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыездСпортсменовНаМероприятиеТренеры.Ссылка,
		|	ВыездСпортсменовНаМероприятиеТренеры.Тренер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВыездСпортсменовНаМероприятиеСпротсмены.Спортсмен КАК Спортсмены,
		|	ФизическиеЛица.ДатаРождения КАК ДатаРождения,
		|	ВыездСпортсменовНаМероприятиеСпротсмены.Ссылка КАК Ссылка,
		|	МАКСИМУМ(ЭтапыСпортивнойПодготовкиСпортсменов.Период) КАК Период
		|ПОМЕСТИТЬ ВТ_Этапы
		|ИЗ
		|	Документ.ВыездСпортсменовНаМероприятие.Спортсмены КАК ВыездСпортсменовНаМероприятиеСпротсмены
		|		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ПО ВыездСпортсменовНаМероприятиеСпротсмены.Спортсмен.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ЭтапыСпортивнойПодготовкиСпортсменов КАК ЭтапыСпортивнойПодготовкиСпортсменов
		|		ПО ВыездСпортсменовНаМероприятиеСпротсмены.Спортсмен = ЭтапыСпортивнойПодготовкиСпортсменов.Спортсмен
		|			И ВыездСпортсменовНаМероприятиеСпротсмены.Ссылка.ВидСпорта = ЭтапыСпортивнойПодготовкиСпортсменов.ВидСпорта
		|			И ВыездСпортсменовНаМероприятиеСпротсмены.Ссылка.ДатаВыезда > ЭтапыСпортивнойПодготовкиСпортсменов.Период
		|ГДЕ
		|	ВыездСпортсменовНаМероприятиеСпротсмены.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыездСпортсменовНаМероприятиеСпротсмены.Ссылка,
		|	ФизическиеЛица.ДатаРождения,
		|	ВыездСпортсменовНаМероприятиеСпротсмены.Спортсмен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Этапы.Спортсмены КАК Спортсмены,
		|	ВТ_Этапы.ДатаРождения КАК ДатаРождения,
		|	ВТ_Этапы.Ссылка КАК Ссылка,
		|	МАКСИМУМ(ВТ_Этапы.Период) КАК Период,
		|	ЭтапыСпортивнойПодготовкиСпортсменов.Этап КАК Этап,
		|	ЭтапыСпортивнойПодготовкиСпортсменов.Тренер КАК Тренер
		|ПОМЕСТИТЬ ВТ_ЭтапыПоследние
		|ИЗ
		|	ВТ_Этапы КАК ВТ_Этапы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭтапыСпортивнойПодготовкиСпортсменов КАК ЭтапыСпортивнойПодготовкиСпортсменов
		|		ПО ВТ_Этапы.Спортсмены = ЭтапыСпортивнойПодготовкиСпортсменов.Спортсмен
		|			И ВТ_Этапы.Период = ЭтапыСпортивнойПодготовкиСпортсменов.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Этапы.Спортсмены,
		|	ВТ_Этапы.ДатаРождения,
		|	ВТ_Этапы.Ссылка,
		|	ЭтапыСпортивнойПодготовкиСпортсменов.Этап,
		|	ЭтапыСпортивнойПодготовкиСпортсменов.Тренер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЭтапыПоследние.Спортсмены КАК Спортсмены,
		|	ВТ_ЭтапыПоследние.ДатаРождения КАК ДатаРождения,
		|	ВТ_ЭтапыПоследние.Ссылка КАК Ссылка,
		|	ВТ_ЭтапыПоследние.Период КАК Период,
		|	ВТ_ЭтапыПоследние.Этап КАК Этап,
		|	ВТ_ЭтапыПоследние.Тренер КАК Тренер
		|ИЗ
		|	ВТ_ЭтапыПоследние КАК ВТ_ЭтапыПоследние
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВыездСпортсменовНаМероприятиеСмета.ВидРасхода КАК ВидРасхода,
		|	ВыездСпортсменовНаМероприятиеСмета.Количество КАК Количество,
		|	ВыездСпортсменовНаМероприятиеСмета.Цена КАК Цена,
		|	ВыездСпортсменовНаМероприятиеСмета.Сумма КАК Сумма,
		|	ВыездСпортсменовНаМероприятиеСмета.Комментарий КАК Комментарий,
		|	ВыездСпортсменовНаМероприятиеСмета.Ссылка КАК Ссылка,
		|	ВыездСпортсменовНаМероприятиеСмета.КоличествоДней КАК КоличествоДней
		|ИЗ
		|	Документ.ВыездСпортсменовНаМероприятие.Смета КАК ВыездСпортсменовНаМероприятиеСмета
		|ГДЕ
		|	ВыездСпортсменовНаМероприятиеСмета.Ссылка В(&МассивОбъектов)";
	
	ПутьКМакету = "Документ.ВыездСпортсменовНаМероприятие.ВыездТабДок";
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету);
	
	ТабДок = Новый ТабличныйДокумент;
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьСтрокаСпортсмена = Макет.ПолучитьОбласть("СтрокаСпортсмены");
	ОбластьОписание = Макет.ПолучитьОбласть("ОбластьОписание");
	ОбластьРасходНаСпортсмена = Макет.ПолучитьОбласть("СтрокаРасходНаСпортсменов");
	ОбластьСмета = Макет.ПолучитьОбласть("СтрокаСмета");
	ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал");
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоЗапросов = РезультатЗапроса.Количество();
	
	ВыборкаДокументы = РезультатЗапроса[КоличествоЗапросов - 6].Выбрать();
	ВыборкаТренеры = РезультатЗапроса[КоличествоЗапросов - 5].Выбрать();
	ВыборкаСпортсмены = РезультатЗапроса[КоличествоЗапросов - 2].Выбрать();
	ВыборкаСмета = РезультатЗапроса[КоличествоЗапросов - 1].Выбрать();
	
	СтруктураПоиска = Новый Структура("Ссылка");
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		ОбластьШапка.Параметры.Заполнить(ВыборкаДокументы);
		ОбластьШапка.Параметры.Соревнование = СклонениеПредставленийОбъектов.ПросклонятьПредставление(ВыборкаДокументы.Соревнование,6);
		ОбластьШапка.Параметры.ДатаВыезда = Формат(ВыборкаДокументы.ДатаВыезда, "ДЛФ=D");
		ОбластьШапка.Параметры.ДатаВозвращения = Формат(ВыборкаДокументы.ДатаВозвращения, "ДЛФ=D");
		ОбластьШапка.Параметры.ДатаПриказа = Формат(ВыборкаДокументы.ДатаПриказа, "ДЛФ=D");
		ОбластьШапка.Параметры.ВидСпорта = НРег(ВыборкаДокументы.ВидСпорта);
		ОбластьШапка.Параметры.ПолноеНаименование = СклонениеПредставленийОбъектов.ПросклонятьПредставление(ВыборкаДокументы.Организация.ПолноеНаименование,6);
		ТабДок.Вывести(ОбластьШапка);
		Индекс = 1;
		СтруктураПоиска.Ссылка = ВыборкаДокументы.Ссылка;
		Пока ВыборкаСпортсмены.НайтиСледующий(СтруктураПоиска) Цикл
			
			ОбластьСтрокаСпортсмена.Параметры.ДатаРождения = Формат(ВыборкаСпортсмены.ДатаРождения,"ДЛФ=D");
			ОбластьСтрокаСпортсмена.Параметры.Спортсмены = ВыборкаСпортсмены.Спортсмены.Наименование;
			Если Не ВыборкаСпортсмены.Тренер = null Тогда
				
				ОбластьСтрокаСпортсмена.Параметры.Тренер = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаСпортсмены.Тренер.ФизическоеЛицо); 
				
			КонецЕсли;
			ОбластьСтрокаСпортсмена.Параметры.Этап = ВыборкаСпортсмены.Этап; 
			ОбластьСтрокаСпортсмена.Параметры.Номер = Индекс;
			ТабДок.Вывести(ОбластьСтрокаСпортсмена);
			Индекс = Индекс + 1;
			
			
		КонецЦикла;
		СписокТренеров = Неопределено;
		Пока ВыборкаТренеры.НайтиСледующий(СтруктураПоиска) Цикл
			
			Если СписокТренеров = Неопределено Тогда
				
				 СписокТренеров = Строка(ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаТренеры.Тренеры.ФизическоеЛицо));
			 Иначе
				 СписокТренеров = СписокТренеров + ", " + Строка(ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаТренеры.Тренеры.ФизическоеЛицо));
				
			КонецЕсли
			
		КонецЦикла;	
		
		ВидыСмет = Неопределено;
		
		Пока ВыборкаСмета.НайтиСледующий(СтруктураПоиска) Цикл
			
			Если ВидыСмет = Неопределено Тогда
				 ВидыСмет = НРег(Строка(ВыборкаСмета.ВидРасхода));
			 Иначе
				 ВидыСмет = НРег(ВидыСмет + ", " + Строка(ВыборкаСмета.ВидРасхода)); 
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьОписание.Параметры.Заполнить(ВыборкаДокументы);
		ОбластьОписание.Параметры.Соревнование = СклонениеПредставленийОбъектов.ПросклонятьПредставление(ВыборкаДокументы.Соревнование,6);
		ОбластьОписание.Параметры.СоревнованиеДругойПадеж = СклонениеПредставленийОбъектов.ПросклонятьПредставление(ВыборкаДокументы.Соревнование,3);
		ОбластьОписание.Параметры.СписокТренеры = СклонениеПредставленийОбъектов.ПросклонятьФИО(СписокТренеров,4);
		ОбластьОписание.Параметры.ВидыСмет = ВидыСмет;
		ОбластьОписание.Параметры.ВидСпорта = НРег(ВыборкаДокументы.ВидСпорта); 
		ОбластьОписание.Параметры.Отправление = НРег(ВыборкаДокументы.Отправление);
		ОбластьОписание.Параметры.ДатаВыезда = Формат(ВыборкаДокументы.ДатаВыезда, "ДЛФ=D");
		ОбластьОписание.Параметры.ДатаВозвращения = Формат(ВыборкаДокументы.ДатаВозвращения, "ДЛФ=D");
		ТабДок.Вывести(ОбластьОписание);
		Индекс = 1;
		ВыборкаСпортсмены.Сбросить();
		Пока ВыборкаСпортсмены.НайтиСледующий(СтруктураПоиска) Цикл
			
			ОбластьРасходНаСпортсмена.Параметры.Спортсмены = ВыборкаСпортсмены.Спортсмены.Наименование; 
			ОбластьРасходНаСпортсмена.Параметры.Номер = Индекс;
			ТабДок.Вывести(ОбластьРасходНаСпортсмена);

			Индекс = Индекс + 1;
			
			
		КонецЦикла;
		ВыборкаСмета.Сбросить();
		Индекс = 1;
		Пока ВыборкаСмета.НайтиСледующий(СтруктураПоиска) Цикл
			
			 ОбластьСмета.Параметры.Заполнить(ВыборкаСмета);
			 Если ВыборкаСмета.КоличествоДней > 1 Тогда
				 ОбластьСмета.Параметры.КоличествоПотраченыхДней = Строка(ВыборкаСмета.КоличествоДней) + " дн. x "; 
			 КонецЕсли;
			 ОбластьСмета.Параметры.НомерСтроки = Индекс;
			 ТабДок.Вывести(ОбластьСмета);
			 Индекс = Индекс + 1;
			 
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Заполнить(ВыборкаДокументы);
		ОбластьПодвал.Параметры.Директор = Строка(ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаДокументы.Организация.Директор));
		ОбластьПодвал.Параметры.МОЛ = СклонениеПредставленийОбъектов.ПросклонятьФИО(Строка(ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаДокументы.МОЛ.ФизическоеЛицо)),4);
		ОбластьПодвал.Параметры.МОЛИмен = Строка(ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаДокументы.МОЛ.ФизическоеЛицо));
		ТабДок.Вывести(ОбластьПодвал);
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;	
	
	ТабДок.КлючПараметровПечати	= ПутьКМакету;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб 			= Истина;

	Возврат ТабДок;
	
	
КонецФункции

#КонецОбласти