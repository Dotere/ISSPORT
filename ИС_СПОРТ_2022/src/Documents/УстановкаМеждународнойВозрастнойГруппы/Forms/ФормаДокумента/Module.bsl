#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// {Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	// {Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	Если РольДоступна("ПолныеПрава") Тогда
		 Элементы.Ответственный.Доступность = Истина;
	Иначе 	 
		 Элементы.Ответственный.Доступность = Ложь;
	КонецЕсли;
	// }Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	// }Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	
	Если Объект.Ссылка.Пустая() Тогда
		Если Параметры.Свойство("МассивСпортсменов") Тогда
			ОчищатьТабличнуюЧасть = Ложь;
			Для Каждого Спортсмен Из Параметры.МассивСпортсменов Цикл
				строкаСпортсмен = Объект.Спортсмены.Добавить();
			    строкаСпортсмен.Спортсмен = Спортсмен;
			КонецЦикла;
		ИначеЕсли Параметры.Свойство("Спортсмен") Тогда
			// {Рарус adilas #10751 -Ноывй документ в карточке спортсмена если не заданы основные значения 2020.11.07
			СтрокаСпортсмен = Объект.Спортсмены.Добавить();
			СтрокаСпортсмен.Спортсмен = Параметры.Спортсмен;
			// }Рарус adilas #10751 -Ноывй документ в карточке спортсмена если не заданы основные значения 2020.11.07
			Элементы.СпортсменыЗаполнитьСпортсменовПоДатеРождения.Доступность = Ложь;
			Элементы.СпортсменыСпортсмен.РежимВыбораИзСписка = Истина;
			Элементы.СпортсменыСпортсмен.СписокВыбора.Добавить(Параметры.Спортсмен);
		// {Рарус adilas #10751 -Новый документ в карточке спортсмена если не заданы основные значения 2020.11.10
		ИначеЕсли Параметры.Свойство("Отбор") Тогда
			СтрокаСпортсмен = Объект.Спортсмены.Добавить();
			СтрокаСпортсмен.Спортсмен = Параметры.Отбор.Спортсмен;
			Элементы.СпортсменыЗаполнитьСпортсменовПоДатеРождения.Доступность = Ложь;
			Элементы.СпортсменыСпортсмен.РежимВыбораИзСписка = Истина;
			Элементы.СпортсменыСпортсмен.СписокВыбора.Добавить(Параметры.Отбор.Спортсмен);
		КонецЕсли;
		
		// }Рарус adilas #10751 -Новый документ в карточке спортсмена если не заданы основные значения 2020.11.10
			
		УчетСпортсменовСервер.ЗаполнитьНастройкиПоУмолчанию(Объект);
		УправлениеУчебнымГодомСсылка = УчетСпортсменовСервер.ПолучитьДокументТекущегоУчебногоГода();
	    Объект.УчебныйГод = УправлениеУчебнымГодомСсылка.УчебныйГод;
		
	Иначе
		УчетСпортсменовСервер.ЗаполнитьДокументУчебныйГод(ЭтотОбъект, Объект.УчебныйГод, Объект.Организация);
		Элементы.НадписьВозраст.Заголовок = ПолучитьНадписьПоГруппе();
	КонецЕсли;
	
	// {Рарус adilas #13392 -Номер документа 2021.02.12
	Если Пользователи.РолиДоступны("АдминистраторСистемы, ПолныеПрава") Тогда
		Элементы.Номер.Доступность = Истина;
		Элементы.Номер.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	// }Рарус adilas #13392 -Номер документа 2021.02.12
					
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МеждународнаяВозрастнаяГруппаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Спортсмены.Количество() > 0 И ОчищатьТабличнуюЧасть Тогда
		
		СтандартнаяОбработка = Ложь;
		ВопросПользователю("ВозрастнаяГруппаПриИзмененииЗавершение");
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Спортсмены.Количество() > 0 И ОчищатьТабличнуюЧасть Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("УчебныйГодПриИзмененииЗавершение",
		ЭтотОбъект);	
		
		ПоказатьВопрос(Оповещение,
		"Табличная часть 'Спортсмены' будет очищена.
		|Продолжить?",
		РежимДиалогаВопрос.ДаНет,
		0);
		
		Возврат;
		
	Иначе
		УчетСпортсменовКлиент.УчебныйГодНачалоВыбора(ЭтотОбъект,СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодОткрытие(Элемент, СтандартнаяОбработка)
	
	УчетСпортсменовКлиент.УчебныйГодОткрытие(УправлениеУчебнымГодомСсылка,СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МеждународнаяВозрастнаяГруппаПриИзменении(Элемент)
	
	УстановитьЗаголовокНадписиВозраст();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСпортсмены

&НаКлиенте
Процедура СпортсменыСпортсменНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	
	Если Объект.ДатаЗачисления = Дата(1, 1, 1) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Укажите дату зачисления!'");
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Поле = "ДатаЗачисления";
		Сообщение.Сообщить();
		Возврат;	
	ИначеЕсли Не ЗначениеЗаполнено(Объект.Организация) Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Укажите организацию!'");
	    Сообщение.ПутьКДанным = "Объект";
		Сообщение.Поле = "Организация";
		Сообщение.Сообщить();
		Возврат;
	Иначе
		ПараметрыФормы.Вставить("ДатаСреза", УчетСпортсменовВызовСервера.ТекущаяДатаНаСервере());
	    ПараметрыФормы.Вставить("Организация", Объект.Организация);
	КонецЕсли;

	ПараметрыФормы.Вставить("ТолькоВыбор", Истина);
	ОбработчикВыбора = Новый ОписаниеОповещения("СпортсменНачалоВыбораЗавершение", ЭтотОбъект);
		
	ОткрытьФорму("Справочник.Спортсмены.Форма.ФормаВыбораСоставУчащихся", ПараметрыФормы, ЭтаФорма, , , , ОбработчикВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЗаполнитьСпортсменовПоДатеРождения(Команда)
	
	Если НЕ ЭтотОбъект.ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Спортсмены.Количество() > 0 Тогда
		
		ВопросПользователю("ЗаполнитьСпортсменовПоДатеРожденияЗавершение");
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьСпортсменовПоДатеРожденияНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПользователю(ИмяОбработчика)
	
	Оповещение = Новый ОписаниеОповещения(ИмяОбработчика,
		ЭтотОбъект);	
		
		ПоказатьВопрос(Оповещение,
		"Данные в табличной части будут очищены. 
		|Продолжить?",
		РежимДиалогаВопрос.ДаНет,
		0,
		КодВозвратаДиалога.Да);
	
КонецПроцедуры	

&НаКлиенте
Процедура ВозрастнаяГруппаПриИзмененииЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.Спортсмены.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСпортсменовПоДатеРожденияЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьСпортсменовПоДатеРожденияНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпортсменовПоДатеРожденияНаСервере()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	Иначе	
		Объект.Спортсмены.Очистить();
		Объект.Спортсмены.Загрузить(Обработки.МастерЗаполненияУчебногоГода.ПолучитьТаблицуНеучтенныхСпортсменов(Объект.УчебныйГод, Объект.ВидСпорта, Объект.МеждународнаяВозрастнаяГруппа));
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура УчебныйГодПриИзмененииЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОчиститьТабличнуюЧастьНаСервере();
		УчетСпортсменовКлиент.УчебныйГодНачалоВыбора(ЭтотОбъект,Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТабличнуюЧастьНаСервере()
	
	Объект.Спортсмены.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодНачалоВыбораЗавершение(Результат, ДопПараметры) Экспорт
	
	УправлениеУчебнымГодомСсылка = Результат;
    ПолучитьУчебныйГодСсылка();
	УстановитьЗаголовокНадписиВозраст();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьУчебныйГодСсылка()
	
	Объект.УчебныйГод = УправлениеУчебнымГодомСсылка.УчебныйГод;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокНадписиВозраст()
	
	Элементы.НадписьВозраст.Заголовок = ПолучитьНадписьПоГруппе();
	
КонецПроцедуры	

&НаСервере
Функция ПолучитьНадписьПоГруппе()
	
	Если ЗначениеЗаполнено(Объект.МеждународнаяВозрастнаяГруппа) Тогда
		СтрокаГоды = "";
		Если ЗначениеЗаполнено(Объект.УчебныйГод) Тогда
			СтрокаГоды = " (" + Строка(Формат(Год(Объект.УчебныйГод.ДатаНачала) - Объект.МеждународнаяВозрастнаяГруппа.ВозрастДо, "ЧГ=0")) + " - "
				+ Строка(Формат(Год(Объект.УчебныйГод.ДатаНачала) - Объект.МеждународнаяВозрастнаяГруппа.ВозрастОт, "ЧГ=0")) + " гг.)";
		КонецЕсли;	
		
		Возврат Строка(Объект.МеждународнаяВозрастнаяГруппа.ВозрастОт) + " - " + Строка(Объект.МеждународнаяВозрастнаяГруппа.ВозрастДо) + " лет" + СтрокаГоды;
	Иначе
		Возврат "";
	КонецЕсли;	
	
КонецФункции

#КонецОбласти