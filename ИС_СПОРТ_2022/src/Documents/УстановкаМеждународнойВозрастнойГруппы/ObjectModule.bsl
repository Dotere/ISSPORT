// {Рарус adilas #1.0.0.2 -Исправления по SonarQube 2021.04.23
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
    КонецЕсли;
	
	УчетСпортсменовСервер.ЗаполнитьРеквизит(ЭтотОбъект, "Ответственный", ПараметрыСеанса.ТекущийПользователь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаСпортсменов.Спортсмен КАК Спортсмен,
	               |	ТаблицаСпортсменов.НомерСтроки КАК НомерСтроки
	               |ПОМЕСТИТЬ ВТ_СпортсменыТекущийДокумент
	               |ИЗ
	               |	&ТаблицаСпортсменов КАК ТаблицаСпортсменов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СпортсменыТекущийДокумент.Спортсмен КАК Спортсмен,
	               |	ВТ_СпортсменыТекущийДокумент.НомерСтроки КАК НомерСтроки,
				   |    ФизическиеЛица.ДатаРождения Как ДатаРождения
	               |ИЗ
	               |	ВТ_СпортсменыТекущийДокумент КАК ВТ_СпортсменыТекущийДокумент
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |		ПО ВТ_СпортсменыТекущийДокумент.Спортсмен.ФизическоеЛицо = ФизическиеЛица.Ссылка
	               |ГДЕ
	               |	ГОД(ФизическиеЛица.ДатаРождения) < &ГодРожденияОт ИЛИ ГОД(ФизическиеЛица.ДатаРождения) > &ГодРожденияДо";
	Запрос.УстановитьПараметр("ТаблицаСпортсменов", Спортсмены.Выгрузить());
	Запрос.УстановитьПараметр("ГодРожденияОт", Год(УчебныйГод.ДатаНачала) - МеждународнаяВозрастнаяГруппа.ВозрастДо);
	Запрос.УстановитьПараметр("ГодРожденияДо", Год(УчебныйГод.ДатаНачала) - МеждународнаяВозрастнаяГруппа.ВозрастОт);
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Отказ = Истина;
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Дата рождения (%1) спортсмена не соответствует возрастному диапазону группы: (%2 - %3) лет.
			|Возрастная группа: %4'"), 
			Формат(Выборка.ДатаРождения,"ДЛФ=Д"), 
			МеждународнаяВозрастнаяГруппа.ВозрастОт,
			МеждународнаяВозрастнаяГруппа.ВозрастДо,
			ВРег(МеждународнаяВозрастнаяГруппа.Наименование));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект.Спортсмены["+ СокрЛП(Выборка.НомерСтроки - 1) + "].Спортсмен");
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	ПроведениеСервер.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ, "МеждународныеВозрастныеГруппы");
	
	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовОтменыПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли