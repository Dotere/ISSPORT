
// {Рарус adilas #18019 -Дисциплины 2021.07.07
#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"               		, ДокументСсылка);
	Запрос.УстановитьПараметр("Период"               		, ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("ВидСпорта"            		, ДокументСсылка.ВидСпорта);
	Запрос.УстановитьПараметр("УчебныйГод"              	, ДокументСсылка.УчебныйГод);
	Запрос.УстановитьПараметр("Организация"          		, ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("ОрганизаторСоревнования" 	, ДокументСсылка.ОрганизаторСоревнования);
	// {Рарус adilas #11621 -Интервал периода соревнований в протоколе 2020.12.14
	Запрос.УстановитьПараметр("ДатаНачалаСоревнования"   	, ДокументСсылка.ДатаНачалаСоревнования);
	Запрос.УстановитьПараметр("ДатаОкончанияСоревнования"   , ДокументСсылка.ДатаОкончанияСоревнования);
	// }Рарус adilas #11621 -Интервал периода соревнований в протоколе 2020.12.14
	Запрос.УстановитьПараметр("Соревнование"         		, ДокументСсылка.Соревнование);
	// {Рарус adilas #11824 -Место проведения соревнования 2021.01.29
	Запрос.УстановитьПараметр("МестоПроведения"         	, ДокументСсылка.МестоПроведения);
	// }Рарус adilas #11824 -Место проведения соревнования 2021.01.29
	Запрос.УстановитьПараметр("ДокументОснование"    		, ДокументСсылка.ДокументОснование);
	Запрос.УстановитьПараметр("ТипРезультата"    	 		, ДокументСсылка.ТипРезультата);
	Запрос.УстановитьПараметр("СпособПодсчетаРезультата"	, ДокументСсылка.СпособПодсчетаРезультата);
	Запрос.УстановитьПараметр("ПроводитьПараметры"   		, ?(ДокументСсылка.ДокументОснование.КоличествоПараметров>1,Истина,Ложь));
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	СоставУчастников.Ссылка КАК Ссылка,
	                |	СоставУчастников.Спортсмен КАК Спортсмен,
	                |	СоставУчастников.GUIDУчастника КАК GUIDУчастника,
	                |	СоставУчастников.СтартовыйНомер КАК СтартовыйНомер,
	                |	СоставУчастников.Место КАК Место,
	                |	СоставУчастников.РезультатЧисло КАК РезультатЧисло,
	                |	СоставУчастников.РезультатБулево КАК РезультатБулево,
	                |	СоставУчастников.ОтставаниеМилСек КАК ОтставаниеМилСек,
	                |	СоставУчастников.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	СоставУчастников.Команда КАК Команда,
	                |	СоставУчастников.Дисциплина КАК Дисциплина
	                |ПОМЕСТИТЬ ВТ_Спортсмены
	                |ИЗ
	                |	Документ.ПротоколСоревнования.СоставУчастников КАК СоставУчастников
	                |ГДЕ
	                |	СоставУчастников.Ссылка = &Ссылка
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	""РезультатыПараметровСпортсменов"" КАК ИмяРегистра_,
	                |	&Период КАК Период,
	                |	&УчебныйГод КАК УчебныйГод,
	                |	&ДокументОснование КАК ДокументОснование,
	                |	&Организация КАК Организация,
	                |	&Соревнование КАК Соревнование,
	                |	&ВидСпорта КАК ВидСпорта,
	                |	ВТ_Спортсмены.Спортсмен КАК Спортсмен,
	                |	ВТ_Спортсмены.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	ВТ_Спортсмены.Команда КАК Команда,
	                |	ПротоколСоревнования.Параметр КАК Параметр,
	                |	ПротоколСоревнования.РезультатЧисло КАК РезультатЧисло,
	                |	ПротоколСоревнования.РезультатБулево КАК РезультатБулево,
	                |	ПротоколСоревнования.Состояние КАК Состояние,
	                |	ВТ_Спортсмены.Дисциплина КАК Дисциплина
	                |ИЗ
	                |	Документ.ПротоколСоревнования.ПараметрыСоревнования КАК ПротоколСоревнования
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Спортсмены КАК ВТ_Спортсмены
	                |		ПО ПротоколСоревнования.Ссылка = ВТ_Спортсмены.Ссылка
	                |			И ПротоколСоревнования.GUIDУчастника = ВТ_Спортсмены.GUIDУчастника
	                |			И ПротоколСоревнования.Дисциплина = ВТ_Спортсмены.Дисциплина
	                |ГДЕ
	                |	ПротоколСоревнования.Ссылка = &Ссылка
	                |	И &ПроводитьПараметры
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	""ИтоговыеРезультатыСоревнования"" КАК ИмяРегистра_,
	                |	&Период КАК Период,
	                |	&УчебныйГод КАК УчебныйГод,
	                |	&Организация КАК Организация,
	                |	&Соревнование КАК Соревнование,
	                |	&ДокументОснование КАК ДокументОснование,
	                |	&ВидСпорта КАК ВидСпорта,
	                |	СоставУчастников.Спортсмен КАК Спортсмен,
	                |	СоставУчастников.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	СоставУчастников.Команда КАК Команда,
	                |	СоставУчастников.РезультатЧисло КАК РезультатЧисло,
	                |	СоставУчастников.РезультатБулево КАК РезультатБулево,
	                |	СоставУчастников.ОтставаниеМилСек КАК ОтставаниеМилСек,
	                |	СоставУчастников.Место КАК Место,
	                |	СоставУчастников.СтартовыйНомер КАК СтартовыйНомер,
	                |	&ТипРезультата КАК ТипРезультата,
	                |	&ОрганизаторСоревнования КАК ОрганизаторСоревнования,
	                |	&СпособПодсчетаРезультата КАК СпособПодсчетаРезультата,
	                |	&ДатаНачалаСоревнования КАК ДатаНачалаСоревнования,
	                |	&ДатаОкончанияСоревнования КАК ДатаОкончанияСоревнования,
	                |	&МестоПроведения КАК МестоПроведения,
	                |	СоставУчастников.Дисциплина КАК Дисциплина
	                |ИЗ
	                |	Документ.ПротоколСоревнования.СоставУчастников КАК СоставУчастников
	                |ГДЕ
	                |	СоставУчастников.Ссылка = &Ссылка
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВключениеСоревнованийВРейтинги.Рейтинг КАК Рейтинг,
	                |	ВключениеСоревнованийВРейтинги.Рейтинг.Организатор КАК Организатор,
	                |	РейтингиМеждународныеВозрастныеГруппы.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	РейтингиМеждународныеВозрастныеГруппы.СистемаПодсчетаБаллов КАК СистемаПодсчетаБаллов,
	                |	&ОрганизаторСоревнования КАК ОрганизаторСоревнования
	                |ПОМЕСТИТЬ ВТ_Рейтинги
	                |ИЗ
	                |	РегистрСведений.ВключениеСоревнованийВРейтинги КАК ВключениеСоревнованийВРейтинги
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Рейтинги.МеждународныеВозрастныеГруппы КАК РейтингиМеждународныеВозрастныеГруппы
	                |		ПО ВключениеСоревнованийВРейтинги.Рейтинг = РейтингиМеждународныеВозрастныеГруппы.Ссылка
	                |ГДЕ
	                |	ВключениеСоревнованийВРейтинги.Соревнование = &Соревнование
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_Рейтинги.Рейтинг КАК Рейтинг,
	                |	ВТ_Рейтинги.Организатор КАК Организатор,
	                |	ВТ_Рейтинги.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	БалльнаяСистема.Место КАК Место,
	                |	БалльнаяСистема.КоличествоБаллов КАК КоличествоБаллов,
	                |	ВТ_Рейтинги.ОрганизаторСоревнования КАК ОрганизаторСоревнования
	                |ПОМЕСТИТЬ ВТ_СистемаПодсчетаБаллов
	                |ИЗ
	                |	ВТ_Рейтинги КАК ВТ_Рейтинги
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СистемаПодсчетаБаллов.БалльнаяСистема КАК БалльнаяСистема
	                |		ПО ВТ_Рейтинги.СистемаПодсчетаБаллов = БалльнаяСистема.Ссылка
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	""ПодсчетРейтингов"" КАК ИмяРегистра,
	                |	&Период КАК Период,
	                |	&УчебныйГод КАК УчебныйГод,
	                |	&Организация КАК Организация,
	                |	&Соревнование КАК Соревнование,
	                |	СоставУчастников.Спортсмен КАК Спортсмен,
	                |	СоставУчастников.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	СоставУчастников.Место КАК Место,
	                |	СоставУчастников.Команда КАК Команда,
	                |	ВТ_СистемаПодсчетаБаллов.Рейтинг КАК Рейтинг,
	                |	ВТ_СистемаПодсчетаБаллов.Организатор КАК Организатор,
	                |	ВТ_СистемаПодсчетаБаллов.КоличествоБаллов КАК КоличествоБаллов,
	                |	ВТ_СистемаПодсчетаБаллов.ОрганизаторСоревнования КАК ОрганизаторСоревнования,
	                |	СоставУчастников.Дисциплина КАК Дисциплина
	                |ИЗ
	                |	Документ.ПротоколСоревнования.СоставУчастников КАК СоставУчастников
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СистемаПодсчетаБаллов КАК ВТ_СистемаПодсчетаБаллов
	                |		ПО СоставУчастников.МеждународнаяВозрастнаяГруппа = ВТ_СистемаПодсчетаБаллов.МеждународнаяВозрастнаяГруппа
	                |			И СоставУчастников.Место = ВТ_СистемаПодсчетаБаллов.Место
	                |ГДЕ
	                |	СоставУчастников.Ссылка = &Ссылка
	                |	И СоставУчастников.Место <> 0
	                |	И ВТ_СистемаПодсчетаБаллов.КоличествоБаллов <> 0";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	ТаблицаРезультатыПараметровСпортсменов = Пакет[1].Выгрузить();
	ТаблицаИтоговыеРезультатыСоревнования  = Пакет[2].Выгрузить();
	ТаблицаПодсчетРейтингов                = Пакет[5].Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаИтоговыеРезультатыСоревнования"  , ТаблицаИтоговыеРезультатыСоревнования);
	ТаблицыДляДвижений.Вставить("ТаблицаРезультатыПараметровСпортсменов" , ТаблицаРезультатыПараметровСпортсменов);
	ТаблицыДляДвижений.Вставить("ТаблицаПодсчетРейтингов"                , ТаблицаПодсчетРейтингов);
	
    ДополнительныеСвойства.Вставить("Отказ", Отказ);
	
КонецПроцедуры	

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ПротоколСоревнования.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольПередПроведением(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ПротоколСоревнования.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовОтменыПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ПротоколСоревнования.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
// }Рарус adilas #18019 -Дисциплины 2021.07.07