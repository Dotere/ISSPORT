#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
	     Возврат;
	КонецЕсли; 
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	УчетСпортсменовСервер.ЗаполнитьРеквизит(ЭтотОбъект, "Ответственный", ПараметрыСеанса.ТекущийПользователь);	
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// {Рарус adilas #22016 -Учет посещаемости спортсменов 2021.11.15
	ВыполнитьПроверкуПередПроведением(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// }Рарус adilas #22016 -Учет посещаемости спортсменов 2021.11.15
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	ПроведениеСервер.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ, "ДанныеТабельногоУчетаПосещенийСпортсменов");

	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовОтменыПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// {Рарус adilas #22016 -Учет посещаемости спортсменов 2021.11.15
Процедура ВыполнитьПроверкуПередПроведением(Отказ)
	
	МассивСпортсменов = Новый Массив;
	МассивСпортсменов = ТаблицаПосещений.ВыгрузитьКолонку("Спортсмен"); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеТабельногоУчетаПосещенийСпортсменов.Регистратор КАК Регистратор,
		|	ДанныеТабельногоУчетаПосещенийСпортсменов.Спортсмен КАК Спортсмен,
		|	ДанныеТабельногоУчетаПосещенийСпортсменов.Тренер КАК Тренер,
		|	ДанныеТабельногоУчетаПосещенийСпортсменов.Этап КАК Этап,
		|	ДанныеТабельногоУчетаПосещенийСпортсменов.ПериодРегистрации КАК ПериодРегистрации
		|ИЗ
		|	РегистрНакопления.ДанныеТабельногоУчетаПосещенийСпортсменов КАК ДанныеТабельногоУчетаПосещенийСпортсменов
		|ГДЕ
		|	ДанныеТабельногоУчетаПосещенийСпортсменов.Спортсмен В(&МассивСпортсменов)
		|	И ДанныеТабельногоУчетаПосещенийСпортсменов.Тренер = &Тренер
		|	И ДанныеТабельногоУчетаПосещенийСпортсменов.Этап = &Этап
		|	И МЕСЯЦ(ДанныеТабельногоУчетаПосещенийСпортсменов.ПериодРегистрации) = &ПериодРегистрации
		|	И ДанныеТабельногоУчетаПосещенийСпортсменов.Регистратор <> &Регистратор";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", Месяц(ПериодРегистрации));
	Запрос.УстановитьПараметр("МассивСпортсменов", МассивСпортсменов);
	Запрос.УстановитьПараметр("Тренер", Тренер);
	Запрос.УстановитьПараметр("Этап", Этап);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Отказ =  Истина;
        	ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Для тренера %1 уже создан табель учета посещаемости за %2 г. спортсменов этапа подготовки %3 - %4'"), 
													ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаДетальныеЗаписи.Тренер.ФизическоеЛицо), Формат(ВыборкаДетальныеЗаписи.ПериодРегистрации, "ДФ='ММММ гггг'"), ВыборкаДетальныеЗаписи.Этап, ВыборкаДетальныеЗаписи.Регистратор));   	
		КонецЦикла;
		
		
	КонецЕсли;
		
КонецПроцедуры	
// }Рарус adilas #22016 -Учет посещаемости спортсменов 2021.11.15

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли