#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"       , ДокументСсылка);
	Запрос.УстановитьПараметр("Период"       , ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Организация"  , ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("УчебныйГод" , ДокументСсылка.УчебныйГод);
	Запрос.Текст = 	"ВЫБРАТЬ
	               	|	""ЕдиныеКалендарныеПланы"" КАК ИмяРегистра_,
	               	|	&Период КАК Период,
	               	|	&Организация КАК Организация,
	               	|	&УчебныйГод КАК УчебныйГод,
	               	|	ЕдиныйКалендарныйПланЕКП.ВидСпорта КАК ВидСпорта,
	               	|	ЕдиныйКалендарныйПланЕКП.Соревнование КАК Соревнование,
	               	|	ЕдиныйКалендарныйПланЕКП.НаименованиеОрганизаторов КАК НаименованиеОрганизаторов,
	               	|	ЕдиныйКалендарныйПланЕКП.КоличествоУчастников КАК КоличествоУчастников,
	               	|	ЕдиныйКалендарныйПланЕКП.КоличествоТренеров КАК КоличествоТренеров,
	               	|	ЕдиныйКалендарныйПланЕКП.Финансирование КАК Финансирование,
	               	|	ЕдиныйКалендарныйПланЕКП.МестоПроведения КАК МестоПроведения,
	               	|	ЕдиныйКалендарныйПланЕКП.ДатаНачала КАК ДатаНачала,
	               	|	ЕдиныйКалендарныйПланЕКП.ДатаОкончания КАК ДатаОкончания,
	               	|	ЕдиныйКалендарныйПланЕКП.НомерСоревнования КАК НомерСоревнования
	               	|ИЗ
	               	|	Документ.ЕдиныйКалендарныйПлан.ЕКП КАК ЕдиныйКалендарныйПланЕКП
	               	|ГДЕ
	               	|	ЕдиныйКалендарныйПланЕКП.Ссылка = &Ссылка";
					
	ТаблицаЕдиныеКалендарныеПланы = Запрос.Выполнить().Выгрузить();
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаЕдиныеКалендарныеПланы" , ТаблицаЕдиныеКалендарныеПланы);
	
	ДополнительныеСвойства.Вставить("Отказ", Отказ);
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ЕдиныйКалендарныйПлан.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольПередПроведением(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ЕдиныйКалендарныйПлан.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовОтменыПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ЕдиныйКалендарныйПлан.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЕКПТабДок") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ЕКПТабДок", "ЕКП", СформироватьПечатнуюФормуЗаявка(МассивОбъектов));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуЗаявка(МассивОбъектовДляПечати) Экспорт
	
	Если ТипЗнч(МассивОбъектовДляПечати) = Тип("Массив") Тогда
		
		МассивОбъектов = МассивОбъектовДляПечати;
		
	Иначе
		
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(МассивОбъектовДляПечати);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЕдиныйКалендарныйПлан.Ссылка КАК Ссылка,
	               |	ЕдиныйКалендарныйПлан.Дата КАК Дата,
	               |	ЕдиныйКалендарныйПлан.УчебныйГод КАК УчебныйГод,
	               |	ЕдиныйКалендарныйПлан.Организация.Директор КАК Директор,
	               |	ЕдиныйКалендарныйПлан.Организация.Наименование КАК Наименование,
	               |	ЕдиныйКалендарныйПлан.Организация.ПолноеНаименование КАК ПолноеНаименование
	               |ПОМЕСТИТЬ ВТ_Организация
	               |ИЗ
	               |	Документ.ЕдиныйКалендарныйПлан КАК ЕдиныйКалендарныйПлан
	               |ГДЕ
	               |	ЕдиныйКалендарныйПлан.Ссылка В(&МассивОбъектов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЕдиныйКалендарныйПланЕКП.Ссылка КАК Ссылка,
	               |	ЕдиныйКалендарныйПланЕКП.ВидСпорта КАК ВидСпорта,
	               |	ЕдиныйКалендарныйПланЕКП.Соревнование КАК Соревнование,
	               |	ЕдиныйКалендарныйПланЕКП.НаименованиеОрганизаторов КАК НаименованиеОрганизаторов,
	               |	ЕдиныйКалендарныйПланЕКП.КоличествоУчастников КАК КоличествоУчастников,
	               |	ЕдиныйКалендарныйПланЕКП.КоличествоТренеров КАК КоличествоТренеров,
	               |	ЕдиныйКалендарныйПланЕКП.Финансирование КАК Финансирование,
	               |	ЕдиныйКалендарныйПланЕКП.МестоПроведения КАК МестоПроведения,
	               |	ЕдиныйКалендарныйПланЕКП.ДатаНачала КАК ДатаНачала,
	               |	ЕдиныйКалендарныйПланЕКП.ДатаОкончания КАК ДатаОкончания,
	               |	ЕдиныйКалендарныйПланЕКП.НомерСоревнования КАК НомерСоревнования,
	               |	ЕдиныйКалендарныйПланЕКП.Город КАК Город,
	               |	РангСоревнование.Ранг КАК Ранг
	               |ПОМЕСТИТЬ ВТ_Соревнования
	               |ИЗ
	               |	Документ.ЕдиныйКалендарныйПлан.ЕКП КАК ЕдиныйКалендарныйПланЕКП
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Соревнование КАК РангСоревнование
	               |		ПО ЕдиныйКалендарныйПланЕКП.Соревнование = РангСоревнование.Ссылка
	               |ГДЕ
	               |	ЕдиныйКалендарныйПланЕКП.Ссылка В(&МассивОбъектов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_Организация.Дата КАК Дата,
	               |	ВТ_Организация.Директор КАК Директор,
	               |	ВТ_Соревнования.ВидСпорта КАК ВидСпорта,
	               |	ВТ_Соревнования.Соревнование КАК Соревнование,
	               |	ВТ_Соревнования.НаименованиеОрганизаторов КАК НаименованиеОрганизаторов,
	               |	ВТ_Соревнования.КоличествоУчастников КАК КоличествоУчастников,
	               |	ВТ_Соревнования.КоличествоТренеров КАК КоличествоТренеров,
	               |	ВТ_Соревнования.Финансирование КАК Финансирование,
	               |	ВТ_Соревнования.МестоПроведения КАК МестоПроведения,
	               |	ВТ_Соревнования.ДатаНачала КАК ДатаНачала,
	               |	ВТ_Соревнования.ДатаОкончания КАК ДатаОкончания,
	               |	ВТ_Организация.Наименование КАК Наименование,
	               |	ВТ_Организация.ПолноеНаименование КАК ПолноеНаименование,
	               |	ВТ_Организация.УчебныйГод.ДатаНачала КАК УчебныйГодДатаНачала,
	               |	ВТ_Организация.Ссылка КАК Ссылка,
	               |	ВТ_Соревнования.НомерСоревнования КАК НомерСоревнования,
	               |	ВТ_Соревнования.Город КАК Город,
	               |	ВТ_Соревнования.Ранг КАК Ранг
	               |ИЗ
	               |	ВТ_Организация КАК ВТ_Организация
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Соревнования КАК ВТ_Соревнования
	               |		ПО ВТ_Организация.Ссылка = ВТ_Соревнования.Ссылка
	               |ИТОГИ ПО
	               |	Ссылка,
	               |	ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СоревнованиеМеждународныеВозрастныеГруппы.Ссылка КАК СсылкаМеждународнаяВозрастнаяГруппа,
	               |	СоревнованиеМеждународныеВозрастныеГруппы.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа
	               |ИЗ
	               |	Справочник.Соревнование.МеждународныеВозрастныеГруппы КАК СоревнованиеМеждународныеВозрастныеГруппы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СведенияОбОтветственныхЛицахСрезПоследних.Заместитель КАК Заместитель,
	               |	СведенияОбОтветственныхЛицахСрезПоследних.Организация КАК Организация
	               |ИЗ
	               |	РегистрСведений.СведенияОбОтветственныхЛицах.СрезПоследних КАК СведенияОбОтветственныхЛицахСрезПоследних";
	
	ПутьКМакету = "Документ.ЕдиныйКалендарныйПлан.ЕКПТабДок";
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету);
	
	ТабДок = Новый ТабличныйДокумент;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоЗапросов = РезультатЗапроса.Количество();
	ВыборкаДокумент = РезультатЗапроса[КоличествоЗапросов - 3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаВозрастныеГруппы = РезультатЗапроса[КоличествоЗапросов - 2].Выбрать();
	ВыборкаЗаместитель = РезультатЗапроса[КоличествоЗапросов - 1].Выбрать();
	СтруктураПоиска = Новый Структура("СсылкаМеждународнаяВозрастнаяГруппа");
	
	СтруктураПоискаЗаместитель = Новый Структура("Организация");
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьВидСпорта = Макет.ПолучитьОбласть("ОбластьВидСпорта");
	ОбластьСтрока = Макет.ПолучитьОбласть("ОбластьСтрокаТаблицы");
	
	Пока ВыборкаДокумент.Следующий() Цикл
		ОбластьШапка.Параметры.Организация = ВыборкаДокумент.Ссылка.Организация.ПолноеНаименование;
		ОбластьШапка.Параметры.Директор = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаДокумент.Ссылка.Организация.Директор);
		ОбластьШапка.Параметры.Дата = Формат(ВыборкаДокумент.Ссылка.Дата, "ДЛФ=DD");
		ОбластьШапка.Параметры.СокрОрганизация = ВыборкаДокумент.Ссылка.Организация.Наименование;
		ОбластьШапка.Параметры.Год =  Формат(ВыборкаДокумент.Ссылка.УчебныйГод.ДатаНачала,"ДФ=гггг");
		СтруктураПоискаЗаместитель.Организация = ВыборкаДокумент.Ссылка.Организация;
		Пока ВыборкаЗаместитель.НайтиСледующий(СтруктураПоискаЗаместитель) Цикл
			Если Не ВыборкаЗаместитель.Заместитель = null тогда
				ОбластьШапка.Параметры.Заместитель =  ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаЗаместитель.Заместитель);
			КонецЕсли;		
		КонецЦикла;
				ТабДок.Вывести(ОбластьШапка);
		
		ВыборкаВидСпорта = ВыборкаДокумент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		
		Пока ВыборкаВидСпорта.Следующий() Цикл
			ОбластьВидСпорта.Параметры.ВидСпорта = ВыборкаВидСпорта.ВидСпорта;
			ТабДок.Вывести(ОбластьВидСпорта);
			
			Выборка = ВыборкаВидСпорта.Выбрать();
			
			Индекс = 1;
			
			Пока Выборка.Следующий() Цикл
				
				СтруктураПоиска.СсылкаМеждународнаяВозрастнаяГруппа = Выборка.Соревнование.Ссылка;
		
				СписокВозрастныхГрупп = Неопределено;
				Пока ВыборкаВозрастныеГруппы.НайтиСледующий(СтруктураПоиска) Цикл
					
					Если СписокВозрастныхГрупп = Неопределено Тогда
						
						СписокВозрастныхГрупп = Строка(ВыборкаВозрастныеГруппы.МеждународнаяВозрастнаяГруппа);
					Иначе
						СписокВозрастныхГрупп = СписокВозрастныхГрупп + ", " + Строка(ВыборкаВозрастныеГруппы.МеждународнаяВозрастнаяГруппа);
						
					КонецЕсли;
						
				КонецЦикла;
				
				ОбластьСтрока.Параметры.Заполнить(Выборка);
				ОбластьСтрока.Параметры.КатегорияУчастников = СписокВозрастныхГрупп;
				Если Выборка.ДатаНачала = Выборка.ДатаОкончания Тогда
					ДатаСоревнования = Формат(Выборка.ДатаНачала,"ДФ='дд ММММ'; ДЛФ=DD");
				ИначеЕсли НачалоМесяца(Выборка.ДатаНачала)<НачалоМесяца(Выборка.ДатаОкончания) Тогда
					ДатаСоревнования = Формат(Выборка.ДатаНачала,"ДФ='дд ММММ'; ДЛФ=DD")+"-"+Формат(Выборка.ДатаОкончания,"ДФ='дд ММММ'; ДЛФ=DD");
				Иначе
					ДатаСоревнования = Формат(Выборка.ДатаНачала,"ДФ=дд")+"-"+Формат(Выборка.ДатаОкончания,"ДФ='дд ММММ'; ДЛФ=DD");
				КонецЕсли;
				
				Если Выборка.КоличествоТренеров = 0 Тогда
					ОбластьСтрока.Параметры.ОбщееКоличествоУчастников = Строка(Выборка.КоличествоУчастников);
				Иначе
					ОбластьСтрока.Параметры.ОбщееКоличествоУчастников = Строка(Выборка.КоличествоУчастников) + "/" + Строка(Выборка.КоличествоТренеров);
				КонецЕсли;
				
				ОбластьСтрока.Параметры.ДатаСоревнования = ДатаСоревнования;  
				ОбластьСтрока.Параметры.НомерСтроки  = Индекс;
				Индекс = Индекс + 1;
				ТабДок.Вывести(ОбластьСтрока);
			КонецЦикла;	
		
		КонецЦикла;
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;	
	
	ТабДок.КлючПараметровПечати	= ПутьКМакету;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб 			= Истина;
	
	Возврат ТабДок;
	
	
КонецФункции	

#КонецОбласти