#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// {Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	// {Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	Если РольДоступна("ПолныеПрава") Тогда
		 Элементы.Ответственный.Доступность = Истина;
	Иначе 	 
		 Элементы.Ответственный.Доступность = Ложь;
	КонецЕсли;
	// }Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	// }Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Ответственный = ПолучитьТекущийПользователь();
		УчетСпортсменовСервер.ЗаполнитьНастройкиПоУмолчанию(Объект);
		
		УправлениеУчебнымГодомСсылка = УчетСпортсменовСервер.ПолучитьДокументТекущегоУчебногоГода();
	    Объект.УчебныйГод = УправлениеУчебнымГодомСсылка.УчебныйГод;
		
	Иначе
		ТекущийДокументОснование = Объект.ДокументОснование;
		УчетСпортсменовСервер.ЗаполнитьДокументУчебныйГод(ЭтотОбъект, Объект.УчебныйГод, Объект.Организация);
	КонецЕсли;
	
	// {Рарус adilas #13392 -Номер документа 2021.02.12
	Если Пользователи.РолиДоступны("АдминистраторСистемы, ПолныеПрава") Тогда
		Элементы.Номер.Доступность = Истина;
		Элементы.Номер.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	// }Рарус adilas #13392 -Номер документа 2021.02.12
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовШапки

&НаКлиенте
Процедура УчебныйГодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	УчетСпортсменовКлиент.УчебныйГодНачалоВыбора(ЭтотОбъект,СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодОткрытие(Элемент, СтандартнаяОбработка)
	УчетСпортсменовКлиент.УчебныйГодОткрытие(УправлениеУчебнымГодомСсылка, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодСоздание(Элемент, СтандартнаяОбработка)
	УчетСпортсменовКлиент.УчебныйГодСоздание(ЭтаФорма, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьДокументПродолжить", ЭтотОбъект);
		
	ПоказатьВопрос(Оповещение,
		"Документ будет заполнен по данным документа Соревнование.
		|Продолжить?",
		РежимДиалогаВопрос.ДаНет,
		0);
		
	Возврат;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекущийДокументОснование = Объект.ДокументОснование;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовТабличнойЧасти

&НаКлиенте
Процедура СпортсменыПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Если ТекДанные.КлючСтроки <> ТекущийКлюч Тогда
		ТекущийКлюч = ТекДанные.КлючСтроки;
		УстановитьОтборНаТаблицуХарактеристик();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпортсменыПередУдалением(Элемент, Отказ)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСтрокиХарактеристикНаСервере(ТекДанные.КлючСтроки);	
	
КонецПроцедуры

&НаКлиенте
Процедура СпортсменыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не выбран документ 'Соревнование'",,,"Объект.ДокументОснование");
		Отказ = Истина;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ВидСпорта) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не выбран вид спорта",,,"Объект.ВидСпорта");
		Отказ = Истина;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура СпортсменыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Если НоваяСтрока И ТекДанные.КлючСтроки = 0 Тогда
		
		ТекДанные.КлючСтроки = ПолучитьТекущийКлюч();
		ДобавитьСтрокиХарактеристикНаСервере(ТекДанные.КлючСтроки);
		ТекущийКлюч = ТекДанные.КлючСтроки;
		УстановитьОтборНаТаблицуХарактеристик();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьТекущийПользователь()
	Возврат Пользователи.ТекущийПользователь();
КонецФункции

&НаКлиенте
Процедура ЗаполнитьДокументПродолжить(Результат, ДопПараметры) Экспорт
	
	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
		Объект.ДокументОснование = ТекущийДокументОснование;
		Возврат;
	КонецЕсли;	
	
	ЗаполнитьДокумент(Объект.ДокументОснование);
	
	УстановитьОтборНаТаблицуХарактеристик();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокумент(Документ)
	
	Объект.УчебныйГод	= Документ.УчебныйГод;
	Объект.Организация	 = Документ.Организация;
	Объект.ВидСпорта	 = Документ.ВидСпорта;
	
	ТЗСпортсмены = Документ.СоставУчастников.Выгрузить();
	ТЗСпортсмены.Сортировать("Спортсмен");
	ТЗСпортсмены.Колонки.Добавить("КлючСтроки");
	
	Объект.Достижения.Очистить();
		
	ТЗХарактеристики = УчетСпортсменовСервер.ПолучитьТаблицуХарактеристикВидаСпорта(Объект.ВидСпорта);
	
	Для Сч = 1 По ТЗСпортсмены.Количество() Цикл
		ТЗСпортсмены[Сч - 1].КлючСтроки = Сч;
		
		Для СчХ = 1 По ТЗХарактеристики.Количество() Цикл
			НоваяСтрока = Объект.Достижения.Добавить();
			НоваяСтрока.Характеристика = ТЗХарактеристики[СчХ - 1].Характеристика;
			НоваяСтрока.КлючСтроки = Сч;
			НоваяСтрока.НомерСтрокиВнутренний = СчХ;
		КонецЦикла;	
	КонецЦикла;
	
	Объект.Спортсмены.Загрузить(ТЗСпортсмены);
	
КонецПроцедуры	

&НаКлиенте
Процедура УчебныйГодСозданиеЗавершение(Результат, ДопПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
	   Объект.УчебныйГод = Результат.УчебныйГодСсылка;
	   УправлениеУчебнымГодомСсылка = Результат.УчебныйГодОбъект;
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура УчебныйГодНачалоВыбораЗавершение(Результат, ДопПараметры) Экспорт
	
	УправлениеУчебнымГодомСсылка = Результат;
    ПолучитьУчебныйГодСсылка();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьУчебныйГодСсылка()
	Объект.УчебныйГод = УправлениеУчебнымГодомСсылка.УчебныйГод;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборНаТаблицуХарактеристик()
	Элементы.Достижения.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСтроки", ТекущийКлюч);		
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокиХарактеристикНаСервере(КлючСтроки)
	
	МассивХарактеристик = Объект.Достижения.НайтиСтроки(Новый Структура("КлючСтроки", КлючСтроки));
	Для Каждого СтрокаХарактеристика Из МассивХарактеристик Цикл
		Объект.Достижения.Удалить(СтрокаХарактеристика);
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокиХарактеристикНаСервере(Ключ)
	
	ТЗХарактеристики = УчетСпортсменовСервер.ПолучитьТаблицуХарактеристикВидаСпорта(Объект.ВидСпорта);
	Для СчХ = 1 По ТЗХарактеристики.Количество() Цикл
		НоваяСтрока = Объект.Достижения.Добавить();
		НоваяСтрока.Характеристика = ТЗХарактеристики[СчХ - 1].Характеристика;
		НоваяСтрока.КлючСтроки = Ключ;
		НоваяСтрока.НомерСтрокиВнутренний = СчХ;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекущийКлюч()
	
	ТЗКлючи = Объект.Достижения.Выгрузить(, "КлючСтроки");
	Если ТЗКлючи.Количество() = 0 Тогда
		Ключ = 1;
	Иначе
		ТЗКлючи.Сортировать("КлючСтроки Убыв");
	    Ключ = ТЗКлючи[0].КлючСтроки + 1;
	КонецЕсли;
	
	Возврат Ключ;
	
КонецФункции

// {Рарус dotere #20699 -Повторное заполнение характеристик 2021.09.30
&НаКлиенте
Процедура ПодобратьХарактеристику(Команда)
	Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОДостижении", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Это действие очистит все данные о достижениях спортсменов.'"), РежимДиалогаВопрос.ДаНет,0);
КонецПроцедуры

&НаКлиенте

Процедура ПослеОтветаНаВопросОДостижении(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
	Объект.Достижения.Очистить();
	Для каждого Споротсмен из Объект.Спортсмены Цикл
		ДобавитьСтрокиХарактеристикНаСервере(Споротсмен.КлючСтроки);
	КонецЦикла
	КонецЕсли;

КонецПроцедуры

// }Рарус dotere #20699 -Повторное заполнение характеристик 2021.09.30

#КонецОбласти
