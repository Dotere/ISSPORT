#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"        	, ДокументСсылка);
	// {Рарус adilas #10048 -Реквизиты дат для документов 2020.10.09
	Запрос.УстановитьПараметр("Период"      	, ДокументСсылка.ДатаЗачисления);
	// }Рарус adilas #10048 -Реквизиты дат для документов 2020.10.09
	Запрос.УстановитьПараметр("УчебныйГод"      , ДокументСсылка.УчебныйГод);
	Запрос.УстановитьПараметр("Организация"   	, ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("Группа"        	, ДокументСсылка.Группа);
	Запрос.УстановитьПараметр("ТренерСборной" 	, ДокументСсылка.ТренерСборной);
	Запрос.УстановитьПараметр("ВидСпорта"     	, ДокументСсылка.ВидСпорта);
	Запрос.Текст =  "ВЫБРАТЬ
	                |	""СоставКоманды"" КАК ИмяРегистра_,
	                |	&Период КАК Период,
	                |	&УчебныйГод КАК УчебныйГод,
	                |	&Организация КАК Организация,
	                |	&Группа КАК Группа,
	                |	&ТренерСборной КАК ТренерСборной,
	                |	&ВидСпорта КАК ВидСпорта,
	                |	Спортсмены.Спортсмен КАК Спортсмен
	                |ИЗ
	                |	Документ.ФормированиеГруппы.Спортсмены КАК Спортсмены
	                |ГДЕ
	                |	Спортсмены.Ссылка = &Ссылка
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	""ЛичныйТренерСпортсмена"" КАК ИмяРегистра_,
	                |	&Период КАК Период,
	                |	&УчебныйГод КАК УчебныйГод,
	                |	&Организация КАК Организация,
	                |	&Группа КАК Группа,
	                |	&ВидСпорта КАК ВидСпорта,
	                |	ФормированиеГруппыСписокЛичныхТренеров.Тренер КАК Тренер,
	                |	ФормированиеГруппыСпортсмены.Спортсмен КАК Спортсмен,
	                |	ФормированиеГруппыСписокЛичныхТренеров.ДатаНачалаРаботыСТренером КАК ДатаНачалаРаботыСТренером,
	                |	ФормированиеГруппыСписокЛичныхТренеров.ДатаОкончанияРаботыСТренером КАК ДатаОкончанияРаботыСТренером
	                |ИЗ
	                |	Документ.ФормированиеГруппы.Спортсмены КАК ФормированиеГруппыСпортсмены
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ФормированиеГруппы.СписокЛичныхТренеров КАК ФормированиеГруппыСписокЛичныхТренеров
	                |		ПО ФормированиеГруппыСпортсмены.Ссылка = ФормированиеГруппыСписокЛичныхТренеров.Ссылка
	                |			И ФормированиеГруппыСпортсмены.КлючСтроки = ФормированиеГруппыСписокЛичныхТренеров.КлючСтроки
	                |ГДЕ
	                |	ФормированиеГруппыСпортсмены.Ссылка = &Ссылка";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	ТаблицаСоставГруппы           = Пакет[0].Выгрузить();
	ТаблицаЛичныйТренерСпортсмена  = Пакет[1].Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаСоставГруппы"           , ТаблицаСоставГруппы);
	ТаблицыДляДвижений.Вставить("ТаблицаЛичныйТренерСпортсмена"  , ТаблицаЛичныйТренерСпортсмена);
	
    ДополнительныеСвойства.Вставить("Отказ", Отказ);
	
КонецПроцедуры	

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ФормированиеГруппы.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольПередПроведением(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ФормированиеГруппы.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовОтменыПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ФормированиеГруппы.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

// + Адильбеков А.Б. 18.05.20 IN-5591 {

#Область ПечатьWord
// Подготавливает данные объекта к выводу на печать.
// 
// Параметры:
//  Согласия - Массив - ссылки на документы, для которых запрашиваются данные для печати;
//  МассивИменМакетов - Массив - имена макетов (Строка), в которые подставляются данные для печати.
//
// Возвращаемое значение:
//  Соответствие - коллекция ссылок на объекты и их данные:
//   * Ключ - ЛюбаяСсылка - ссылка на объект информационной базы;
//   * Значение - Структура - макет и данные:
//    ** Ключ - Строка - имя макета,
//    ** Значение - Структура - данные объекта.
//
Функция ПолучитьДанныеПечати(Знач МассивДокументов, Знач МассивИменМакетов) Экспорт
	
	// Создание функции подготовки данных для печати.
	ДанныеПоВсемОбъектам = Новый Соответствие;
	
	Для Каждого ОбъектСсылка Из МассивДокументов Цикл
		ДанныеОбъектаПоМакетам = Новый Соответствие;
		Для Каждого ИмяМакета Из МассивИменМакетов Цикл
			ДанныеОбъектаПоМакетам.Вставить(ИмяМакета, ПолучитьДанныеОбъекта(ОбъектСсылка,ИмяМакета));
		КонецЦикла;
		ДанныеПоВсемОбъектам.Вставить(ОбъектСсылка, ДанныеОбъектаПоМакетам);
	КонецЦикла;
	
	ОписаниеОбластей = Новый Соответствие;
	ДвоичныеДанныеМакетов = Новый Соответствие;
	ТипыМакетов = Новый Соответствие;
	                                                             
	Для Каждого ИмяМакета Из МассивИменМакетов Цикл
		Если ИмяМакета = "ПФ_DOC_Приказ" Тогда
			ДвоичныеДанныеМакетов.Вставить(ИмяМакета, Документы.ФормированиеГруппы.ПолучитьМакет("ПФ_DOC_Приказ"));
			ТипыМакетов.Вставить(ИмяМакета, "DOC");
		КонецЕсли;
		ОписаниеОбластей.Вставить(ИмяМакета, ПолучитьОписаниеОбластейМакетаОфисногоДокумента(ИмяМакета));
	КонецЦикла;

	
	Возврат Новый Структура("Данные, Макеты",
											ДанныеПоВсемОбъектам,
											Новый Структура("ОписаниеОбластей, ТипыМакетов, ДвоичныеДанныеМакетов",
											ОписаниеОбластей,
											ТипыМакетов,
											ДвоичныеДанныеМакетов));


КонецФункции

Функция ПолучитьОписаниеОбластейМакетаОфисногоДокумента(ИмяМакета)
	
	ОписаниеОбластей = Новый Структура;
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ОбластьШапка"        				       , "Общая");            // Описание областей в вордовском документе
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ОбластьШапкаТаблицы"        			   , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ОбластьСтрокаТаблицыСпортсмены"       	   , "СтрокаТаблицы"); 
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ОбластьПодвал"     				       , "Общая");
	
	Возврат ОписаниеОбластей;
	
КонецФункции

Функция ПолучитьДанныеОбъекта(ОбъектыПечати, ИмяМакета) Экспорт
	Если ИмяМакета = "ПФ_DOC_Приказ" Тогда
		Возврат ПолучитьДанныеОбъектаПриказ(ОбъектыПечати);
	КонецЕсли;
КонецФункции

Функция ПолучитьДанныеОбъектаПриказ(ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ФормированиеГруппы.Группа КАК Группа,
		|	ФормированиеГруппы.УчебныйГод КАК УчебныйГод,
		|	ФормированиеГруппы.ТренерСборной КАК ТренерСборной,
		|	ФормированиеГруппы.Дата КАК Дата,
		|	ФормированиеГруппы.Ссылка КАК Ссылка,
		|	ФормированиеГруппы.Организация КАК Организация,
		|	ФормированиеГруппы.ВидСпорта КАК ВидСпорта
		|ПОМЕСТИТЬ ВТ_Данные
		|ИЗ
		|	Документ.ФормированиеГруппы КАК ФормированиеГруппы
		|ГДЕ
		|	ФормированиеГруппы.Ссылка В(&СписокДокументов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Данные.Группа КАК Группа,
		|	ВТ_Данные.ТренерСборной КАК ТренерСборной,
		|	ВТ_Данные.Ссылка КАК Ссылка,
		|	ФормированиеГруппыСпортсмены.Спортсмен КАК Спортсмен,
		|	ВТ_Данные.ВидСпорта КАК ВидСпорта,
		|	ФизическиеЛица.ГодРождения КАК СпортсменФизическоеЛицоГодРождения,
		|	ВТ_Данные.Организация КАК Организация,
		|	ВТ_Данные.Дата КАК Дата
		|ПОМЕСТИТЬ ВТ_Итог
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ФормированиеГруппы.Спортсмены КАК ФормированиеГруппыСпортсмены
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|			ПО ФормированиеГруппыСпортсмены.Спортсмен.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|		ПО ВТ_Данные.Ссылка = ФормированиеГруппыСпортсмены.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Итог.Группа КАК Группа,
		|	ВТ_Итог.ТренерСборной КАК ТренерСборной,
		|	ВТ_Итог.Ссылка КАК Ссылка,
		|	ВТ_Итог.Спортсмен КАК Спортсмен,
		|	ВТ_Итог.ВидСпорта КАК ВидСпорта,
		|	ВТ_Итог.СпортсменФизическоеЛицоГодРождения КАК ГодРождения,
		|	ВТ_Итог.Организация КАК Организация,
		|	ВТ_Итог.Дата КАК Дата
		|ИЗ
		|	ВТ_Итог КАК ВТ_Итог
		|ИТОГИ ПО
		|	Ссылка";
	
	Запрос.УстановитьПараметр("СписокДокументов", ОбъектыПечати);
	
	МассивДокументов = Новый Массив;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСсылка.Следующий() Цикл
			// Вставить обработку выборки ВыборкаСсылка
		    СтруктураДокумента = Новый Структура;
			
			СтруктураШапка = Новый Структура;
		
			МассивСтрокСпортсмены = Новый Массив;
			НомерСтрокиСпр = 1;
			
			ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				// Вставить обработку выборки ВыборкаДетальныеЗаписи
				Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Спортсмен) Тогда
					// Область шапка таблицы
					СтруктураШапкаТаблицы = Новый Структура;
					СтруктураШапкаТаблицы.Вставить("Организация", ВыборкаДетальныеЗаписи.Организация);
					СтруктураШапкаТаблицы.Вставить("ВидСпорта", ВыборкаДетальныеЗаписи.ВидСпорта);
					СтруктураШапкаТаблицы.Вставить("Группа", ВыборкаДетальныеЗаписи.Группа);
			 		СтруктураШапкаТаблицы.Вставить("ТренерСборной", ВыборкаДетальныеЗаписи.ТренерСборной);
					// Таблица спортсмены
					СтруктураСтрокаСпортсмены = Новый Структура("НомерСтроки,Спортсмен,ГодРождения,Группа");				
					ЗаполнитьЗначенияСвойств(СтруктураСтрокаСпортсмены, ВыборкаДетальныеЗаписи);
					СтруктураСтрокаСпортсмены.НомерСтроки 	= НомерСтрокиСпр;
					НомерСтрокиСпр 							= НомерСтрокиСпр + 1;
					// Фамилия и Имя
					ЧастиИмени 								= СтрРазделить(ВыборкаДетальныеЗаписи.Спортсмен, " ", Ложь);
					Фамилия 								= ЧастиИмени[0];
					Имя 									= ЧастиИмени[1];
					СтруктураСтрокаСпортсмены.Спортсмен 	= Фамилия + " " + Имя;
					МассивСтрокСпортсмены.Добавить(СтруктураСтрокаСпортсмены);	
				Иначе
					ОбщегоНазначения.СообщитьПользователю("Состав группы пустой!");
				КонецЕсли;	
			КонецЦикла;
			
			// Подвал
			СтруктураПодвал = Новый Структура;
			СтруктураПодвал.Вставить("Комментарий", ВыборкаСсылка.Ссылка.Комментарий);
			СтруктураПодвал.Вставить("Ответственный", ВыборкаСсылка.Ссылка.Ответственный);
			
			// Заполение Областей документа Ворд
			СтруктураДокумента.Вставить("ОбластьШапка", СтруктураШапка);
			СтруктураДокумента.Вставить("ОбластьШапкаТаблицы", СтруктураШапкаТаблицы);
			СтруктураДокумента.Вставить("ОбластьСтрокаТаблицыСпортсмены", МассивСтрокСпортсмены);
			СтруктураДокумента.Вставить("ОбластьПодвал", СтруктураПодвал);
					
			МассивДокументов.Добавить(СтруктураДокумента);
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивДокументов
	
КонецФункции	

#КонецОбласти

#Область Печать
// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Приказ") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
																	"ПФ_MXL_Приказ", // Идентфикатор
																	"Приказ о формировании группы", // Название (представление) 
								  СформироватьПечатнуюФормуПриказ(МассивОбъектов)); // Функция которая получает макет и его заполняет
		// {Рарус dotere #17261 -Заполнение печатной формы 2021.07.30
	  
		ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм,"ПриказВключениеТабДок") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
																	"ПриказВключениеТабДок", // Идентфикатор
																	"Включение в группу", // Название (представление) 
								  СформироватьПечатнуюФормуПриказВключениеТабДок(МассивОбъектов)); // Функция которая получает макет и его заполняет
	КонецЕсли;
	     // }Рарус dotere #17261 -Заполнение печатной формы 2021.07.30
КонецПроцедуры

Функция СформироватьПечатнуюФормуПриказ(МассивОбъектовДляПечати) Экспорт  // Функция которая получает макет и заполняет его 
	
	Если ТипЗнч(МассивОбъектовДляПечати) = Тип("Массив") Тогда
		
		МассивОбъектов = МассивОбъектовДляПечати;
		
	Иначе
		
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(МассивОбъектовДляПечати);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ФормированиеГруппы.Группа КАК Группа,
		|	ФормированиеГруппы.УчебныйГод КАК УчебныйГод,
		|	ФормированиеГруппы.ТренерСборной КАК ТренерСборной,
		|	ФормированиеГруппы.Дата КАК Дата,
		|	ФормированиеГруппы.Ссылка КАК Ссылка,
		|	ФормированиеГруппы.Организация КАК Организация,
		|	ФормированиеГруппы.ВидСпорта КАК ВидСпорта
		|ПОМЕСТИТЬ ВТ_Данные
		|ИЗ
		|	Документ.ФормированиеГруппы КАК ФормированиеГруппы
		|ГДЕ
		|	ФормированиеГруппы.Ссылка В(&МассивОбъектов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Данные.Группа КАК Группа,
		|	ВТ_Данные.ТренерСборной КАК ТренерСборной,
		|	ВТ_Данные.Ссылка КАК Ссылка,
		|	ФормированиеГруппыСпортсмены.Спортсмен КАК Спортсмен,
		|	ВТ_Данные.ВидСпорта КАК ВидСпорта,
		|	ФизическиеЛица.ГодРождения КАК ГодРождения,
		|	ВТ_Данные.Организация КАК Организация,
		|	ВТ_Данные.Дата КАК Дата
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ФормированиеГруппы.Спортсмены КАК ФормированиеГруппыСпортсмены
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|			ПО ФормированиеГруппыСпортсмены.Спортсмен.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|		ПО ВТ_Данные.Ссылка = ФормированиеГруппыСпортсмены.Ссылка
		|ИТОГИ ПО
		|	Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПутьКМакету = "Документ.ФормированиеГруппы.ПФ_MXL_Приказ";
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету); // Получаем макет
	
	ТабДок = Новый ТабличныйДокумент;  // Создаем новый ТабДок
	
	РезультатЗапроса = Запрос.Выполнить();

	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСсылка.Следующий() Цикл
			// Вставить обработку выборки ВыборкаСсылка
			ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка"); // Получаем области из макета
			ТабДок.Вывести(ОбластьШапка);
			
			ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ОбластьШапкаТаблицы");
			ОбластьШапкаТаблицы.Параметры.ТренерСборной 				= ВыборкаСсылка.Ссылка.ТренерСборной;
			ОбластьШапкаТаблицы.Параметры.Организация 					= ВыборкаСсылка.Ссылка.Организация;
			ОбластьШапкаТаблицы.Параметры.ВидСпорта 					= ВыборкаСсылка.Ссылка.ВидСпорта;
			ТабДок.Вывести(ОбластьШапкаТаблицы);
			
			НомерСтрокиСпр = 1;
			
			ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать(); 
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Спортсмен) Тогда
					// Таблица спортсмены
					ОбластьСтрокаТаблицыСпортсмены 							= Макет.ПолучитьОбласть("ОбластьСтрокаТаблицыСпортсмены");
					ОбластьСтрокаТаблицыСпортсмены.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
					ОбластьСтрокаТаблицыСпортсмены.Параметры.НомерСтроки 	= НомерСтрокиСпр;
					НомерСтрокиСпр 											= НомерСтрокиСпр + 1;
					// Фамилия и Имя
					ЧастиИмени 												= СтрРазделить(ВыборкаДетальныеЗаписи.Спортсмен, " ", Ложь);
					Фамилия 												= ЧастиИмени[0];
					Имя 													= ЧастиИмени[1];
					ОбластьСтрокаТаблицыСпортсмены.Параметры.Спортсмен 		= Фамилия + " " + Имя;
					ТабДок.Вывести(ОбластьСтрокаТаблицыСпортсмены);
				Иначе
					ОбщегоНазначения.СообщитьПользователю("Состав группы пустой!");
				КонецЕсли;
			КонецЦикла;
		
			// Подвал
			ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал");
			ТабДок.Вывести(ОбластьПодвал);
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЦикла;
	КонецЕсли;
	
	ТабДок.КлючПараметровПечати	= ПутьКМакету;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб 			= Истина;
	
	Возврат ТабДок;
	
КонецФункции	


// {Рарус dotere #17261 -Заполнение печатной формы 2021.07.30
Функция СформироватьПечатнуюФормуПриказВключениеТабДок(МассивОбъектовДляПечати) Экспорт
	   Если ТипЗнч(МассивОбъектовДляПечати) = Тип("Массив") Тогда
		МассивОбъектов = МассивОбъектовДляПечати;	
	Иначе	
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(МассивОбъектовДляПечати);	
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ФормированиеГруппы.Организация.ПолноеНаименование КАК ОрганизацияПолноеНаименование,
		|	ФормированиеГруппы.Организация.Директор КАК ОрганизацияДиректор,
		|	ФормированиеГруппы.Ссылка КАК Ссылка,
		|	ФормированиеГруппы.ТренерСборной КАК ТренерСборной,
		|	ФормированиеГруппы.ДатаЗачисления КАК ДатаЗачисления,
		|	ФормированиеГруппы.Группа КАК Группа
		|ПОМЕСТИТЬ ВТ_ДокументВключенияВГруппу
		|ИЗ
		|	Документ.ФормированиеГруппы КАК ФормированиеГруппы
		|ГДЕ
		|	ФормированиеГруппы.Ссылка В(&МассивОбъектов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ФормированиеГруппыСпортсмены.Спортсмен КАК Спортсмен,
		|	ВТ_ДокументВключенияВГруппу.ОрганизацияПолноеНаименование КАК ОрганизацияПолноеНаименование,
		|	ВТ_ДокументВключенияВГруппу.ОрганизацияДиректор КАК ОрганизацияДиректор,
		|	ВТ_ДокументВключенияВГруппу.Ссылка КАК Ссылка,
		|	ВТ_ДокументВключенияВГруппу.ТренерСборной КАК ТренерСборной,
		|	ВТ_ДокументВключенияВГруппу.ДатаЗачисления КАК ДатаЗачисления,
		|	ВТ_ДокументВключенияВГруппу.Группа КАК Группа
		|ПОМЕСТИТЬ ВТ_Спортсмены
		|ИЗ
		|	ВТ_ДокументВключенияВГруппу КАК ВТ_ДокументВключенияВГруппу
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ФормированиеГруппы.Спортсмены КАК ФормированиеГруппыСпортсмены
		|		ПО ВТ_ДокументВключенияВГруппу.Ссылка = ФормированиеГруппыСпортсмены.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.Организация КАК Организация,
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.Спортсмен КАК Спортсмен,
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.ВидСпорта КАК ВидСпорта,
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.Разряд КАК Разряд
		|ПОМЕСТИТЬ ВТ_Разряды
		|ИЗ
		|	РегистрСведений.ПодтвержденныеРазрядыСпортсменов.СрезПоследних КАК ПодтвержденныеРазрядыСпортсменовСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Разряды.Разряд КАК Разряд,
		|	ВТ_Спортсмены.Спортсмен КАК Спортсмен,
		|	ВТ_Спортсмены.ОрганизацияПолноеНаименование КАК ОрганизацияПолноеНаименование,
		|	ВТ_Спортсмены.ОрганизацияДиректор КАК ОрганизацияДиректор,
		|	ВТ_Спортсмены.Ссылка КАК Ссылка,
		|	ВТ_Спортсмены.ТренерСборной КАК ТренерСборной,
		|	ВТ_Спортсмены.ДатаЗачисления КАК ДатаЗачисления,
		|	ВТ_Спортсмены.Группа КАК Группа
		|ИЗ
		|	ВТ_Спортсмены КАК ВТ_Спортсмены
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Разряды КАК ВТ_Разряды
		|		ПО ВТ_Спортсмены.Спортсмен = ВТ_Разряды.Спортсмен
		|ИТОГИ ПО
		|	Ссылка";
	
		
	ПутьКМакету = "Документ.ФормированиеГруппы.ПриказВключениеТабДок";
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьСпортсмен 	= Макет.ПолучитьОбласть("ОбластьСпортсмен");
	ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал");
	
	ТабДок = Новый ТабличныйДокумент;
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСсылка.Следующий() Цикл
			
			ОбластьШапка.Параметры.ПолноеНаименование 	= ВРег(ВыборкаСсылка.Ссылка.Организация.ПолноеНаименование);
			ОбластьШапка.Параметры.ДатаДокумента = Формат(ВыборкаСсылка.Ссылка.ДатаДокумента, "ДЛФ=D");
			ОбластьШапка.Параметры.ДатаЗачисления = Формат(ВыборкаСсылка.Ссылка.ДатаЗачисления,  "ДФ='dd MMMM yyyy'; ДЛФ=DD");
			ОбластьШапка.Параметры.ВидСпорта = ВыборкаСсылка.Ссылка.ВидСпорта;
			ОбластьШапка.Параметры.НомерПриказа = ВыборкаСсылка.Ссылка.НомерДокумента;
			ОбластьШапка.Параметры.Группа = ВыборкаСсылка.Ссылка.Группа;
			ТабДок.Вывести(ОбластьШапка);
			ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
			НомерСтрокиСпр = 1;			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Спортсмен) Тогда
							
							ОбластьСпортсмен.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
							ОбластьСпортсмен.Параметры.Номер 	= Строка(НомерСтрокиСпр) + ".";
							ЧастиИмени 												= СтрРазделить(ВыборкаДетальныеЗаписи.Спортсмен, " ", Ложь);
							Фамилия 												= ЧастиИмени[0];
							Имя 													= ЧастиИмени[1];
							ОбластьСпортсмен.Параметры.Спортсмен 		= Фамилия + " " + Имя;
							ОбластьСпортсмен.Параметры.ГодРождения = ВыборкаДетальныеЗаписи.Спортсмен.ФизическоеЛицо.ГодРождения;
							Если Не ВыборкаДетальныеЗаписи.Разряд = null Тогда
								ОбластьСпортсмен.Параметры.Разряд = ВыборкаДетальныеЗаписи.Разряд;
							Иначе
							    ОбластьСпортсмен.Параметры.Разряд = "б/р";
							КонецЕсли;

							НомерСтрокиСпр = НомерСтрокиСпр + 1;
							
							ТабДок.Вывести(ОбластьСпортсмен);
				КонецЕсли;

			КонецЦикла;
			ОбластьПодвал.Параметры.Тренер = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаСсылка.Ссылка.ТренерСборной.ФизическоеЛицо);
			ОбластьПодвал.Параметры.Директор = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаСсылка.Ссылка.Организация.Директор);
			ТабДок.Вывести(ОбластьПодвал);
		КонецЦикла
	КонецЕсли;
	
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();	
	
	ТабДок.КлючПараметровПечати	= ПутьКМакету;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб 			= Истина;
	
	Возврат ТабДок;


КонецФункции
// }Рарус dotere #17261 -Заполнение печатной формы 2021.07.30
#КонецОбласти

#КонецОбласти

// - Адильбеков А.Б. 18.05.20 IN-5591 }

#Область ПрограммныйИнтерфейс

// {Рарус adilas #11479 -Механизм шаблонов 2020.12.23

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

// }Рарус adilas #11479 -Механизм шаблонов 2020.12.23

#КонецОбласти