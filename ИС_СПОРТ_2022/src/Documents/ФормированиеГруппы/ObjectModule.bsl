// {Рарус adilas #1.0.0.2 -Исправления по SonarQube 2021.04.23
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
    КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	УчетСпортсменовСервер.ЗаполнитьРеквизит(ЭтотОбъект, "Ответственный", ПараметрыСеанса.ТекущийПользователь);	
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	                                                                                   
	УчетСпортсменовСервер.ПроверитьСоставГруппыНаДубли(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат
	КонецЕсли;	
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	ПроведениеСервер.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ, "СоставГруппы");
	ПроведениеСервер.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ, "ЛичныйТренерСпортсмена");

	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовОтменыПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеПоИсключеннымИзКомандыСпортсменам() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяКоманда" , Группа);
	Запрос.УстановитьПараметр("Ссылка"         , Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ФормированиеГруппыСпортсмены.НомерСтроки КАК НомерСтроки,
	               |	ФормированиеГруппыСпортсмены.Спортсмен КАК Спортсмен
	               |ПОМЕСТИТЬ Вт_Группа
	               |ИЗ
	               |	Документ.ФормированиеГруппы.Спортсмены КАК ФормированиеГруппыСпортсмены
	               |ГДЕ
	               |	ФормированиеГруппыСпортсмены.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СоставГруппы.Спортсмен КАК Спортсмен,
	               |	СоставГруппы.ДатаИсключенияИзГруппы КАК ДатаИсключенияИзГруппы
	               |ПОМЕСТИТЬ ВТ_СписокИсключенных
	               |ИЗ
	               |	РегистрСведений.СоставГруппы.СрезПоследних(
	               |			,
	               |			ДатаИсключенияИзГруппы <> ДАТАВРЕМЯ(1, 1, 1)
	               |				И Группа = &ТекущаяГруппа) КАК СоставГруппы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Вт_Группа.НомерСтроки КАК НомерСтроки,
	               |	Вт_Группа.Спортсмен КАК Спортсмен,
	               |	ВТ_СписокИсключенных.ДатаИсключенияИзГруппы КАК ДатаИсключенияИзГруппы
	               |ИЗ
	               |	Вт_Группа КАК Вт_Группа
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокИсключенных КАК ВТ_СписокИсключенных
	               |		ПО Вт_Группа.Спортсмен = ВТ_СписокИсключенных.Спортсмен
	               |ГДЕ
	               |	НЕ ВТ_СписокИсключенных.Спортсмен ЕСТЬ NULL";
	Возврат Запрос.Выполнить().Выгрузить()
	
КонецФункции	

Процедура ЗаполнитьПоЗаявлениямНаУчебныйГод() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("УчебныйГод",    УчебныйГод);
	Запрос.УстановитьПараметр("Тренер",        ТренерСборной);
	Запрос.УстановитьПараметр("ВидСпорта",     ВидСпорта);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Заявления.Спортсмен КАК Спортсмен
	               |ИЗ
	               |	РегистрСведений.СоставУчащихсяСпортивногоУчреждения.СрезПоследних(
	               |			&ДатаДокумента,
	               |			Организация = &Организация
	               |				И Тренер = &Тренер
	               |				И ВидСпорта = &ВидСпорта) КАК Заявления";
	Результат = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаРезультат Из Результат Цикл
		строкаСпортсмен = Спортсмены.Добавить();
		ЗаполнитьЗначенияСвойств(строкаСпортсмен,строкаРезультат);
		строкаСпортсмен.КлючСтроки = СокрЛП(строкаСпортсмен.Спортсмен.УникальныйИдентификатор());
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли