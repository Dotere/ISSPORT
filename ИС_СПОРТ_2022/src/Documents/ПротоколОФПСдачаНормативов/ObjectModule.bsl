// {Рарус adilas #1.0.0.2 -Исправления по SonarQube 2021.04.23
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
    КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);	
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	УчетСпортсменовСервер.ЗаполнитьРеквизит(ЭтотОбъект, "Ответственный", ПараметрыСеанса.ТекущийПользователь);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	ПроведениеСервер.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ, "ИтоговыеРезультатыСоревнованиийОФПСдачаНормативов");
    ПроведениеСервер.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ, "РезультатыВыполненияПоказателейИНормативов");
	ПроведениеСервер.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ, "РезультатыПараметровСоревнованиийОФПСдачаНормативов");
	
	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовОтменыПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СоревнованиеОФПСдачаНормативов") Тогда
		
		ЗаполнитьПротоколНаОснованиеДокумента(ДанныеЗаполнения);	
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПротоколНаОснованиеДокумента(ДанныеЗаполнения) Экспорт
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,,"Дата, Номер");
	ДокументОснование = ДанныеЗаполнения;
	
	Для Каждого СтрокаНорматив Из ДанныеЗаполнения.ЗаявленныеНормативы Цикл
		СтрокаНормативОбъект                                                   = ЗаявленныеНормативы.Добавить();
		СтрокаНормативОбъект.Норматив                                          = СтрокаНорматив.Норматив;
		СтрокаНормативОбъект.УникальныйИдентификаторСтрокиЗаявленногоНорматива = СтрЗаменить(СтрокаНорматив.УникальныйИдентификаторНорматива,"_","-");
	КонецЦикла;	
	
	Для Каждого строкаДанныеЗаполнения Из ДанныеЗаполнения.СоставУчастников Цикл
		строкаСоставУчастников = СоставУчастников.Добавить();
		ЗаполнитьЗначенияСвойств(строкаСоставУчастников,строкаДанныеЗаполнения);
		строкаСоставУчастников.УникальныйИдентификаторСтрокиЗаявленногоНорматива = СтрЗаменить(строкаДанныеЗаполнения.УникальныйИдентификаторНорматива,"_","-");
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли