
// {Рарус adilas #18019 -Дисциплины 2021.07.07
#Область СлужебныйПрограммныйИнтерфейс

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"       , ДокументСсылка);
	Запрос.УстановитьПараметр("Период"       , ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("УчебныйГод"   , ДокументСсылка.УчебныйГод);
	Запрос.УстановитьПараметр("Организация"  , ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("Соревнование" , ДокументСсылка.Соревнование);
	Запрос.УстановитьПараметр("Тренер"       , ДокументСсылка.Тренер);
	Запрос.УстановитьПараметр("ВидСпорта"    , ДокументСсылка.ВидСпорта);
	Запрос.Текст =  "ВЫБРАТЬ
	                |	""ЗаявкиНаПроведениеСоревнования"" КАК ИмяРегистра_,
	                |	&Период КАК Период,
	                |	&УчебныйГод КАК УчебныйГод,
	                |	&Организация КАК Организация,
	                |	&Тренер КАК Тренер,
	                |	&ВидСпорта КАК ВидСпорта,
	                |	&Соревнование КАК Соревнование,
	                |	ЗаявкаНаПроведениеСоревнованияСоставУчастников.Спортсмен КАК Спортсмен,
	                |	ЗаявкаНаПроведениеСоревнованияСоставУчастников.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	ЗаявкаНаПроведениеСоревнованияСоставУчастников.Команда КАК Команда,
					// {Рарус adilas #18019 -Дисциплины 2021.07.07
	                |	ЗаявкаНаПроведениеСоревнованияСоставУчастников.Дисциплина КАК Дисциплина
					// }Рарус adilas #18019 -Дисциплины 2021.07.07
	                |ИЗ
	                |	Документ.ЗаявкаНаПроведениеСоревнования.СоставУчастников КАК ЗаявкаНаПроведениеСоревнованияСоставУчастников
	                |ГДЕ
	                |	ЗаявкаНаПроведениеСоревнованияСоставУчастников.Ссылка = &Ссылка";
	
	
	ТаблицаЗаявкиНаПроведениеСоревнования = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаЗаявкиНаПроведениеСоревнования" , ТаблицаЗаявкиНаПроведениеСоревнования);
	
    ДополнительныеСвойства.Вставить("Отказ", Отказ);
	
КонецПроцедуры	

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ЗаявкаНаПроведениеСоревнования.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольПередПроведением(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ЗаявкаНаПроведениеСоревнования.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовОтменыПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ЗаявкаНаПроведениеСоревнования.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПечатьWord

Функция ПолучитьДанныеПечати(знач МассивДокументов, знач МассивИменМакетов) Экспорт
	
	ДанныеПоВсемОбъектам = Новый Соответствие;
	
	Для Каждого ОбъектСсылка Из МассивДокументов Цикл
		ДанныеОбъектаПоМакетам = Новый Соответствие;
		Для Каждого ИмяМакета Из МассивИменМакетов Цикл
			ДанныеОбъектаПоМакетам.Вставить(ИмяМакета, ПолучитьДанныеОбъекта(ОбъектСсылка,ИмяМакета));
		КонецЦикла;
		ДанныеПоВсемОбъектам.Вставить(ОбъектСсылка, ДанныеОбъектаПоМакетам);
	КонецЦикла;
	
	ОписаниеОбластей = Новый Соответствие;
	ДвоичныеДанныеМакетов = Новый Соответствие;
	ТипыМакетов = Новый Соответствие;
	                                                             
	Для Каждого ИмяМакета Из МассивИменМакетов Цикл
		Если ИмяМакета = "ЗаявкаWord" Тогда
			ДвоичныеДанныеМакетов.Вставить(ИмяМакета, Документы.ЗаявкаНаПроведениеСоревнования.ПолучитьМакет("ЗаявкаWord"));
			ТипыМакетов.Вставить(ИмяМакета, "DOC");
		КонецЕсли;
		ОписаниеОбластей.Вставить(ИмяМакета, ПолучитьОписаниеОбластейМакетаОфисногоДокумента(ИмяМакета));
	КонецЦикла;

	
	Возврат Новый Структура("Данные, Макеты",
	ДанныеПоВсемОбъектам,
	Новый Структура("ОписаниеОбластей, ТипыМакетов, ДвоичныеДанныеМакетов",
	ОписаниеОбластей,
	ТипыМакетов,
	ДвоичныеДанныеМакетов));


КонецФункции

Функция ПолучитьОписаниеОбластейМакетаОфисногоДокумента(ИмяМакета)
	
	ОписаниеОбластей = Новый Структура;
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Заголовок"           , "Общая");
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаТаблицы"       , "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Подвал"              , "Общая");
	
	Возврат ОписаниеОбластей;
	
КонецФункции

Функция ПолучитьДанныеОбъекта(ОбъектыПечати,ИмяМакета) Экспорт
	Если ИмяМакета = "ЗаявкаWord" Тогда
		Возврат ПолучитьДанныеОбъектаЗаявка(ОбъектыПечати);
	КонецЕсли;
КонецФункции

Функция ПолучитьДанныеОбъектаЗаявка(ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументов", ОбъектыПечати);
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	СоставУчастников.Ссылка КАК Документ,
	                |	СоставУчастников.Ссылка.Дата КАК Дата,
	                |	СоставУчастников.Спортсмен КАК Спортсмен,
					// {Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
					|	СоставУчастников.Дисциплина КАК Дисциплина
					// }Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
	                |ПОМЕСТИТЬ ВТ_СоставУчастников
	                |ИЗ
	                |	Документ.ЗаявкаНаПроведениеСоревнования.СоставУчастников КАК СоставУчастников
	                |ГДЕ
	                |	СоставУчастников.Ссылка В(&СписокДокументов)
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_СоставУчастников.Документ КАК Документ,
	                |	КонтактнаяИнформацияАдрес.Представление КАК Адрес,
	                |	"""" КАК Телефон
	                |ПОМЕСТИТЬ ВТ_КонтактнаяИнформация
	                |ИЗ
	                |	ВТ_СоставУчастников КАК ВТ_СоставУчастников
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформацияАдрес
	                |		ПО ВТ_СоставУчастников.Документ.Организация = КонтактнаяИнформацияАдрес.Ссылка
	                |ГДЕ
	                |	КонтактнаяИнформацияАдрес.Тип = ЗНАЧЕНИЕ(перечисление.типыконтактнойинформации.адрес)
	                |	И КонтактнаяИнформацияАдрес.Вид = ЗНАЧЕНИЕ(справочник.видыконтактнойинформации.ФактАдресОрганизации)
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	ВТ_СоставУчастников.Документ,
	                |	"""",
	                |	КонтактнаяИнформацияТелефон.Представление
	                |ИЗ
	                |	ВТ_СоставУчастников КАК ВТ_СоставУчастников
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформацияТелефон
	                |		ПО ВТ_СоставУчастников.Документ.Организация = КонтактнаяИнформацияТелефон.Ссылка
	                |ГДЕ
	                |	КонтактнаяИнформацияТелефон.Тип = ЗНАЧЕНИЕ(перечисление.типыконтактнойинформации.Телефон)
	                |	И КонтактнаяИнформацияТелефон.Вид = ЗНАЧЕНИЕ(справочник.видыконтактнойинформации.ТелефонОрганизации)
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_СоставУчастников.Спортсмен КАК Спортсмен,
	                |	МАКСИМУМ(РегистрацияДопусков.Период) КАК Период
	                |ПОМЕСТИТЬ ВТ_МаксимумДопуски
	                |ИЗ
	                |	ВТ_СоставУчастников КАК ВТ_СоставУчастников
	                |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияДопусковКСпортивнымСоревнованиям КАК РегистрацияДопусков
	                |		ПО ВТ_СоставУчастников.Спортсмен = РегистрацияДопусков.Спортсмен
	                |			И ВТ_СоставУчастников.Дата <= РегистрацияДопусков.ДействуетДо
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ВТ_СоставУчастников.Спортсмен
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_МаксимумДопуски.Спортсмен КАК Спортсмен,
	                |	РегистрацияДопусков.ДействуетДо КАК ДействуетДо
	                |ПОМЕСТИТЬ ВТ_Допуски
	                |ИЗ
	                |	ВТ_МаксимумДопуски КАК ВТ_МаксимумДопуски
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияДопусковКСпортивнымСоревнованиям КАК РегистрацияДопусков
	                |		ПО ВТ_МаксимумДопуски.Спортсмен = РегистрацияДопусков.Спортсмен
	                |			И ВТ_МаксимумДопуски.Период = РегистрацияДопусков.Период
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_СоставУчастников.Спортсмен КАК Спортсмен,
	                |	МАКСИМУМ(ПодтвержденныеРазрядыСпортсменов.Период) КАК Период
	                |ПОМЕСТИТЬ ВТ_МаксимумПоРазрядам
	                |ИЗ
	                |	ВТ_СоставУчастников КАК ВТ_СоставУчастников
	                |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК ПодтвержденныеРазрядыСпортсменов
	                |		ПО ВТ_СоставУчастников.Спортсмен = ПодтвержденныеРазрядыСпортсменов.Спортсмен
	                |			И ВТ_СоставУчастников.Дата >= ПодтвержденныеРазрядыСпортсменов.Период
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ВТ_СоставУчастников.Спортсмен
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_МаксимумПоРазрядам.Спортсмен КАК Спортсмен,
	                |	РазрядыСпортсменов.Разряд КАК Разряд
	                |ПОМЕСТИТЬ ВТ_Разряды
	                |ИЗ
	                |	ВТ_МаксимумПоРазрядам КАК ВТ_МаксимумПоРазрядам
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК РазрядыСпортсменов
	                |		ПО ВТ_МаксимумПоРазрядам.Спортсмен = РазрядыСпортсменов.Спортсмен
	                |			И ВТ_МаксимумПоРазрядам.Период = РазрядыСпортсменов.Период
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_КонтактнаяИнформация.Документ КАК Документ,
	                |	МАКСИМУМ(ВТ_КонтактнаяИнформация.Адрес) КАК Адрес,
	                |	МАКСИМУМ(ВТ_КонтактнаяИнформация.Телефон) КАК Телефон
	                |ПОМЕСТИТЬ ВТ_КонтатнаяИнфорацияИтог
	                |ИЗ
	                |	ВТ_КонтактнаяИнформация КАК ВТ_КонтактнаяИнформация
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ВТ_КонтактнаяИнформация.Документ
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_СоставУчастников.Документ КАК Документ,
	                |	ВТ_СоставУчастников.Спортсмен КАК ФамилияИмяОтчество,
	                |	ФизическиеЛица.ГодРождения КАК ГодРождения,
	                |	ВТ_Разряды.Разряд КАК СпортивныйРазряд,
	                |	ВЫБОР
	                |		КОГДА ВТ_Допуски.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1)
	                |				ИЛИ ВТ_Допуски.ДействуетДо ЕСТЬ NULL
	                |			ТОГДА ""НЕ ДОПУЩЕН""
	                |		ИНАЧЕ ""ДОПУЩЕН""
	                |	КОНЕЦ КАК МедДопуск,
	                |	ВТ_КонтатнаяИнфорацияИтог.Адрес КАК Адрес,
	                |	ВТ_КонтатнаяИнфорацияИтог.Телефон КАК Телефон,
					// {Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
	                |	ВТ_СоставУчастников.Дисциплина КАК Дисциплина
					// }Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
	                |ИЗ
	                |	ВТ_СоставУчастников КАК ВТ_СоставУчастников
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	                |		ПО ВТ_СоставУчастников.Спортсмен.ФизическоеЛицо = ФизическиеЛица.Ссылка
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Допуски КАК ВТ_Допуски
	                |		ПО ВТ_СоставУчастников.Спортсмен = ВТ_Допуски.Спортсмен
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Разряды КАК ВТ_Разряды
	                |		ПО ВТ_СоставУчастников.Спортсмен = ВТ_Разряды.Спортсмен
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КонтатнаяИнфорацияИтог КАК ВТ_КонтатнаяИнфорацияИтог
	                |		ПО ВТ_СоставУчастников.Документ = ВТ_КонтатнаяИнфорацияИтог.Документ
	                |ИТОГИ ПО
	                |	Документ";
	
	МассивДокументов = Новый Массив;
	
	ВыборкаДокумент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДокумент.Следующий() Цикл
		
		СтруктураДокумента = Новый Структура;
		
		ОрганизацияАдресТелефон = "";
		
		СтруктураШапка = Новый Структура;
		СтруктураШапка.Вставить("Соревнование", ВыборкаДокумент.Документ.Соревнование);
		
		МассивСтрок = Новый Массив;
		Выборка = ВыборкаДокумент.Выбрать();
		НомерСтроки = 1;
		Пока Выборка.Следующий() Цикл
			
			Если ОрганизацияАдресТелефон = "" Тогда
				ОрганизацияАдресТелефон = СокрЛП(Выборка.Документ.Организация) + ?(ЗначениеЗаполнено(Выборка.Адрес),"; адрес: " + Выборка.Адрес,"") +
				   ?(ЗначениеЗаполнено(Выборка.Телефон),"; тел.: " + Выборка.Телефон,"");
				СтруктураШапка.Вставить("ОрганизацияАдресТелефон", ОрганизацияАдресТелефон);
				СтруктураДокумента.Вставить("Заголовок", СтруктураШапка);
			КонецЕсли;	
			// {Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
			СтруктураСтрока = Новый Структура("НомерСтроки, ФамилияИмяОтчество, ГодРождения, Дисциплина, СпортивныйРазряд, МедДопуск");
			// }Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
			ЗаполнитьЗначенияСвойств(СтруктураСтрока, Выборка);
			СтруктураСтрока.НомерСтроки = НомерСтроки;
			СтруктураСтрока.ГодРождения = Выборка.ГодРождения;
			НомерСтроки = НомерСтроки +1;
			МассивСтрок.Добавить(СтруктураСтрока);
		КонецЦикла;
		СтруктураДокумента.Вставить("СтрокиТаблицы", МассивСтрок);
		
		СтруктураДокумента.Вставить("Подвал", Новый Структура("Количество", Выборка.Количество()));
		
		МассивДокументов.Добавить(СтруктураДокумента);
			
	КонецЦикла;
	
	Возврат МассивДокументов
	
КонецФункции	

#КонецОбласти

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявкаТабДок") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ЗаявкаТабДок", "Заявка", СформироватьПечатнуюФормуЗаявка(МассивОбъектов));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуЗаявка(МассивОбъектовДляПечати) Экспорт
	
	Если ТипЗнч(МассивОбъектовДляПечати) = Тип("Массив") Тогда
		
		МассивОбъектов = МассивОбъектовДляПечати;
		
	Иначе
		
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(МассивОбъектовДляПечати);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗаявкаНаПроведениеСоревнования.Ссылка КАК Ссылка,
	               |	ЗаявкаНаПроведениеСоревнования.Организация КАК Организация,
	               |	ЗаявкаНаПроведениеСоревнования.Дата КАК ДатаСреза,
	               |	ЗаявкаНаПроведениеСоревнования.Соревнование КАК Соревнование
	               |ПОМЕСТИТЬ ВТ_Данные
	               |ИЗ
	               |	Документ.ЗаявкаНаПроведениеСоревнования КАК ЗаявкаНаПроведениеСоревнования
	               |ГДЕ
	               |	ЗаявкаНаПроведениеСоревнования.Ссылка В(&МассивОбъектов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_Данные.Организация КАК Организация,
	               |	МАКСИМУМ(КонтактнаяИнформация.Представление) КАК Телефон
	               |ПОМЕСТИТЬ ВТ_Телефон
	               |ИЗ
	               |	ВТ_Данные КАК ВТ_Данные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |		ПО ВТ_Данные.Организация = КонтактнаяИнформация.Ссылка
	               |ГДЕ
	               |	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(перечисление.типыконтактнойинформации.телефон)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Данные.Организация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_Данные.Организация КАК Организация,
	               |	МАКСИМУМ(КонтактнаяИнформация.Представление) КАК Адрес
	               |ПОМЕСТИТЬ ВТ_Адрес
	               |ИЗ
	               |	ВТ_Данные КАК ВТ_Данные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |		ПО ВТ_Данные.Организация = КонтактнаяИнформация.Ссылка
	               |ГДЕ
	               |	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(перечисление.типыконтактнойинформации.Адрес)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Данные.Организация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СоставУчастников.Ссылка КАК Ссылка,
	               |	СоставУчастников.Спортсмен КАК Спортсмен,
	               |	СоставУчастников.Ссылка.Дата КАК ДатаСреза,
	               |	ФизическиеЛица.ГодРождения КАК ГодРождения,
				   // {Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
	               |	СоставУчастников.Дисциплина КАК Дисциплина
				   // }Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
	               |ПОМЕСТИТЬ ВТ_ТаблицаСпортсменов
	               |ИЗ
	               |	Документ.ЗаявкаНаПроведениеСоревнования.СоставУчастников КАК СоставУчастников
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |		ПО СоставУчастников.Спортсмен.ФизическоеЛицо = ФизическиеЛица.Ссылка
	               |ГДЕ
	               |	СоставУчастников.Ссылка В(&МассивОбъектов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ТаблицаСпортсменов.Ссылка КАК Ссылка,
	               |	ВТ_ТаблицаСпортсменов.Спортсмен КАК Спортсмен,
	               |	МАКСИМУМ(ПодтвержденныеРазрядыСпортсменов.Период) КАК Период
	               |ПОМЕСТИТЬ ВТ_МаксимальныеДаты
	               |ИЗ
	               |	ВТ_ТаблицаСпортсменов КАК ВТ_ТаблицаСпортсменов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК ПодтвержденныеРазрядыСпортсменов
	               |		ПО ВТ_ТаблицаСпортсменов.Спортсмен = ПодтвержденныеРазрядыСпортсменов.Спортсмен
	               |			И ВТ_ТаблицаСпортсменов.ДатаСреза >= ПодтвержденныеРазрядыСпортсменов.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_ТаблицаСпортсменов.Ссылка,
	               |	ВТ_ТаблицаСпортсменов.Спортсмен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_МаксимальныеДаты.Ссылка КАК Ссылка,
	               |	ВТ_МаксимальныеДаты.Спортсмен КАК Спортсмен,
	               |	ПодтвержденныеРазрядыСпортсменов.Разряд КАК Разряд
	               |ПОМЕСТИТЬ ВТ_РазрядыСпортсменов
	               |ИЗ
	               |	ВТ_МаксимальныеДаты КАК ВТ_МаксимальныеДаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК ПодтвержденныеРазрядыСпортсменов
	               |		ПО ВТ_МаксимальныеДаты.Спортсмен = ПодтвержденныеРазрядыСпортсменов.Спортсмен
	               |			И ВТ_МаксимальныеДаты.Период = ПодтвержденныеРазрядыСпортсменов.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ТаблицаСпортсменов.Ссылка КАК Ссылка,
	               |	ВТ_ТаблицаСпортсменов.Спортсмен КАК Спортсмен,
	               |	МАКСИМУМ(РегистрацияДопусковКСпортивнымСоревнованиям.Период) КАК Период
	               |ПОМЕСТИТЬ ВТ_МаксимальныеДатыДопуск
	               |ИЗ
	               |	ВТ_ТаблицаСпортсменов КАК ВТ_ТаблицаСпортсменов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияДопусковКСпортивнымСоревнованиям КАК РегистрацияДопусковКСпортивнымСоревнованиям
	               |		ПО ВТ_ТаблицаСпортсменов.Спортсмен = РегистрацияДопусковКСпортивнымСоревнованиям.Спортсмен
	               |			И ВТ_ТаблицаСпортсменов.ДатаСреза >= РегистрацияДопусковКСпортивнымСоревнованиям.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_ТаблицаСпортсменов.Ссылка,
	               |	ВТ_ТаблицаСпортсменов.Спортсмен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_МаксимальныеДатыДопуск.Ссылка КАК Ссылка,
	               |	ВТ_МаксимальныеДатыДопуск.Спортсмен КАК Спортсмен,
	               |	РегистрацияДопусковКСпортивнымСоревнованиям.ДействуетДо КАК ДействуетДо
	               |ПОМЕСТИТЬ ВТ_Допуски
	               |ИЗ
	               |	ВТ_МаксимальныеДатыДопуск КАК ВТ_МаксимальныеДатыДопуск
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияДопусковКСпортивнымСоревнованиям КАК РегистрацияДопусковКСпортивнымСоревнованиям
	               |		ПО ВТ_МаксимальныеДатыДопуск.Спортсмен = РегистрацияДопусковКСпортивнымСоревнованиям.Спортсмен
	               |			И ВТ_МаксимальныеДатыДопуск.Период = РегистрацияДопусковКСпортивнымСоревнованиям.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Данные.Ссылка КАК Ссылка,
	               |	ВТ_Данные.Организация КАК Организация,
	               |	ВТ_Данные.Соревнование КАК Соревнование,
	               |	ЕСТЬNULL(ВТ_Адрес.Адрес, """") КАК Адрес,
	               |	ЕСТЬNULL(ВТ_Телефон.Телефон, """") КАК Телефон,
	               |	ЕСТЬNULL(ВТ_Допуски.ДействуетДо, ДАТАВРЕМЯ(1, 1, 1)) КАК Допуск,
	               |	ЕСТЬNULL(ВТ_РазрядыСпортсменов.Разряд, ЗНАЧЕНИЕ(справочник.спортивнаяклассификация.пустаяссылка)) КАК СпортивныйРазряд,
	               |	ВТ_ТаблицаСпортсменов.Спортсмен КАК ФамилияИмяОтчество,
	               |	ВТ_Данные.ДатаСреза КАК ДатаСреза,
	               |	ВТ_ТаблицаСпортсменов.ГодРождения КАК ГодРождения,
				   // {Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
	               |	ВТ_ТаблицаСпортсменов.Дисциплина КАК Дисциплина
				   // }Рарус adilas #18075 -Печатная форма Заявки на участие в соревновании 2021.07.05
	               |ИЗ
	               |	ВТ_Данные КАК ВТ_Данные  
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Телефон КАК ВТ_Телефон
	               |		ПО ВТ_Данные.Организация = ВТ_Телефон.Организация
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Адрес КАК ВТ_Адрес
	               |		ПО ВТ_Данные.Организация = ВТ_Адрес.Организация
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаСпортсменов КАК ВТ_ТаблицаСпортсменов
	               |			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РазрядыСпортсменов КАК ВТ_РазрядыСпортсменов
	               |			ПО ВТ_ТаблицаСпортсменов.Спортсмен = ВТ_РазрядыСпортсменов.Спортсмен
	               |				И ВТ_ТаблицаСпортсменов.Ссылка = ВТ_РазрядыСпортсменов.Ссылка
	               |			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Допуски КАК ВТ_Допуски
	               |			ПО ВТ_ТаблицаСпортсменов.Спортсмен = ВТ_Допуски.Спортсмен
	               |				И ВТ_ТаблицаСпортсменов.Ссылка = ВТ_Допуски.Ссылка
	               |		ПО ВТ_Данные.Ссылка = ВТ_ТаблицаСпортсменов.Ссылка
	               |ИТОГИ ПО
	               |	Ссылка";
	
	ПутьКМакету = "Документ.ЗаявкаНаПроведениеСоревнования.ЗаявкаТабДок";
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету);
	
	ТабДок = Новый ТабличныйДокумент;
	
	ВыборкаДокумент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДокумент.Следующий() Цикл
		
		ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
		
		ШапкаЗаполнена = Ложь;
		
		Выборка = ВыборкаДокумент.Выбрать();
		Индекс = 1;
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ШапкаЗаполнена Тогда
				ОрганизацияАдресТелефон = СокрЛП(Выборка.Организация) + ?(ЗначениеЗаполнено(Выборка.Адрес), "; " + СокрЛП(Выборка.Адрес), "") +
				   ?(ЗначениеЗаполнено(Выборка.Телефон), "; тел. " + СокрЛП(Выборка.Телефон), "");
				ОбластьШапка.Параметры.ОрганизацияАдресТелефон = ОрганизацияАдресТелефон;
				ОбластьШапка.Параметры.Соревнование = Выборка.Соревнование;
				ТабДок.Вывести(ОбластьШапка);
				ШапкаЗаполнена = Истина;
			КонецЕсли;
			
			ОбластьСтрока = Макет.ПолучитьОбласть("ОбластьСтрокаТаблицы");
			ОбластьСтрока.Параметры.Заполнить(Выборка);
			ОбластьСтрока.Параметры.ГодРождения  = Выборка.ГодРождения;
			ОбластьСтрока.Параметры.МедДопуск    = ?(Выборка.Допуск = Дата(1,1,1) ИЛИ Выборка.Допуск < Выборка.ДатаСреза, "Не допущен", "Допущен. Действует до " + Формат(Выборка.Допуск,"ДЛФ=Д"));
			ОбластьСтрока.Параметры.НомерСтроки  = Индекс;
			Индекс = Индекс + 1;
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;	
		
		ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПовал");
		ОбластьПодвал.Параметры.Количество = Выборка.Количество();
		ТабДок.Вывести(ОбластьПодвал);
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;	
	
	ТабДок.КлючПараметровПечати	= ПутьКМакету;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб 			= Истина;
	
	Возврат ТабДок;
	
	
КонецФункции	

#КонецОбласти

#КонецОбласти
// }Рарус adilas #18019 -Дисциплины 2021.07.07