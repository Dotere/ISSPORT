// {Рарус adilas #1.0.0.2 -Исправления по SonarQube 2021.04.23
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
    КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	УчетСпортсменовСервер.ЗаполнитьРеквизит(ЭтотОбъект, "Ответственный", ПараметрыСеанса.ТекущийПользователь);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)	
	
	// {Рарус adilas #Возрастные группы в соревновании -11068 2020.12.09
	// Уведомления о страховании и возрастных группах					
	ВыполнитьПроверкуПередПроведением(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// }Рарус adilas #Возрастные группы в соревновании -11068 2020.12.09
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	ПроведениеСервер.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ, "ЗаявкиНаПроведениеСоревнования");

	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	Движения.Записать();

	ПроведениеСервер.ВыполнитьКонтрольРезультатовОтменыПроведения(ЭтотОбъект, Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьПоВозрастнымГруппам() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаДокумент"          , Дата);
	Запрос.УстановитьПараметр("Тренер"                , Тренер);
	Запрос.УстановитьПараметр("Организация"           , Организация);
	Запрос.УстановитьПараметр("УчебныйГод"            , УчебныйГод);
	Запрос.УстановитьПараметр("Соревнование"          , Соревнование);
	Запрос.УстановитьПараметр("ВидСпорта"             , ВидСпорта);
	//{Рарус lobash IN-9303 11.09.20
	МеждународнаяВозрастнаяГруппа = Соревнование.МеждународныеВозрастныеГруппы.ВыгрузитьКолонку("МеждународнаяВозрастнаяГруппа");
	СпортивнаяКлассификация = Соревнование.СпортивнаяКлассификация.ВыгрузитьКолонку("Разряд");
	Запрос.УстановитьПараметр("СписокВозрастныхГрупп" , МеждународнаяВозрастнаяГруппа);
	Запрос.УстановитьПараметр("СписокРазрядов"        , СпортивнаяКлассификация);
	//}Рарус lobash IN-9303 11.09.20
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ЛичныйТренерСпортсмена.Группа КАК Группа
	                |ПОМЕСТИТЬ ВТ_СписокГрупп
	                |ИЗ
	                |	РегистрСведений.ЛичныйТренерСпортсмена КАК ЛичныйТренерСпортсмена
	                |ГДЕ
	                |	ЛичныйТренерСпортсмена.УчебныйГод = &УчебныйГод
	                |	И ЛичныйТренерСпортсмена.Организация = &Организация %1
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ЛичныйТренерСпортсмена.Группа
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	СоставГруппы.Группа
	                |ИЗ
	                |	РегистрСведений.СоставГруппы КАК СоставГруппы
	                |ГДЕ
	                |	СоставГруппы.УчебныйГод = &УчебныйГод
	                |	И СоставГруппы.Организация = &Организация
	                |	%2
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	СоставГруппы.Группа
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	СоставГруппы.Спортсмен КАК Спортсмен
	                |ПОМЕСТИТЬ ВТ_СписокСпортсменов
	                |ИЗ
	                |	РегистрСведений.СоставГруппы КАК СоставГруппы
					|ГДЕ
					|  СоставГруппы.Период <= &ДатаДокумент И
					|     СоставГруппы.УчебныйГод = &УчебныйГод И СоставГруппы.Группа В
	                |					(ВЫБРАТЬ
	                |						ВТ_СписокГрупп.Группа
	                |					 ИЗ
	                |						ВТ_СписокГрупп)
					|				И (СоставГруппы.ДатаИсключенияИзГруппы >= &ДатаДокумент
	                |					ИЛИ СоставГруппы.ДатаИсключенияИзГруппы = ДАТАВРЕМЯ(1, 1, 1))
	                |				И СоставГруппы.Организация = &Организация
	                |				И СоставГруппы.ВидСпорта = &ВидСпорта
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	Разряды.Спортсмен КАК Спортсмен,
	                |	МАКСИМУМ(Разряды.Период) КАК Период
	                |ПОМЕСТИТЬ ВТ_МаксимумРазряды
	                |ИЗ
	                |	РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК Разряды
					|ГДЕ
					|  Разряды.ДатаПрисвоения <=&ДатаДокумент
					|   ИЛИ (Разряды.ДатаПодтверждения <=&ДатаДокумент И Разряды.ДатаПодтверждения <>&ДатаДокумент)
					|    И Разряды.Спортсмен В
	                |					(ВЫБРАТЬ
	                |						ВТ_СписокСпортсменов.Спортсмен
	                |					ИЗ
	                |						ВТ_СписокСпортсменов)
	                |				И Разряды.ВидСпорта = &ВидСпорта
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	Разряды.Спортсмен
	                |;
					|////////////////////////////////////////////////////////////////////////////////
					|
					|ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	Разряды.Спортсмен КАК Спортсмен,
					|	Разряды.Разряд КАК Разряд
					|ПОМЕСТИТЬ ВТ_ПрисвоенныеРазрядыСпортсменов
					|ИЗ
					|	РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК Разряды
					|ГДЕ
					|	(Разряды.ДатаПрисвоения <= &ДатаДокумент
					|			ИЛИ Разряды.ДатаПодтверждения <= &ДатаДокумент
					|				И Разряды.ДатаПодтверждения <> &ДатаДокумент
					|				И Разряды.Спортсмен В
					|					(ВЫБРАТЬ
					|						ВТ_СписокСпортсменов.Спортсмен
					|					ИЗ
					|						ВТ_СписокСпортсменов)
					|				И Разряды.ВидСпорта = &ВидСпорта)
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	ВТ_МаксимумРазряды.Спортсмен КАК Спортсмен,
					|	РазрядыСпортсменов.Разряд КАК Разряд
					|ПОМЕСТИТЬ ВТ_Разряды
	                |ИЗ
	                |	ВТ_МаксимумРазряды КАК ВТ_МаксимумРазряды
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК РазрядыСпортсменов
	                |		ПО ВТ_МаксимумРазряды.Спортсмен = РазрядыСпортсменов.Спортсмен
	                |			И ВТ_МаксимумРазряды.Период = РазрядыСпортсменов.Период
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	РегистрацияДопусков.Спортсмен КАК Спортсмен,
	                |	МАКСИМУМ(РегистрацияДопусков.Период) КАК Период
	                |ПОМЕСТИТЬ ВТ_МаксимумДопуски
	                |ИЗ
	                |	РегистрСведений.РегистрацияДопусковКСпортивнымСоревнованиям КАК РегистрацияДопусков
					|ГДЕ
					|   РегистрацияДопусков.ДействуетДо>=&ДатаДокумент И
					|    РегистрацияДопусков.Спортсмен В
	                |					(ВЫБРАТЬ
	                |						ВТ_СписокСпортсменов.Спортсмен
	                |					ИЗ
	                |						ВТ_СписокСпортсменов)
	                |				И РегистрацияДопусков.ВидСпорта = &ВидСпорта
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	РегистрацияДопусков.Спортсмен
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_МаксимумДопуски.Спортсмен КАК Спортсмен,
	                |	РегистрацияДопусков.ДействуетДо КАК ДействуетДо
	                |ПОМЕСТИТЬ ВТ_Допуски
	                |ИЗ
	                |	ВТ_МаксимумДопуски КАК ВТ_МаксимумДопуски
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияДопусковКСпортивнымСоревнованиям КАК РегистрацияДопусков
	                |		ПО ВТ_МаксимумДопуски.Спортсмен = РегистрацияДопусков.Спортсмен
	                |			И ВТ_МаксимумДопуски.Период = РегистрацияДопусков.Период
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_СписокСпортсменов.Спортсмен КАК Спортсмен,
	                |	МеждународныеВозрастныеГруппы.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа
	                |ПОМЕСТИТЬ ВТ_СпортсменыГруппы
	                |ИЗ
	                |	ВТ_СписокСпортсменов КАК ВТ_СписокСпортсменов
	                |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МеждународныеВозрастныеГруппы.СрезПоследних(&ДатаДокумент, УчебныйГод = &УчебныйГод И Организация = &Организация И ВидСпорта = &ВидСпорта) КАК МеждународныеВозрастныеГруппы
	                |		ПО ВТ_СписокСпортсменов.Спортсмен = МеждународныеВозрастныеГруппы.Спортсмен
	                |ГДЕ
	                |	%3
	                |;
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_СпортсменыГруппы.Спортсмен КАК Спортсмен,
	                |	ВТ_СпортсменыГруппы.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	РазрядыСпортсменов.Разряд КАК Разряд
	                |ПОМЕСТИТЬ ВТ_СпортсменыГруппыСРазрядом
	                |ИЗ
	                |	ВТ_СпортсменыГруппы КАК ВТ_СпортсменыГруппы
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Разряды КАК РазрядыСпортсменов
	                |		ПО ВТ_СпортсменыГруппы.Спортсмен = РазрядыСпортсменов.Спортсмен
	                |ГДЕ
	                |	%4
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	РегистрацияСтраховыхПолисов.Спортсмен КАК Спортсмен,
	                |	МАКСИМУМ(РегистрацияСтраховыхПолисов.Период) КАК Период
	                |ПОМЕСТИТЬ ВТ_МаксимумПоСтраховымПолисам
	                |ИЗ
	                |	РегистрСведений.РегистрацияСтраховыхПолисовСпортсменов КАК РегистрацияСтраховыхПолисов
					|ГДЕ
					|   РегистрацияСтраховыхПолисов.ДатаНачала <= &ДатаДокумент И
					|    РегистрацияСтраховыхПолисов.ВидСпорта = &ВидСпорта
	                |				И РегистрацияСтраховыхПолисов.Спортсмен В
	                |					(ВЫБРАТЬ
	                |						ВТ_СписокСпортсменов.Спортсмен
	                |					ИЗ
	                |						ВТ_СписокСпортсменов)
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	РегистрацияСтраховыхПолисов.Спортсмен
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_МаксимумПоСтраховымПолисам.Спортсмен КАК Спортсмен,
					|	РегистрацияСтраховыхПолисовСпортсменов.Регистратор КАК СтраховойПолисРегистратор,
	                |	РегистрацияСтраховыхПолисовСпортсменов.СтраховаяКомпания КАК СтраховаяКомпания,
	                |	РегистрацияСтраховыхПолисовСпортсменов.ДатаОкончания КАК ДатаОкончания,
	                |	РегистрацияСтраховыхПолисовСпортсменов.НомерСтраховогоПолиса КАК НомерСтраховогоПолиса,
	                |	РегистрацияСтраховыхПолисовСпортсменов.ДатаВыдачиПолиса КАК ДатаВыдачиПолиса
	                |ПОМЕСТИТЬ ВТ_СтраховойПолис
	                |ИЗ
	                |	ВТ_МаксимумПоСтраховымПолисам КАК ВТ_МаксимумПоСтраховымПолисам
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияСтраховыхПолисовСпортсменов КАК РегистрацияСтраховыхПолисовСпортсменов
	                |		ПО ВТ_МаксимумПоСтраховымПолисам.Спортсмен = РегистрацияСтраховыхПолисовСпортсменов.Спортсмен
	                |			И ВТ_МаксимумПоСтраховымПолисам.Период = РегистрацияСтраховыхПолисовСпортсменов.Период
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	ВТ_СпортсменыГруппыСРазрядом.Спортсмен КАК Спортсмен,
	                |	ГОД(ВТ_СпортсменыГруппыСРазрядом.Спортсмен.ФизическоеЛицо.ДатаРождения) КАК ГодРождения,
	                |	ВТ_СпортсменыГруппыСРазрядом.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	ЕСТЬNULL(ВТ_Допуски.ДействуетДо, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДопуска,
	                |	ЕСТЬNULL(ВТ_СпортсменыГруппыСРазрядом.Разряд, ЗНАЧЕНИЕ(Справочник.СпортивнаяКлассификация.пустаяссылка)) КАК Разряд,
	                |	ВЫРАЗИТЬ("""" КАК СТРОКА(50)) КАК Допуск,
					|	ВТ_СтраховойПолис.СтраховойПолисРегистратор КАК СтраховойПолисРегистратор,
	                |	ВТ_СтраховойПолис.СтраховаяКомпания КАК СтраховаяКомпания,
	                |	ЕСТЬNULL(ВТ_СтраховойПолис.ДатаОкончания, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОкончания,
	                |	ВТ_СтраховойПолис.НомерСтраховогоПолиса КАК НомерСтраховогоПолиса,
	                |	ЕСТЬNULL(ВТ_СтраховойПолис.ДатаВыдачиПолиса, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВыдачиПолиса,
	                |	ВЫРАЗИТЬ("""" КАК СТРОКА(300)) КАК СтраховойПолис
	                |ИЗ
	                |	ВТ_СпортсменыГруппыСРазрядом КАК ВТ_СпортсменыГруппыСРазрядом
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Допуски КАК ВТ_Допуски
	                |		ПО ВТ_СпортсменыГруппыСРазрядом.Спортсмен = ВТ_Допуски.Спортсмен
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтраховойПолис КАК ВТ_СтраховойПолис
	                |		ПО ВТ_СпортсменыГруппыСРазрядом.Спортсмен = ВТ_СтраховойПолис.Спортсмен";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ?(ЗначениеЗаполнено(Тренер), " И ЛичныйТренерСпортсмена.Тренер = &Тренер", ""));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ?(ЗначениеЗаполнено(Тренер), " И СоставГруппы.ТренерСборной = &Тренер", ""));
	//{Рарус lobash IN-9303 11.09.20
	Если НЕ МеждународнаяВозрастнаяГруппа.Количество() = 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%3", " МеждународныеВозрастныеГруппы.МеждународнаяВозрастнаяГруппа В(&СписокВозрастныхГрупп)");
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%3", " ИСТИНА");
	КонецЕсли;
		
	Если НЕ СпортивнаяКлассификация.Количество() = 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%4", " РазрядыСпортсменов.Разряд В(&СписокРазрядов)");
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%4", " ИСТИНА");
	КонецЕсли;
	//}Рарус lobash IN-9303 11.09.20
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	Для Каждого строкаРезультат Из тзРезультат Цикл
		строкаРезультат.Допуск = ?(строкаРезультат.ДатаДопуска = Дата(1,1,1), "НЕ ДОПУЩЕН - Нет допуска", "ДОПУЩЕН - Действует до " + Формат(строкаРезультат.ДатаДопуска,"ДЛФ=Д"));
		
		// {Рарус adilas #22219 -Функциональные опции 2021.12.08
		// - Нельзя удалять! ТекущиеПараметрыФО 
		Если УчетСпортсменовВызовСервера.ТекущиеПараметрыФО(Организация).СтрахованиеСпортсменовОрганизация Тогда	 
			// {Рарус adilas #-11071 -Ведение страховых полисов спортсмнов 2020.11.26
			// {Рарус dotere #-19145 -Убраны ТекущиеПараметрыФО, выдавал ошибку 2021.08.31
			строкаРезультат.СтраховойПолис = ?(ЗначениеЗаполнено(строкаРезультат.СтраховаяКомпания),СокрЛП(строкаРезультат.СтраховаяКомпания)+", ","") + 
			?(строкаРезультат.ДатаОкончания = Дата(1,1,1),"", "действует до " + Формат(строкаРезультат.ДатаОкончания,"ДЛФ=Д")+", ") +
			?(ЗначениеЗаполнено(строкаРезультат.НомерСтраховогоПолиса), "№ " + СокрЛП(строкаРезультат.НомерСтраховогоПолиса) + ", ","")+
			?(строкаРезультат.ДатаВыдачиПолиса = Дата(1,1,1),"","от " + Формат(строкаРезультат.ДатаВыдачиПолиса,"ДЛФ=Д"));
			строкаРезультат.СтраховойПолис = ?(строкаРезультат.ДатаОкончания < Дата И строкаРезультат.ДатаОкончания	<> Дата(1,1,1), строкаРезультат.СтраховойПолис + " ПРОСРОЧЕН", строкаРезультат.СтраховойПолис);
			строкаРезультат.СтраховойПолис = ?(НЕ ЗначениеЗаполнено(строкаРезультат.СтраховойПолис), "не введен",строкаРезультат.СтраховойПолис);
		КонецЕсли;
		
		
		Если УчетСпортсменовВызовСервера.ТекущиеПараметрыФО(Организация).СтрахованиеСпортсменовОрганизация Тогда	
			Если строкаРезультат.ДатаОкончания < ТекущаяДатаСеанса() ИЛИ строкаРезультат.СтраховойПолисРегистратор.Пустая()  Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У спортсмена %1 отсутствует или просрочен страховой полис.'"),
				строкаРезультат.Спортсмен);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		// }Рарус dotere #-19145 -Убраны ТекущиеПараметрыФО, выдавал ошибку 2021.08.31
		// }Рарус adilas #-11071 -Ведение страховых полисов спортсмнов 2020.11.26 
		КонецЕсли;
		// }Рарус adilas #22219 -Функциональные опции 2021.12.08	
	КонецЦикла;
	 
	 Возврат тзРезультат
	 
 КонецФункции	
 
// {Рарус adilas #11068 -Возрастные группы в соревновании 2020.12.07
Процедура ВыполнитьПроверкуПередПроведением(Отказ) Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаСпортсменов"     , СоставУчастников.Выгрузить());
	Запрос.УстановитьПараметр("ДатаДокумент"           , Дата);
	Запрос.УстановитьПараметр("ВидСпорта"              , ВидСпорта);
	Запрос.УстановитьПараметр("СписокВозврастныхГрупп" , Соревнование.МеждународныеВозрастныеГруппы.Выгрузить().ВыгрузитьКолонку("МеждународнаяВозрастнаяГруппа"));
	Запрос.Текст =  "ВЫБРАТЬ
	                |	ТаблицаСпортсменов.Спортсмен КАК Спортсмен,
	                |	ТаблицаСпортсменов.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа
	                |ПОМЕСТИТЬ ВТ_ТаблицаСпортсменов
	                |ИЗ
	                |	&ТаблицаСпортсменов КАК ТаблицаСпортсменов
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	РегистрацияСтраховыхПолисовСпортсменов.Спортсмен КАК Спортсмен
	                |ПОМЕСТИТЬ ВТ_СпортменыСПолисом
	                |ИЗ
	                |	РегистрСведений.РегистрацияСтраховыхПолисовСпортсменов КАК РегистрацияСтраховыхПолисовСпортсменов
	                |ГДЕ
	                |	РегистрацияСтраховыхПолисовСпортсменов.Спортсмен В
	                |			(ВЫБРАТЬ
	                |				ВТ_ТаблицаСпортсменов.Спортсмен
	                |			ИЗ
	                |				ВТ_ТаблицаСпортсменов)
	                |	И РегистрацияСтраховыхПолисовСпортсменов.ВидСпорта = &ВидСпорта
	                |	И РегистрацияСтраховыхПолисовСпортсменов.ДатаНачала <= &ДатаДокумент
	                |	И РегистрацияСтраховыхПолисовСпортсменов.ДатаОкончания >= &ДатаДокумент
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	СоставУчащихсяСпортивногоУчрежденияСрезПоследних.Спортсмен КАК Спортсмен,
	                |	СоставУчащихсяСпортивногоУчрежденияСрезПоследних.Спортсмен.СтатусСпортсмена КАК СпортсменСтатусСпортсмена
	                |ПОМЕСТИТЬ ВТ_СоставУчащихся
	                |ИЗ
	                |	РегистрСведений.СоставУчащихсяСпортивногоУчреждения.СрезПоследних(&ДатаДокумент, ВидСпорта = &ВидСпорта) КАК СоставУчащихсяСпортивногоУчрежденияСрезПоследних
	                |ГДЕ
	                |	(СоставУчащихсяСпортивногоУчрежденияСрезПоследних.ДатаОкончанияОбучения = ДАТАВРЕМЯ(1, 1, 1)
	                |			ИЛИ СоставУчащихсяСпортивногоУчрежденияСрезПоследних.ДатаОкончанияОбучения >= &ДатаДокумент
	                |			ИЛИ СоставУчащихсяСпортивногоУчрежденияСрезПоследних.Спортсмен.СтатусСпортсмена <> ЗНАЧЕНИЕ(Перечисление.СтатусыСпортсменов.СпортсменДругогоСпортивногоУчреждения))
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	ВТ_ТаблицаСпортсменов.Спортсмен КАК Спортсмен,
	                |	ВТ_ТаблицаСпортсменов.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа
	                |ИЗ
	                |	ВТ_ТаблицаСпортсменов КАК ВТ_ТаблицаСпортсменов
	                |ГДЕ
	                |	НЕ ВТ_ТаблицаСпортсменов.МеждународнаяВозрастнаяГруппа В (&СписокВозврастныхГрупп)
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	ВТ_ТаблицаСпортсменов.Спортсмен КАК Спортсмен
	                |ИЗ
	                |	ВТ_ТаблицаСпортсменов КАК ВТ_ТаблицаСпортсменов
	                |ГДЕ
	                |	НЕ ВТ_ТаблицаСпортсменов.Спортсмен В
	                |				(ВЫБРАТЬ
	                |					ВТ_СпортменыСПолисом.Спортсмен
	                |				ИЗ
	                |					ВТ_СпортменыСПолисом)
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	ВТ_ТаблицаСпортсменов.Спортсмен КАК Спортсмен
	                |ИЗ
	                |	ВТ_ТаблицаСпортсменов КАК ВТ_ТаблицаСпортсменов
	                |ГДЕ
	                |	НЕ ВТ_ТаблицаСпортсменов.Спортсмен В
	                |				(ВЫБРАТЬ
	                |					ВТ_СоставУчащихся.Спортсмен КАК Спортсмен
	                |				ИЗ
	                |					ВТ_СоставУчащихся КАК ВТ_СоставУчащихся)";					
			
	Результат = Запрос.ВыполнитьПакет();
		
	Если УчетСпортсменовВызовСервера.ТекущиеПараметрыФО(Организация).ВозрастныеГруппыОрганизация Тогда
		Если НЕ Соревнование.МеждународныеВозрастныеГруппы.Выгрузить().ВыгрузитьКолонку("МеждународнаяВозрастнаяГруппа").Количество() = 0 Тогда
			тзСпортсменыБезГруппы = Результат[3].Выгрузить();
			Если НЕ тзСпортсменыБезГруппы.Количество() = 0 Тогда
				
				Для Каждого строкаСпортсмен Из тзСпортсменыБезГруппы Цикл
					Если строкаСпортсмен.Спортсмен.СтатусСпортсмена <> Перечисления.статусыСпортсменов.СпортсменДругогоСпортивногоУчреждения Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В соревнование не заявлена возрастная группа %1, спортсмен %2.'"),
					строкаСпортсмен.МеждународнаяВозрастнаяГруппа,
					строкаСпортсмен.Спортсмен);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
					Отказ = Истина;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если УчетСпортсменовВызовСервера.ТекущиеПараметрыФО(Организация).СтрахованиеСпортсменовОрганизация Тогда
		тзСпортсменыБезПолиса = Результат[4].Выгрузить();
		Если НЕ тзСпортсменыБезПолиса.Количество() = 0 Тогда
			Для Каждого строкаСпортсмен Из тзСпортсменыБезПолиса Цикл
				Если строкаСпортсмен.Спортсмен.СтатусСпортсмена <> Перечисления.статусыСпортсменов.СпортсменДругогоСпортивногоУчреждения Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У спортсмена %1 отсутствует или просрочен страховой полис.'"),
				строкаСпортсмен.Спортсмен);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецЕсли
			КонецЦикла;	
		КонецЕсли;
	КонецЕсли;
	
	// {Рарус adilas #11666 -Тестирование. Пункт №2 2020.12.11
	// Добавить проверку на отчисление
	тзОтчисленныеСпортсмены = Результат[5].Выгрузить();
	Если НЕ тзОтчисленныеСпортсмены.Количество() = 0 Тогда
		Для Каждого строкаСпортсмен Из тзОтчисленныеСпортсмены Цикл
			Если строкаСпортсмен.Спортсмен.СтатусСпортсмена <> Перечисления.статусыСпортсменов.СпортсменДругогоСпортивногоУчреждения Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Спортсмен %1 не обучается в спортивном учреждении (не зачислен или отчислен из учреждения).'"), строкаСпортсмен.Спортсмен);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Отказ = Истина;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	// }Рарус adilas #11666 -Тестирование. Пункт №2 2020.12.11
	
КонецПроцедуры

// }Рарус adilas #11068 -Возрастные группы в соревновании 2020.12.07

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли