#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	ЗаполнитьТаблицуСпортсменов(Параметры.СтруктураПараметров);
	
	Для Каждого Параметр Из Параметры.СтруктураПараметров Цикл
		// {Рарус adilas #1.0.0.2 -Испрваления SonarQube 2021.04.23
		// Удалены/добавлены пробелы
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокЭтапов, Параметр.Ключ, Параметр.Значение, Истина);
		Если Параметр.Ключ = "ОФП" Тогда
			ОФП = Параметр.Значение;
		КонецЕсли;
		Если Параметр.Ключ = "Тренер" ИЛИ Параметр.Ключ = "Этап" Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокЭтапов, Параметр.Ключ, Параметр.Значение, ВидСравненияКомпоновкиДанных.Равно, , Истина);
		// }Рарус adilas #1.0.0.2 -Исправления SonarQube 2021.04.23
		КонецЕсли;	
	КонецЦикла;	
		
	УстановитьОтборНаДинамическийСписок();
	
	Если Не УчетСпортсменовВызовСервера.ТекущиеПараметрыФО(Элементы.СписокКомандСпортивноеУчреждение).ВозрастныеГруппыОрганизация Тогда
		Элементы.СписокСпортсменовМеждународнаяВозрастнаяГруппа.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбор(Команда)
	массивВыделенныхСтрок = Элементы.СписокЭтапов.ВыделенныеСтроки;
	Если массивВыделенныхСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ВыбратьУчастниковПродолжить(массивВыделенныхСтрок);
КонецПроцедуры

&НаКлиенте
Процедура ВыборСпортсменов(Команда)
	массивВыделенныхСтрок = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.СписокСпортсменов.ВыделенныеСтроки Цикл
		строки = Элементы.СписокСпортсменов.ДанныеСтроки(ВыделеннаяСтрока);
		массивВыделенныхСтрок.Добавить(строки.Спортсмен);
	КонецЦикла;
	Если массивВыделенныхСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ОповеститьОВыборе(ДинамическийСписокВТаблицуЗначений("СписокСпортсменов", массивВыделенныхСтрок));
КонецПроцедуры

#КонецОбласти

// {Рарус adilas #1.0.0.2 -Испрваления SonarQube 2021.04.23
// Переименована область и имена процедур
#Область ОбработчикиСобытийЭлементовДинамическогоСпискаСписокЭтапов

&НаКлиенте
Процедура СписокЭтаповВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	МассивУчастников = Новый Массив;
	МассивУчастников.Добавить(ВыбраннаяСтрока);
	ВыбратьУчастниковПродолжить(МассивУчастников);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЭтаповПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элементы.СписокЭтапов.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		ТекущаяКомандаСписка = Неопределено;
	Иначе
		ТекущаяКомандаСписка = ТекДанные.Регистратор;
	КонецЕсли;
	Если ТекущаяКомандаСписка <> ТекущийДокумент Тогда
		ТекущийДокумент = ТекущаяКомандаСписка;
		УстановитьОтборНаДинамическийСписок();
	КонецЕсли;
	
КонецПроцедуры

// }Рарус adilas #1.0.0.2 -Исправления SonarQube 2021.04.23

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьУчастниковПродолжить(массивВыделенныхСтрок)
	ОповеститьОВыборе(ДинамическийСписокВТаблицуЗначений("СписокЭтапов", массивВыделенныхСтрок));  
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборНаДинамическийСписок()
	// {Рарус adilas #1.0.0.2 -Испрваления SonarQube 2021.04.23	
	Элементы.СписокСпортсменов.ОтборСтрок = Новый ФиксированнаяСтруктура("Регистратор", ТекущийДокумент);	
	// }Рарус adilas #1.0.0.2 -Исправления SonarQube 2021.04.23
КонецПроцедуры

&НаСервере
Функция ДинамическийСписокВТаблицуЗначений(ИмяСписка, массивДокументов)
	
	Если ИмяСписка = "СписокСпортсменов" Тогда
		ЗначениеФильтра = "Спортсмен";
	Иначе
		ЗначениеФильтра = "Регистратор";
	КонецЕсли;
	
	МассивВыделенныхСтрок = Новый Массив;
	Для Каждого строкаРезультат Из СписокСпортсменов Цикл
	// {Рарус adilas #1.0.0.2 -Испрваления SonarQube 2021.04.23
		Если НЕ массивДокументов.Найти(строкаРезультат[ЗначениеФильтра]) = Неопределено Тогда
		
			Если ОФП Тогда
			   структура = Новый Структура("Спортсмен, МеждународнаяВозрастнаяГруппа, Этап, Тренер, Пол, ГодРождения"
			     , строкаРезультат.Спортсмен, строкаРезультат.МеждународнаяВозрастнаяГруппа, строкаРезультат.Регистратор.Группа, строкаРезультат.Регистратор.Тренер, строкаРезультат.Пол, строкаРезультат.ГодРождения);
			   МассивВыделенныхСтрок.Добавить(структура);
			Иначе	
			   МассивВыделенныхСтрок.Добавить(строкаРезультат.Спортсмен);
			КонецЕсли;   
		КонецЕсли;	
	КонецЦикла;
	
	Возврат МассивВыделенныхСтрок;
	// }Рарус adilas #1.0.0.2 -Исправления SonarQube 2021.04.23
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуСпортсменов(ПараметрыДанных)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ПараметрыДанных.Организация);
	Запрос.УстановитьПараметр("УчебныйГод" , ПараметрыДанных.УчебныйГод);
	Запрос.УстановитьПараметр("ВидСпорта"  , ПараметрыДанных.ВидСпорта);
	Запрос.УстановитьПараметр("Текущаядата", ПараметрыДанных.Текущаядата);
	Запрос.УстановитьПараметр("СтатусСпортсмена", Перечисления.СтатусыСпортсменов.СпортсменДругогоСпортивногоУчреждения);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СоставУчащихсяСпортивногоУчрежденияСрезПоследних.Спортсмен КАК Спортсмен,
	               |	СоставУчащихсяСпортивногоУчрежденияСрезПоследних.Организация КАК Организация
	               |ПОМЕСТИТЬ ТЗСоставУчащихся
	               |ИЗ
	               |	РегистрСведений.СоставУчащихсяСпортивногоУчреждения.СрезПоследних(&ТекущаяДата, Организация = &Организация) КАК СоставУчащихсяСпортивногоУчрежденияСрезПоследних
	               |ГДЕ
	               |	(СоставУчащихсяСпортивногоУчрежденияСрезПоследних.ДатаОкончанияОбучения = ДАТАВРЕМЯ(1, 1, 1))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВозрастныеГруппы.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	               |	ВозрастныеГруппы.Спортсмен КАК Спортсмен
	               |ПОМЕСТИТЬ ТЗВозрастныеГруппы
	               |ИЗ
	               |	РегистрСведений.МеждународныеВозрастныеГруппы.СрезПоследних(
	               |			,
	               |			Организация = &Организация
	               |				И УчебныйГод = &УчебныйГод
	               |				И ВидСпорта = &ВидСпорта) КАК ВозрастныеГруппы
                   |ГДЕ
                   |    %1
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВозрастныеГруппы.МеждународнаяВозрастнаяГруппа,
	               |	ВозрастныеГруппы.Спортсмен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЭтапыСпортивнойПодготовкиСпортсменов.Регистратор КАК Регистратор,
	               |	ЭтапыСпортивнойПодготовкиСпортсменов.Спортсмен КАК Спортсмен
	               |ПОМЕСТИТЬ ТЗЭтапыСпортПодготовки
	               |ИЗ
	               |	РегистрСведений.ЭтапыСпортивнойПодготовкиСпортсменов.СрезПоследних(
	               |			,
	               |			Организация = &Организация
	               |				И ВидСпорта = &ВидСпорта) КАК ЭтапыСпортивнойПодготовкиСпортсменов
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЭтапыСпортивнойПодготовкиСпортсменов.Регистратор,
	               |	ЭтапыСпортивнойПодготовкиСпортсменов.Спортсмен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Спортсмены.Ссылка КАК Спортсмен,
	               |	Спортсмены.ФизическоеЛицо.ГодРождения КАК ГодРождения,
	               |	ВЫБОР
	               |		КОГДА ФизическиеЛица.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Мужской)
	               |			ТОГДА ""М""
	               |		КОГДА ФизическиеЛица.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Женский)
	               |			ТОГДА ""Ж""
	               |		ИНАЧЕ ""Не задан""
	               |	КОНЕЦ КАК Пол,
	               |	ТЗВозрастныеГруппы.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	               |	ТЗЭтапыСпортПодготовки.Регистратор КАК Регистратор
	               |ИЗ
	               |	ТЗСоставУчащихся КАК ТЗСоставУчащихся
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спортсмены КАК Спортсмены
	               |			ЛЕВОЕ СОЕДИНЕНИЕ ТЗВозрастныеГруппы КАК ТЗВозрастныеГруппы
	               |			ПО Спортсмены.Ссылка = ТЗВозрастныеГруппы.Спортсмен
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |			ПО Спортсмены.ФизическоеЛицо = ФизическиеЛица.Ссылка
	               |			ЛЕВОЕ СОЕДИНЕНИЕ ТЗЭтапыСпортПодготовки КАК ТЗЭтапыСпортПодготовки
	               |			ПО (ТЗЭтапыСпортПодготовки.Спортсмен = Спортсмены.Ссылка)
	               |		ПО ТЗСоставУчащихся.Спортсмен = Спортсмены.Ссылка
	               |ГДЕ
                   |    Спортсмены.СтатусСпортсмена <> &СтатусСпортсмена И
                   |    %2
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Спортсмены.Ссылка,
	               |	ТЗВозрастныеГруппы.МеждународнаяВозрастнаяГруппа,
	               |	Спортсмены.ФизическоеЛицо.ГодРождения,
	               |	ВЫБОР
	               |		КОГДА ФизическиеЛица.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Мужской)
	               |			ТОГДА ""М""
	               |		КОГДА ФизическиеЛица.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Женский)
	               |			ТОГДА ""Ж""
	               |		ИНАЧЕ ""Не задан""
	               |	КОНЕЦ,
	               |	ТЗЭтапыСпортПодготовки.Регистратор";
	// {Рарус adilas #1.0.0.2 -SonarQube 2021.04.23
    Если УчетСпортсменовВызовСервера.ТекущиеПараметрыФО(Элементы.СписокКомандСпортивноеУчреждение).ВозрастныеГруппыОрганизация Тогда
        // {Рарус kotana #IN-9625 2021.06.04 -Ошибка при заполнении табличной части ведомости КПН
        //Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ?(ПараметрыДанных.Свойство("СписокВозрастныхГрупп"), " ВозрастныеГруппы.МеждународнаяВозрастнаяГруппа В(&МеждународнаяВозрастнаяГруппа) И", "Истина"));
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ?(ПараметрыДанных.Свойство("СписокВозрастныхГрупп"), " ВозрастныеГруппы.МеждународнаяВозрастнаяГруппа В(&МеждународнаяВозрастнаяГруппа)", "Истина"));
        // }Рарус kotana #IN-9625 2021.06.04

    Иначе
        // {Рарус kotana #IN-9625 2021.06.17 -Ошибка при заполнении табличной части ведомости КПН
        //Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ?(ПараметрыДанных.Свойство("СписокВозрастныхГрупп"), "", "Истина"));
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", "Истина");
        // }Рарус kotana #IN-9625 2021.06.17
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ?(ПараметрыДанных.Свойство("Пол"), " ФизическиеЛица.Пол В(&СписокПол)", "Истина"));	
	
	Если ПараметрыДанных.Свойство("СписокВозрастныхГрупп") Тогда
		Запрос.УстановитьПараметр("МеждународнаяВозрастнаяГруппа", ПараметрыДанных.СписокВозрастныхГрупп);
	КонецЕсли;
	Если ПараметрыДанных.Свойство("Пол") Тогда
		Запрос.УстановитьПараметр("СписокПол", ПараметрыДанных.Пол);
	КонецЕсли;
	
	СписокСпортсменов.Загрузить(Запрос.Выполнить().Выгрузить());
	// }Рарус adilas #1.0.0.2 -SonarQube 2021.04.23

КонецПроцедуры

#КонецОбласти