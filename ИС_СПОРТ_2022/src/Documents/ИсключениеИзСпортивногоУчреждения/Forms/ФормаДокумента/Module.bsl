
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// {Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	// {Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	Если РольДоступна("ПолныеПрава") Тогда
		 Элементы.Ответсвенный.Доступность = Истина;
	Иначе 	 
		 Элементы.Ответсвенный.Доступность = Ложь;
	КонецЕсли;
	// }Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	// }Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	
	// {Рарус adilas #10427 -Исключение списком 2020.10.21
	Если Параметры.Свойство("Спортсмен") Тогда
		СтрокаСпортсмен = Объект.Спортсмены.Добавить();
		СтрокаСпортсмен.Спортсмен = Параметры.Спортсмен;
	КонецЕсли;
	// }Рарус adilas #10427 -Исключение списком 2020.10.21
	
	// {Рарус adilas #10700 -Вид спорта подставлять по текущему этапу спортсмена 2020.11.05
	Если Объект.Ссылка.Пустая() Тогда
		УчетСпортсменовСервер.ЗаполнитьНастройкиПоУмолчанию(Объект);
	КонецЕсли;
	// }Рарус adilas #10700 -Вид спорта подставлять по текущему этапу спортсмена 2020.11.05
	
	
	// {Рарус adilas #13392 -Номер документа 2021.02.12
	Если Пользователи.РолиДоступны("АдминистраторСистемы, ПолныеПрава") Тогда
		Элементы.Номер.Доступность = Истина;
		Элементы.Номер.ТолькоПросмотр = Ложь;
	КонецЕсли;
	// }Рарус adilas #13392 -Номер документа 2021.02.12
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСпортсмены

&НаКлиенте
Процедура СпортсменыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование) 
	
	Если ЗначениеЗаполнено(Объект.ДатаДокумента) Тогда
		Если НоваяСтрока Тогда
			Элемент.ТекущиеДанные.ДатаОкончанияОбучения = Объект.ДатаДокумента;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// {Рарус adilas #19051 -Отчисление из спортивного учреждения. Статус. 2021.08.16
&НаКлиенте
Процедура СпортсменыСпортсменНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	
	Если Объект.ДатаДокумента = Дата(1, 1, 1) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Укажите дату приказа!'");
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Поле = "ДатаДокумента";
		Сообщение.Сообщить();
		Возврат;	
	ИначеЕсли Не ЗначениеЗаполнено(Объект.Организация) Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Укажите спортивное учреждение!'");
	    Сообщение.ПутьКДанным = "Объект";
		Сообщение.Поле = "Организация";
		Сообщение.Сообщить();
		Возврат;
	Иначе
		ПараметрыФормы.Вставить("ДатаСреза", УчетСпортсменовВызовСервера.ТекущаяДатаНаСервере());
	    ПараметрыФормы.Вставить("Организация", Объект.Организация);
	КонецЕсли;

	ПараметрыФормы.Вставить("ТолькоВыбор", Истина);
	ОбработчикВыбора = Новый ОписаниеОповещения("СпортсменНачалоВыбораЗавершение", ЭтотОбъект);
		
	ОткрытьФорму("Справочник.Спортсмены.Форма.ФормаВыбораСоставУчащихся", ПараметрыФормы, ЭтаФорма, , , , ОбработчикВыбора);	
	
КонецПроцедуры
// }Рарус adilas #19051 -Отчисление из спортивного учреждения. Статус. 2021.08.16

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// {Рарус adilas #10427 -Исключение списком 2020.10.21
&НаКлиенте
Процедура ДатаДокументаПриИзменении(Элемент)
	
	Для Каждого СтрокаСпортсмен Из Объект.Спортсмены Цикл
		СтрокаСпортсмен.ДатаОкончанияОбучения = Объект.ДатаДокумента;	
	КонецЦикла;
	
КонецПроцедуры
// }Рарус adilas #10427 -Исключение списком 2020.10.21

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьДатой(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаДаты", ЭтотОбъект);
    ПоказатьВводДаты(Оповещение, , "Введите дату приказа");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаДаты(Дата, Параметры) Экспорт
 
	Если Не Дата = Неопределено Тогда
		Для Каждого Строка Из Объект.Спортсмены Цикл
			Строка.ДатаОкончанияОбучения = Дата;  
		КонецЦикла;
    КонецЕсли;
 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// {Рарус adilas #19051 -Отчисление из спортивного учреждения. Статус. 2021.08.16
&НаКлиенте
Процедура СпортсменНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт  
	
	Если Не Результат = Неопределено Тогда
		
		ТекущийСпортсмен = Элементы.Спортсмены.ТекущиеДанные;
		ТекущийСпортсмен.Спортсмен = Результат.Спортсмен;
		
	КонецЕсли;
	
КонецПроцедуры
// }Рарус adilas #19051 -Отчисление из спортивного учреждения. Статус. 2021.08.16

#КонецОбласти