
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Отказ = Ложь;
	
	МассивДатаОкончанияОбучения = Новый Массив;
	
	Для Каждого Строка Из ДокументСсылка.Спортсмены Цикл
		МассивДатаОкончанияОбучения.Добавить(Строка.ДатаОкончанияОбучения);
	КонецЦикла;
	
	// {Рарус adilas #10427 -Исключение списком 2020.10.21
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"     	, ДокументСсылка.Ссылка);
	Запрос.УстановитьПараметр("Организация"	, ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("ВидСпорта"   , ДокументСсылка.ВидСпорта);
	Запрос.УстановитьПараметр("ДатаПриказа" , ДокументСсылка.ДатаДокумента);	
	Запрос.УстановитьПараметр("НомерПриказа", ДокументСсылка.НомерДокумента);
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	""СоставУчащихсяСпортивногоУчреждения"" КАК ИмяРегистра_,
	                |	СоставУчащихсяСпортивногоУчреждения.Спортсмен КАК Спортсмен,
	                |	СоставУчащихсяСпортивногоУчреждения.ДатаОкончанияОбучения КАК ДатаОкончанияОбучения,
	                |	&Организация КАК Организация,
	                |	&ВидСпорта КАК ВидСпорта,
	                |	СоставУчащихсяСпортивногоУчреждения.ДатаОкончанияОбучения КАК Период,
	                |	&ДатаПриказа КАК ДатаПриказа,
	                |	&НомерПриказа КАК НомерПриказа
	                |ИЗ
	                |	Документ.ИсключениеИзСпортивногоУчреждения.Спортсмены КАК СоставУчащихсяСпортивногоУчреждения
	                |ГДЕ
	                |	СоставУчащихсяСпортивногоУчреждения.Ссылка = &Ссылка";
	
	// }Рарус adilas #10427 -Исключение списком 2020.10.21
	ТаблицаСоставУчащихся = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаСоставУчащихсяСпортивногоУчреждения", ТаблицаСоставУчащихся);
	
    ДополнительныеСвойства.Вставить("Отказ", Отказ);	
	
КонецПроцедуры	

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ИсключениеИзСпортивногоУчреждения.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольПередПроведением(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ИсключениеИзСпортивногоУчреждения.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовОтменыПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ИсключениеИзСпортивногоУчреждения.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

// {Рарус dotere #17262 -Печатная форма 2021.07.02

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ИсключениеИзСпортивногоУчереждения") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПФ_MXL_ИсключениеИзСпортивногоУчереждения", "ПриказИсключение", СформироватьПечатнуюФормуЗаявка(МассивОбъектов));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуЗаявка(МассивОбъектовДляПечати) Экспорт
	
	Если ТипЗнч(МассивОбъектовДляПечати) = Тип("Массив") Тогда
		
		МассивОбъектов = МассивОбъектовДляПечати;
		
	Иначе
		
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(МассивОбъектовДляПечати);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсключениеИзСпортивногоУчреждения.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Данные
		|ИЗ
		|	Документ.ИсключениеИзСпортивногоУчреждения КАК ИсключениеИзСпортивногоУчреждения
		|ГДЕ
		|	ИсключениеИзСпортивногоУчреждения.Ссылка В(&МассивОбъектов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыСпорта.Ссылка КАК ВидСпорта,
		|	ИсключениеИзСпортивногоУчрежденияСпортсмены.ДатаОкончанияОбучения КАК ДатаОкончанияОбучения,
		|	ВТ_Данные.Ссылка КАК Ссылка,
		|	ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних.Этап КАК Этап,
		|	ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних.Тренер КАК Тренер,
		|	ИсключениеИзСпортивногоУчрежденияСпортсмены.Спортсмен КАК Спортсмен
		|ПОМЕСТИТЬ ВТ_ТаблицаДляИтогов
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыСпорта КАК ВидыСпорта
		|		ПО ВТ_Данные.Ссылка.ВидСпорта = ВидыСпорта.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИсключениеИзСпортивногоУчреждения.Спортсмены КАК ИсключениеИзСпортивногоУчрежденияСпортсмены
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭтапыСпортивнойПодготовкиСпортсменов.СрезПоследних КАК ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних
		|			ПО ИсключениеИзСпортивногоУчрежденияСпортсмены.Спортсмен = ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних.Спортсмен
		|		ПО ВТ_Данные.Ссылка = ИсключениеИзСпортивногоУчрежденияСпортсмены.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.Спортсмен КАК Спортсмен,
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.ВидСпорта КАК ВидСпорта,
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.Разряд КАК Разряд
		|ПОМЕСТИТЬ ВТ_Разряды
		|ИЗ
		|	РегистрСведений.ПодтвержденныеРазрядыСпортсменов.СрезПоследних(, ) КАК ПодтвержденныеРазрядыСпортсменовСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.Спортсмен,
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.ВидСпорта,
		|	ПодтвержденныеРазрядыСпортсменовСрезПоследних.Разряд
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ТаблицаДляИтогов.Ссылка КАК Ссылка,
		|	ВТ_ТаблицаДляИтогов.ВидСпорта КАК ВидСпорта,
		|	ВТ_ТаблицаДляИтогов.ДатаОкончанияОбучения КАК ДатаОкончанияОбучения,
		|	ВТ_Разряды.Разряд КАК Разряд,
		|	ФизическиеЛица.ГодРождения КАК ГодРождения,
		|	ВТ_ТаблицаДляИтогов.Этап КАК Этап,
		|	ВТ_ТаблицаДляИтогов.Тренер КАК Тренер,
		|	ВТ_ТаблицаДляИтогов.Спортсмен КАК Спортсмен
		|ИЗ
		|	ВТ_ТаблицаДляИтогов КАК ВТ_ТаблицаДляИтогов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Разряды КАК ВТ_Разряды
		|		ПО ВТ_ТаблицаДляИтогов.ВидСпорта = ВТ_Разряды.ВидСпорта
		|			И ВТ_ТаблицаДляИтогов.Спортсмен = ВТ_Разряды.Спортсмен
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ПО ВТ_ТаблицаДляИтогов.Спортсмен.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|ИТОГИ ПО
		|	Ссылка,
		|	ДатаОкончанияОбучения,
		|	ВидСпорта,
		|	Этап,
		|	Тренер";
	
	ПутьКМакету = "Документ.ИсключениеИзСпортивногоУчреждения.ПФ_MXL_ИсключениеИзСпортивногоУчереждения";
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету);
	
	ТабДок = Новый ТабличныйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьДатыОтчисления = Макет.ПолучитьОбласть("ОбластьДатаОтчисления");
	ОбластьВидСпорта = Макет.ПолучитьОбласть("ОбластьВидСпорта");
	ОбластьЭтап = Макет.ПолучитьОбласть("ОбластьЭтап");
	ОбластьСпортсмен 	= Макет.ПолучитьОбласть("ОбластьСпортсмен");
	ОбластьТренер 	= Макет.ПолучитьОбласть("ОбластьТренер");
	ОбластьОснование 	= Макет.ПолучитьОбласть("ОбластьОснование");
	ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал");

	Если Не РезультатЗапроса.Пустой() Тогда
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСсылка.Следующий() Цикл
			
			
			ОбластьШапка.Параметры.ПолноеНаименование 	= ВРег(ВыборкаСсылка.Ссылка.Организация.ПолноеНаименование);
			ОбластьШапка.Параметры.НомерПриказа 		= ВыборкаСсылка.Ссылка.НомерДокумента;
			// {Рарус adilas #17262 - 2021.07.16
			ОбластьШапка.Параметры.ДатаДокумента = Формат(ВыборкаСсылка.Ссылка.ДатаДокумента, "ДЛФ=D");
			// }Рарус adilas #17262 - 2021.07.16
				
			ТабДок.Вывести(ОбластьШапка);
			
			ВыборкаДатыОтчисления = ВыборкаСсылка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			
			Пока ВыборкаДатыОтчисления.Следующий() Цикл
				
				ОбластьДатыОтчисления.Параметры.ДатаОтчисления = Формат(ВыборкаДатыОтчисления.ДатаОкончанияОбучения,"ДЛФ=DD");
				Если ВыборкаДатыОтчисления.Ссылка.ПричинаОтчисления = Перечисления.ПричиныОтчисления.Переезд Тогда
					ОбластьДатыОтчисления.Параметры.ПричинаОтчисления = "переездом";
				ИначеЕсли ВыборкаДатыОтчисления.Ссылка.ПричинаОтчисления = Перечисления.ПричиныОтчисления.НарушениеПравилВнутреннегоРаспорядкаУчреждения Тогда
					ОбластьДатыОтчисления.Параметры.ПричинаОтчисления = "нарушением правил внутреннего распорядка следующих";
				ИначеЕсли ВыборкаДатыОтчисления.Ссылка.ПричинаОтчисления = Перечисления.ПричиныОтчисления.НевозможностьПоМедицинскимПоказаниямЗаниматьсяИзбраннымВидомСпорта Тогда
					ОбластьДатыОтчисления.Параметры.ПричинаОтчисления = "невозможностью по медецинским показаниям заниматься избранным видом спорта следующих";
				ИначеЕсли ВыборкаДатыОтчисления.Ссылка.ПричинаОтчисления = Перечисления.ПричиныОтчисления.НепосещениеЗанятий Тогда	
					ОбластьДатыОтчисления.Параметры.ПричинаОтчисления = "непосещением занятий следующих";
				ИначеЕсли ВыборкаДатыОтчисления.Ссылка.ПричинаОтчисления = Перечисления.ПричиныОтчисления.ИныеОбстоятельства Тогда
					ОбластьДатыОтчисления.Параметры.ПричинаОтчисления = "иными обстоятельствами следующих";
				КонецЕсли;
					
				ТабДок.Вывести(ОбластьДатыОтчисления);
				
				ВыборкаВидСпорта = ВыборкаДатыОтчисления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаВидСпорта.Следующий() Цикл
					
										
					ОбластьВидСпорта.Параметры.ВидСпорта = ТРег(ВыборкаВидСпорта.ВидСпорта);
					
					ТабДок.Вывести(ОбластьВидСпорта);
					
					ВыборкаЭтап = ВыборкаВидСпорта.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Пока ВыборкаЭтап.Следующий() Цикл
						
						
						ОбластьЭтап.Параметры.Этап = ВыборкаЭтап.Этап;
						
						ТабДок.Вывести(ОбластьЭтап);
						
						НомерСтрокиСпр = 1;
						
						ВыборкаТренер = ВыборкаЭтап.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаТренер.Следующий() Цикл 
							
							ВыборкаДетальныеЗаписи = ВыборкаТренер.Выбрать();
							
							Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
								Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Спортсмен) Тогда
								
									ОбластьСпортсмен.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
									ОбластьСпортсмен.Параметры.Номер 	= Строка(НомерСтрокиСпр) + ".";
									ЧастиИмени 												= СтрРазделить(ВыборкаДетальныеЗаписи.Спортсмен, " ", Ложь);
									Фамилия 												= ЧастиИмени[0];
									Имя 													= ЧастиИмени[1];
									ОбластьСпортсмен.Параметры.Спортсмен 		= Фамилия + " " + Имя;
									НомерСтрокиСпр = НомерСтрокиСпр + 1;
									
									ТабДок.Вывести(ОбластьСпортсмен);
								КонецЕсли;
							КонецЦикла;	
							Если ЗначениеЗаполнено(ВыборкаТренер.Тренер) Тогда
								ОбластьТренер = Макет.ПолучитьОбласть("ОбластьТренер");
								ОбластьТренер.Параметры.Тренер = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаТренер.Тренер.ФизическоеЛицо);
								
								ТабДок.Вывести(ОбластьТренер);
							КонецЕсли;
							ОбластьОснование.Параметры.ОснованиеЗаявления = ВыборкаСсылка.Ссылка.ОснованиеОтчисления;
							ТабДок.Вывести(ОбластьОснование);
						КонецЦикла
					КонецЦикла
					
				КонецЦикла
				
			КонецЦикла;
			
 
			ОбластьПодвал.Параметры.Директор = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаСсылка.Ссылка.Организация.Директор);
			ТабДок.Вывести(ОбластьПодвал);
		КонецЦикла
		
	КонецЕсли;
	
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();	
	
	ТабДок.КлючПараметровПечати	= ПутьКМакету;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб 			= Истина;
	
	Возврат ТабДок;
	
	
КонецФункции	

#КонецОбласти

// }Рарус dotere #17262 -Печатная форма 2021.07.02