#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипРезультата = Параметры.Норматив.ТипРезультата;
	Элементы.РезультатБулево.Видимость = ТипРезультата  = Перечисления.ТипыРезультатов.Булево;
	Элементы.РезультатСтрока.Видимость = ТипРезультата  = Перечисления.ТипыРезультатов.Время;
	Элементы.РезультатЧисло.Видимость  = ТипРезультата  = Перечисления.ТипыРезультатов.Число;
	
	Если ТипРезультата  = Перечисления.ТипыРезультатов.Булево Тогда
		РезультатБулево = ?(Параметры.Результат = Истина, Элементы.РезультатБулево.СписокВыбора[1].Представление,Элементы.РезультатБулево.СписокВыбора[0].Представление);
	ИначеЕсли ТипРезультата  = Перечисления.ТипыРезультатов.Время Тогда
		РезультатСтрока = Параметры.Результат;
	Иначе
		РезультатЧисло = Параметры.Результат;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовФормы

&НаКлиенте
Процедура РезультатСтрокаПриИзменении(Элемент)
	
	Отказ = Ложь;
	УчетСпортсменовОбщегоНазначенияКлиентСервер.ПроверитьРезультатСтроковыйПриИзменении(РезультатСтрока, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Внести(Команда)
	
	СтруктураВозврата = Новый Структура("Результат, РезультатЧисло, РезультатБулево, ТипРезультата");
	СтруктураВозврата.ТипРезультата = ТипРезультата;
	Если ТипРезультата = ПредопределенноеЗначение("Перечисление.ТипыРезультатов.Булево") Тогда
		СтруктураВозврата.Результат = ?(ВРег(РезультатБулево) = "ИСТИНА", "Выполнил", "Не выполнил");
		СтруктураВозврата.РезультатБулево = РезультатБулево;
	ИначеЕсли ТипРезультата = ПредопределенноеЗначение("Перечисление.ТипыРезультатов.Время") Тогда
		
		Отказ = Ложь;
		УчетСпортсменовОбщегоНазначенияКлиентСервер.ПроверитьРезультатСтроковыйПриИзменении(РезультатСтрока,Отказ);
		Если Отказ Тогда
			Возврат
		Иначе	
			СтруктураВозврата.Результат = РезультатСтрока;
			СтруктураВозврата.РезультатЧисло = УчетСпортсменовОбщегоНазначенияКлиентСервер.ПересчитатьВремяИзСтрокиВМиллесекунды(РезультатСтрока);
		КонецЕсли;
		
	Иначе
		СтруктураВозврата.Результат = РезультатЧисло;
		СтруктураВозврата.РезультатЧисло = РезультатЧисло;
	КонецЕсли;
	
	ЭтаФорма.Закрыть(СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти
