
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"            , ДокументСсылка);
	Запрос.УстановитьПараметр("Дата"              , ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("УчебныйГод"        , ДокументСсылка.УчебныйГод);
	Запрос.УстановитьПараметр("Организация"       , ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("Тренер"            , ДокументСсылка.Тренер);
	Запрос.УстановитьПараметр("ВидСпорта"         , ДокументСсылка.ВидСпорта);
	Запрос.Текст =  "ВЫБРАТЬ
	                |	""РезультатыСдачиКонтрольноПереводныхНормативов"" КАК ИмяРегистра_,
	                |	&Дата КАК Период,
	                |	&УчебныйГод КАК УчебныйГод,
	                |	&Организация КАК Организация,
	                |	&ВидСпорта КАК ВидСпорта,
	                |	&Тренер КАК Тренер,
	                |	Нормативы.Норматив КАК Норматив,
	                |	Нормативы.УникальныйИдентификаторСтрокиНорматива КАК УникальныйИдентификаторСтрокиНорматива,
	                |	СоставУчастников.УникальныйИдентификаторИтоговогоБалла КАК УникальныйИдентификаторИтоговогоБалла,
	                |	СоставУчастников.Спортсмен КАК Спортсмен,
					|	СоставУчастников.ТекущийЭтап КАК ТекущийЭтап,
					|	СоставУчастников.СледующийЭтап КАК СледующийЭтап,
	                |	СоставУчастников.Группа КАК Группа,
	                |	СоставУчастников.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	                |	СоставУчастников.РезультатЧисло КАК РезультатЧисло,
	                |	СоставУчастников.РезультатБулево КАК РезультатБулево,
	                |	СоставУчастников.РезультатСдачиНорматива КАК РезультатСдачиНорматива,
	                |	СоставУчастников.Балл КАК Балл
	                |ИЗ
	                |	Документ.РезультатыСдачиКонтрольноПереводныхНормативов.Нормативы КАК Нормативы
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РезультатыСдачиКонтрольноПереводныхНормативов.СоставУчастников КАК СоставУчастников
	                |		ПО Нормативы.УникальныйИдентификаторСтрокиНорматива = СоставУчастников.УникальныйИдентификаторСтрокиНорматива
	                |ГДЕ
	                |	Нормативы.Ссылка = &Ссылка
	                |	И НЕ СоставУчастников.Ссылка ЕСТЬ NULL
	                |	И (СоставУчастников.РезультатСдачиНорматива = ""Сдал""
	                |			ИЛИ СоставУчастников.РезультатСдачиНорматива = ""Не сдал"")";
	
	
	ТаблицаРезультатыСдачиКонтрольноПереводныхНормативов = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаРезультатыСдачиКонтрольноПереводныхНормативов", ТаблицаРезультатыСдачиКонтрольноПереводныхНормативов);
	ТаблицыДляДвижений.Вставить("ТаблицаИтоговыеРезультатыСдачиКонтрольноПереводныхНормативов", ТаблицаРезультатыСдачиКонтрольноПереводныхНормативов);
	
    ДополнительныеСвойства.Вставить("Отказ", Отказ);
	
КонецПроцедуры	

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.РезультатыСдачиКонтрольноПереводныхНормативов.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольПередПроведением(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.РезультатыСдачиКонтрольноПереводныхНормативов.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовОтменыПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.РезультатыСдачиКонтрольноПереводныхНормативов.СоздатьДокумент();
	КонецЕсли;
    
    ОтменитьПроведениеСвзанныхДокументов(Объект.Ссылка);
    
КонецПроцедуры

Процедура ОтменитьПроведениеСвзанныхДокументов(СсылкаНаОбъект)
  
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |   ПереводСпортсменовНаЭтап.Ссылка КАК Ссылка
        |ИЗ
        |   Документ.ПереводСпортсменовНаЭтап КАК ПереводСпортсменовНаЭтап
        |ГДЕ
        |   ПереводСпортсменовНаЭтап.ДокументОснование = &ДокументОснование
        |   И ПереводСпортсменовНаЭтап.Проведен = ИСТИНА
        |
        |ОБЪЕДИНИТЬ ВСЕ
        |
        |ВЫБРАТЬ
        |   ЗачислениеСпортсменовНаЭтап.Ссылка
        |ИЗ
        |   Документ.ЗачислениеСпортсменовНаЭтап КАК ЗачислениеСпортсменовНаЭтап
        |ГДЕ
        |   ЗачислениеСпортсменовНаЭтап.ДокументОснование = &ДокументОснование
        |   И ЗачислениеСпортсменовНаЭтап.Проведен = ИСТИНА";
    
    Запрос.УстановитьПараметр("ДокументОснование", СсылкаНаОбъект);
    
    РезультатЗапроса = Запрос.Выполнить();
    
    Если НЕ РезультатЗапроса.Пустой() Тогда
        
        ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
        
        ТекстСообщения = "Отменено проведение связанных документов:" + Символы.ПС;
        
        Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
            
            СсылкаНаДокументОтменыПроведения = ВыборкаДетальныеЗаписи.Ссылка;
            ОбъектДокументОтменыПроведения = СсылкаНаДокументОтменыПроведения.ПолучитьОбъект();
            ОбъектДокументОтменыПроведения.Записать(РежимЗаписиДокумента.ОтменаПроведения);
            ТекстСообщения = ТекстСообщения + СсылкаНаДокументОтменыПроведения + Символы.ПС;
            
        КонецЦикла;
        
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = ТекстСообщения;
        Сообщение.Сообщить(); 
    КонецЕсли; 

КонецПроцедуры
 
// {Рарус dotere #16629 -Печатная форма 2021.08.24

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РезульататыТабДок") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РезульататыТабДок", "Результаты", СформироватьПечатнуюФормуЗаявка(МассивОбъектов));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуЗаявка(МассивОбъектовДляПечати) Экспорт
	
	Если ТипЗнч(МассивОбъектовДляПечати) = Тип("Массив") Тогда
		
		МассивОбъектов = МассивОбъектовДляПечати;
		
	Иначе
		
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(МассивОбъектовДляПечати);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РезультатыСдачиКонтрольноПереводныхНормативов.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Документы
		|ИЗ
		|	Документ.РезультатыСдачиКонтрольноПереводныхНормативов КАК РезультатыСдачиКонтрольноПереводныхНормативов
		|ГДЕ
		|	РезультатыСдачиКонтрольноПереводныхНормативов.Ссылка В(&МассивОбъектов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Документы.Ссылка КАК Ссылка,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Спортсмен КАК Спортсмен,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Норматив КАК Норматив,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.РезультатСдачиНорматива КАК РезультатСдачиНорматива,
		|	ФизическиеЛица.ГодРождения КАК ГодРождения,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.РезультатЧисло КАК РезультатЧисло,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.РезультатБулево КАК РезультатБулево,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Балл КАК Балл,
		|	Нормативы.ТипРезультата КАК ТипРезультата,
		|	СоставГруппыСрезПоследних.Группа КАК Группа,
		|	СоставГруппыСрезПоследних.ТренерСборной КАК Тренер,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.УникальныйИдентификаторСтрокиНорматива КАК УникальныйИдентификаторСтрокиНорматива
		|ИЗ
		|	ВТ_Документы КАК ВТ_Документы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РезультатыСдачиКонтрольноПереводныхНормативов.СоставУчастников КАК РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|			ПО РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Спортсмен = ФизическиеЛица.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Нормативы КАК Нормативы
		|			ПО РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Норматив = Нормативы.Ссылка
		|				И РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Норматив = Нормативы.Ссылка
		|		ПО ВТ_Документы.Ссылка = РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставГруппы.СрезПоследних КАК СоставГруппыСрезПоследних
		|		ПО (РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Спортсмен = СоставГруппыСрезПоследних.Спортсмен)
		|
		|СГРУППИРОВАТЬ ПО
		|	СоставГруппыСрезПоследних.Группа,
		|	СоставГруппыСрезПоследних.ТренерСборной,
		|	ВТ_Документы.Ссылка,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Спортсмен,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Норматив,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.РезультатСдачиНорматива,
		|	ФизическиеЛица.ГодРождения,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.РезультатЧисло,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.РезультатБулево,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.Балл,
		|	Нормативы.ТипРезультата,
		|	РезультатыСдачиКонтрольноПереводныхНормативовСоставУчастников.УникальныйИдентификаторСтрокиНорматива
		|
		|УПОРЯДОЧИТЬ ПО
		|	УникальныйИдентификаторСтрокиНорматива
		|ИТОГИ ПО
		|	Ссылка,
		|	Группа,
		|	Тренер,
		|	Спортсмен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РезультатыСдачиКонтрольноПереводныхНормативовНормативы.Норматив КАК Норматив,
		|	РезультатыСдачиКонтрольноПереводныхНормативовНормативы.Ссылка КАК Ссылка,
		|	РезультатыСдачиКонтрольноПереводныхНормативовНормативы.УникальныйИдентификаторСтрокиНорматива КАК УникальныйИдентификаторСтрокиНорматива
		|ИЗ
		|	Документ.РезультатыСдачиКонтрольноПереводныхНормативов.Нормативы КАК РезультатыСдачиКонтрольноПереводныхНормативовНормативы
		|ГДЕ
		|	РезультатыСдачиКонтрольноПереводныхНормативовНормативы.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	УникальныйИдентификаторСтрокиНорматива";
	
	ПутьКМакету = "Документ.РезультатыСдачиКонтрольноПереводныхНормативов.РезульататыТабДок";
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету);
	
	ТабДок = Новый ТабличныйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ОбластьШапкаТаблицы");
	ОбластьНорматив = Макет.ПолучитьОбласть("ОбластьНорматив");
	ОбластьДанные = Макет.ПолучитьОбласть("ОбластьДанные");
	ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал");
	ОбластьНормативРезультат = Макет.ПолучитьОбласть("ОбластьНормативРезультат");
	ОбластьСуммаБалловНадпись = Макет.ПолучитьОбласть("ОбластьСуммаБалловНадпись");
	ОбластьСуммаБаллов = Макет.ПолучитьОбласть("ОбластьСуммаБаллов");
	ОбластьГруппа = Макет.ПолучитьОбласть("ОбластьГруппа");
	ОбластьДатаПроведения = Макет.ПолучитьОбласть("ОбластьДатаПроведения");
	ОбластьТренер = Макет.ПолучитьОбласть("ОбластьТренер");
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоЗапросов = РезультатЗапроса.Количество();
	
	ВыборкаСсылка = РезультатЗапроса[КоличествоЗапросов - 2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаНормативы = РезультатЗапроса[КоличествоЗапросов - 1].Выбрать();
	
	СтруктураПоиска = Новый Структура("Ссылка");
	ВысотаТабДока = 7;
		
		Пока ВыборкаСсылка.Следующий() Цикл
			 ОбластьШапка.Параметры.Организация = ВыборкаСсылка.Ссылка.Организация;
			 ОбластьШапка.Параметры.ВидСпорта = ВыборкаСсылка.Ссылка.ВидСпорта;
			 ВыборкаГруппа = ВыборкаСсылка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			 ТабДок.Вывести(ОбластьШапка);
			 НомерПрохода=1;
			 Пока ВыборкаГруппа.Следующий() Цикл
				 ОбластьГруппа.Параметры.Группа = ВыборкаГруппа.Группа;
				 ТабДок.Вывести(ОбластьГруппа);
				 ВыборкаТренер = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				 Пока ВыборкаТренер.Следующий() Цикл
					 ОбластьТренер.Параметры.Тренер = ВыборкаТренер.Тренер;
					 ТабДок.Присоединить(ОбластьТренер);
					 Если НомерПрохода = 1 Тогда
					 	ОбластьДатаПроведения.Параметры.ДатаПроведения = Формат(ВыборкаСсылка.Ссылка.Дата,"ДЛФ=DD");
						ТабДок.присоединить(ОбластьДатаПроведения);
					 КонецЕсли;
					 НомерПрохода = НомерПрохода +1;
					 ТабДок.Вывести(ОбластьШапкаТаблицы);
					 СтруктураПоиска.Ссылка = ВыборкаСсылка.Ссылка;
					 Пока ВыборкаНормативы.НайтиСледующий(СтруктураПоиска) Цикл
						 
						 ОбластьНорматив.Параметры.Норматив = ВыборкаНормативы.Норматив;
						 ТабДок.присоединить(ОбластьНорматив);
				 
					 КонецЦикла;
					 ТабДок.присоединить(ОбластьСуммаБалловНадпись);
					 ВыборкаНормативы.Сбросить();
					 КоличествоНормативов = ВыборкаНормативы.Количество();
					 ДлинаНовойОбласти = 8+4*КоличествоНормативов;
					 ОбластьНазвание = ТабДок.Область(1,1,1,ДлинаНовойОбласти);
					 ОбластьНазвание.Объединить();
					 ОбластьОрганизация = ТабДок.Область(2,1,2,ДлинаНовойОбласти);
					 ОбластьОрганизация.Объединить();
					 ОбластьВидСпорта = ТабДок.Область(3,1,3,ДлинаНовойОбласти);
					 ОбластьВидСпорта.Объединить();
					 ОбластьДатаПроведения = ТабДок.Область(5,9,5,ДлинаНовойОбласти);
					 ОбластьДатаПроведения.Объединить();
					 Номер = 1;
					 ВыборкаСпортсмен = ВыборкаТренер.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					 ОбластьНормативыЗаголовок = ТабДок.Область(ВысотаТабДока,7,ВысотаТабДока,ДлинаНовойОбласти);
					 ОбластьНормативыЗаголовок.Объединить();
					 ВысотаТабДока = ВысотаТабДока + ВыборкаСпортсмен.Количество() + 5;
					 Пока ВыборкаСпортсмен.Следующий() Цикл
						ИтоговыйБалл = 0;
					    ОбластьДанные.Параметры.Номер = Номер;
					    Номер = Номер + 1;
					    ОбластьДанные.Параметры.Спортсмен = ВыборкаСпортсмен.Спортсмен;
						ОбластьДанные.Параметры.ДатаРождения = Формат(ВыборкаСпортсмен.Спортсмен.ФизическоеЛицо.ДатаРождения,"ДФ=dd.MM.yyyy");
					    ТабДок.Вывести(ОбластьДанные);
						ВыборкаДетальныеЗаписи = ВыборкаСпортсмен.Выбрать();
						Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
							Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ТипРезультата) Тогда
								Если ВыборкаДетальныеЗаписи.РезультатБулево Или ВыборкаДетальныеЗаписи.РезультатСдачиНорматива = "Не сдал" Тогда
									 ОбластьНормативРезультат.Параметры.НормативРезультаты = ВыборкаДетальныеЗаписи.РезультатСдачиНорматива;
								ИначеЕсли ВыборкаДетальныеЗаписи.ТипРезультата = Перечисления.ТипыРезультатов.Число Тогда
									 ОбластьНормативРезультат.Параметры.НормативРезультаты = ВыборкаДетальныеЗаписи.РезультатЧисло;
								ИначеЕсли ВыборкаДетальныеЗаписи.ТипРезультата = Перечисления.ТипыРезультатов.Время Тогда
									 ОбластьНормативРезультат.Параметры.НормативРезультаты =  УчетСпортсменовОбщегоНазначенияКлиентСервер.ПересчитатьВремяВМиллесекундахВСтроку(ВыборкаДетальныеЗаписи.РезультатЧисло);
								КонецЕсли;
								ТабДок.присоединить(ОбластьНормативРезультат);
								ИтоговыйБалл = ИтоговыйБалл + ВыборкаДетальныеЗаписи.Балл;
								ОбластьНормативРезультат.Параметры.НормативРезультаты = "";
							Иначе
								ОбластьНормативРезультат.Параметры.НормативРезультаты = "";
								ТабДок.присоединить(ОбластьНормативРезультат);
							КонецЕсли;
						КонецЦикла;
						ОбластьСуммаБаллов.Параметры.СуммаБаллов = ИтоговыйБалл;
						ТабДок.присоединить(ОбластьСуммаБаллов);
						КонецЦикла;
				КонецЦикла;
			 КонецЦикла;
			 КоличествоСпортсменов = ВыборкаСпортсмен.Количество();
			 ОбластьПодвал.Параметры.Тренер = Строка(ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ВыборкаСсылка.Ссылка.Тренер.ФизическоеЛицо));
			 ТабДок.Вывести(ОбластьПодвал);
			 ОбластьТренерПодвал = ТабДок.Область(ВысотаТабДока-1,1,ВысотаТабДока-1,ДлинаНовойОбласти);
			 ОбластьТренерПодвал.Объединить();

			 ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		 	 КонецЦикла;	
	
	ТабДок.КлючПараметровПечати	= ПутьКМакету;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб 			= Истина;
	
	Возврат ТабДок;

КонецФункции

#КонецОбласти

// }Рарус dotere #16629 -Печатная форма 2021.08.24