
#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// {Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	// {Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	Если РольДоступна("ПолныеПрава") Тогда
		 Элементы.Ответственный.Доступность = Истина;
	Иначе 	 
		 Элементы.Ответственный.Доступность = Ложь;
	КонецЕсли;
	// }Рарус dotere #21842 -В методите поле ответственный недоступно 2020.11.06
	// }Рарус adilas #23059 -РС Руководитель. Карточка спортсмена.Карточка тренера. 2021.12.13
	
	// {Рарус adilas #9220 -Заполнение реквизита Спортсмен в документах 2020.09.10
	Если Параметры.Свойство("Спортсмен") Тогда
		Объект.Спортсмен = Параметры.Спортсмен;
	КонецЕсли;
	// }Рарус adilas #9220 -Заполнение реквизита Спортсмен в документах 2020.09.10
	
	// {Рарус adilas #10700 -Вид спорта подставлять по текущему этапу спортсмена 2020.11.05
	Если Объект.Ссылка.Пустая() Тогда
		УчетСпортсменовСервер.ЗаполнитьНастройкиПоУмолчанию(Объект);
        
        // {Рарус kotana #IN-7380 -Передача заявлений из мобильного 2021.04.06
        Объект.СтатусЗаявления = Перечисления.СтатусыЗаявленийНаПриемВСпортивноеУчреждение.Принято;
        // }Рарус kotana #IN-7380 -Передача заявлений из мобильного 2021.04.06
        
    КонецЕсли;
	// }Рарус adilas #10700 -Вид спорта подставлять по текущему этапу спортсмена 2020.11.05
	
	// {Рарус adilas #13392 -Номер документа 2021.02.12
	Если Пользователи.РолиДоступны("АдминистраторСистемы, ПолныеПрава") Тогда
		Элементы.Номер.Доступность = Истина;
		Элементы.Номер.ТолькоПросмотр = Ложь;
	КонецЕсли;
	// }Рарус adilas #13392 -Номер документа 2021.02.12
     
    ЗаполнитьРеквизитыФормы();
    УстановитьВидимостьЗакладок();
    
КонецПроцедуры

// {Рарус adilas #18200 -Тестирование релиза Альфа СПОРТ 1.0.0.3. Заявление на прием в спорт. учреждение 2021.07.08
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаЗаписать.Видимость = Ложь;
	
	Если Объект.Ссылка.Пустая() ИЛИ Не (Объект.СогласиеНаОбработкуПерсональныхДанныхПолучено) Тогда
		
		Элементы.ДокументСогласиеНаОбработкуПерсональныхДанных.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
		
	ПроверкаПрошлаУспешно = ЭтаФорма.ПроверитьЗаполнение();
	
	Если ПроверкаПрошлаУспешно И Объект.Ссылка.Пустая() Тогда
	
		Если Не ВыполняетсяЗакрытие И Не Объект.СогласиеНаОбработкуПерсональныхДанныхПолучено Тогда
			
			Отказ = Истина;
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПередЗаписью", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Согласие на обработку персональных данных получено?'"), РежимДиалогаВопрос.ДаНет,0);
			
		КонецЕсли;
		
	ИначеЕсли Не ПроверкаПрошлаУспешно И Объект.Ссылка.Пустая() Тогда 
		
		Отказ = Истина;
		
	Иначе
		
		//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());	
		
	КонецЕсли;
	
КонецПроцедуры
// }Рарус adilas #18200 -Тестирование релиза Альфа СПОРТ 1.0.0.3. Заявление на прием в спорт. учреждение 2021.07.08

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// {Рарус adilas #11811 -Прикреплять согласие на обработку перс. данных к заявлению на прием 2021.02.24
&НаКлиенте
Процедура СогласиеНаОбработкуПерсональныхДанныхПолученоПриИзменении(Элемент)
	
	Если Объект.СогласиеНаОбработкуПерсональныхДанныхПолучено Тогда 	
		
		ОбратботкаРезультатаВыбораСервер();
		
	КонецЕсли;
	
КонецПроцедуры
// }Рарус adilas #11811 -Прикреплять согласие на обработку перс. данных к заявлению на прием 2021.02.24

// {Рарус adilas #11834 -Изменить механизм присоединения файлов для Заявления на прием в учреждение 2021.02.03
&НаКлиенте
Процедура ОткрытьПредоставленныеДокументы(Команда)
	
	СтандартнаяОбработка = Ложь;

	Если Объект.Ссылка.Пустая() Тогда	
		ОбработчикОповещенияОЗакрытии = Новый ОписаниеОповещения("ВопросОНеобходимостиЗаписиПослеЗавершения", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОповещенияОЗакрытии, 
						НСтр("ru = 'Данные еще не записаны. 
						|Переход к ""Присоединенные файлы"" возможен только после записи данных.'"), 
						РежимДиалогаВопрос.ОК);			
	Иначе	
		ПараметрыФормы = Новый Структура;
		
		ПараметрыФормы.Вставить("Объект", Объект);
		
		ОбработчикОповещениеПослеЗакрытия = Новый ОписаниеОповещения("ПослеЗакрытияФормыПредоставленныеДокументы", ЭтотОбъект);
		
		ОткрытьФорму("Документ.ЗаявленияНаПриемВСпортивноеУчреждение.Форма.ФормаЭлементаПредоставления", 
						ПараметрыФормы, ЭтаФорма, , , , ОбработчикОповещениеПослеЗакрытия,);
	КонецЕсли;
	
КонецПроцедуры
// }Рарус adilas #11834 -Изменить механизм присоединения файлов для Заявления на прием в учреждение 2021.02.03

// {Рарус adilas #11842 -Заявка на прием в спортивное учреждение 2021.02.15
&НаКлиенте
Процедура РодительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Спортсмен", Объект.Спортсмен);
	ПараметрыФормы.Вставить("ЭтоВыбор", Истина);
	
	ОповещениеФормы = Новый ОписаниеОповещения("ПослеЗакрытияФормыРодительНачалоВыбора", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.СоставыСемейСпортсменов.ФормаСписка", ПараметрыФормы, ЭтаФорма,,,, ОповещениеФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура РодительСоздание(Элемент, СтандартнаяОбработка)
	    СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Спортсмен", Объект.Спортсмен);
		ОповещениеФормы = Новый ОписаниеОповещения("ПослеЗакрытияФормыРодительСоздание", ЭтотОбъект,ПараметрыФормы);
		ОткрытьФорму("РегистрСведений.СоставыСемейСпортсменов.ФормаЗаписи", ПараметрыФормы, ЭтаФорма,,,,ОповещениеФормы);
КонецПроцедуры
// }Рарус adilas #11842 -Заявка на прием в спортивное учреждение 2021.02.15

&НаКлиенте
Процедура УстановитьСтатусЗаявления(Команда)
    
    Если НЕ Объект.ПолученоИзВнешнегоИсточника Тогда
        
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Статус можно изменить только документам, полученным из внешних источников";
        Сообщение.Сообщить(); 
        
    Иначе
        
        СписокСтатусов = Новый СписокЗначений;
        СписокСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЗаявленийНаПриемВСпортивноеУчреждение.Подготовлено"));
        СписокСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЗаявленийНаПриемВСпортивноеУчреждение.Принято"));
        СписокСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЗаявленийНаПриемВСпортивноеУчреждение.Отменено"));
        
        ОписаниеОповещенияВыбораЭлемента = Новый ОписаниеОповещения("ВыбранВариантСтатуса",ЭтотОбъект,);
        
        СписокСтатусов.ПоказатьВыборЭлемента(ОписаниеОповещенияВыбораЭлемента,"Выберите статус заявления",СписокСтатусов[1]);
        
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// {Рарус adilas #11811 -Прикреплять согласие на обработку перс. данных к заявлению на прием 2021.02.24
&НаСервере
Процедура ОбратботкаРезультатаВыбораСервер()
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СогласияНаОбработкуПерсональныхДанныхСрезПоследних.Регистратор.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.СогласияНаОбработкуПерсональныхДанных.СрезПоследних(
	|			,
	|			ТИПЗНАЧЕНИЯ(Регистратор) = ТИП(Документ.СогласиеНаОбработкуПерсональныхДанных)
	|				И Субъект = &Субъект
	|				И Организация = &Организация
	|				И Действует = ИСТИНА
	|				И Регистратор.ПометкаУдаления = ЛОЖЬ) КАК СогласияНаОбработкуПерсональныхДанныхСрезПоследних";
	
	Запрос.УстановитьПараметр("Субъект", 		Объект.Спортсмен);
	Запрос.УстановитьПараметр("Организация", 	Объект.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Элементы.ДокументСогласиеНаОбработкуПерсональныхДанных.Видимость = Истина;
			Объект.ДокументСогласиеНаОбработкуПерсональныхДанных = ВыборкаДетальныеЗаписи.Ссылка;
			Объект.СогласиеНаОбработкуПерсональныхДанныхПолучено = Истина;
			
		КонецЦикла;
		
	Иначе
		
		Объект.СогласиеНаОбработкуПерсональныхДанныхПолучено = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПередЗаписью(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда	
		
		Объект.СогласиеНаОбработкуПерсональныхДанныхПолучено = Истина;
		ВыполняетсяЗакрытие = Истина;
		ПараметрыЗаписи = Новый Структура("РежимЗаписи",РежимЗаписиДокумента.Проведение);
   		ЭтотОбъект.Записать(ПараметрыЗаписи);
    	Закрыть();
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда 	
		
		Объект.СогласиеНаОбработкуПерсональныхДанныхПолучено = Ложь;
		ВыполняетсяЗакрытие = Истина;
		Элементы.ДокументСогласиеНаОбработкуПерсональныхДанных.Видимость = Ложь;
		ПараметрыЗаписи = Новый Структура("РежимЗаписи",РежимЗаписиДокумента.Проведение);
   		ЭтотОбъект.Записать(ПараметрыЗаписи);
    	Закрыть();
		
	КонецЕсли;
		
КонецПроцедуры
// }Рарус adilas #11811 -Прикреплять согласие на обработку перс. данных к заявлению на прием 2021.02.24

// {Рарус adilas #11834 -Изменить механизм присоединения файлов для Заявления на прием в учреждение 2021.02.03
&НаКлиенте
Процедура ПослеЗакрытияФормыПредоставленныеДокументы(Результат, ДополнительныеПараметры) Экспорт

	//ЭтаФорма.Прочитать();
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПослеЗакрытияФормыПредоставленныеДокументыНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗакрытияФормыПредоставленныеДокументыНаСервере(НовоеЗаявление)
	
	НовоеЗаявлениеЗначение = ДанныеФормыВЗначение(НовоеЗаявление, Тип("ДокументОбъект.ЗаявленияНаПриемВСпортивноеУчреждение"));
	ЗначениеВРеквизитФормы(НовоеЗаявлениеЗначение, "Объект");
	ПараметрыЗаписи = Новый Структура("РежимЗаписи",РежимЗаписиДокумента.Проведение);
   	ЭтотОбъект.Записать(ПараметрыЗаписи);
	
КонецПроцедуры
// }Рарус adilas #11834 -Изменить механизм присоединения файлов для Заявления на прием в учреждение 2021.02.03

// {Рарус adilas #11842 -Заявка на прием в спортивное учреждение 2021.02.15
&НаКлиенте
Процедура ПослеЗакрытияФормыРодительНачалоВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		Объект.Родитель = Результат.Родитель;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыРодительСоздание(Результат, ДополнительныеПараметры) Экспорт

	Если НЕ Результат = Неопределено Тогда
		
		Объект.Родитель = Результат;
		
	КонецЕсли;
	
КонецПроцедуры
// }Рарус adilas #11842 -Заявка на прием в спортивное учреждение 2021.02.15

// {Рарус adilas #11834 -Изменить механизм присоединения файлов для Заявления на прием в учреждение 2021.02.03
&НаСервере
Функция ПолучитьТекущуюОрганизацию()
	
	ТекущаяОрганизация = ПараметрыСеанса.ТекущаяОрганизация;
	
	Возврат ТекущаяОрганизация;
	
КонецФункции

&НаКлиенте
Процедура ВопросОНеобходимостиЗаписиПослеЗавершения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_заявления" Тогда
		ЭтаФорма.Прочитать();
	КонецЕсли;
	
КонецПроцедуры
// }Рарус adilas #11834 -Изменить механизм присоединения файлов для Заявления на прием в учреждение 2021.02.03

&НаКлиенте
Процедура ВыбранВариантСтатуса(ВыбранноеЗначение, Документы) Экспорт
    
    Если ВыбранноеЗначение<>Неопределено Тогда
        
           УстановитьСтатусЗаявленияСервер(ВыбранноеЗначение.Значение)
                            
    КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусЗаявленияСервер(ВыбранноеЗначение)
    
    НужноРаспаковывать = Ложь;
    
    Если ВыбранноеЗначение = Перечисления.СтатусыЗаявленийНаПриемВСпортивноеУчреждение.Принято и Объект.СтатусЗаявления <> ВыбранноеЗначение Тогда
        
        НужноРаспаковывать = Истина;
        
    КонецЕсли;
    
    РеквизитОбъект = РеквизитФормыВЗначение("Объект",Тип("ДокументОбъект.ЗаявленияНаПриемВСпортивноеУчреждение"));
    РаботаСоСтатусамиДокументов.УстановитьСтатусЗаявления(РеквизитОбъект,ВыбранноеЗначение);
    
    Если НужноРаспаковывать Тогда
        
        РеквизитОбъект.РаспаковатьЗаявление(РеквизитОбъект); 
        
    КонецЕсли; 
    
    ЗначениеВРеквизитФормы(РеквизитОбъект,"Объект");
    
    УстановитьВидимостьЗакладок();
    
    ЗаполнитьРеквизитыФормы();
    
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЗакладок()
    
   Элементы.Заявление.Видимость = (Объект.СтатусЗаявления = Перечисления.СтатусыЗаявленийНаПриемВСпортивноеУчреждение.Принято);
   Элементы.ЧерновикЗаявления.Видимость = НЕ (Объект.СтатусЗаявления = Перечисления.СтатусыЗаявленийНаПриемВСпортивноеУчреждение.Принято);
   
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
    
    Если Объект.ПолученоИзВнешнегоИсточника И Объект.СтатусЗаявления = Перечисления.СтатусыЗаявленийНаПриемВСпортивноеУчреждение.Подготовлено Тогда
        
        РеквизитОбъект = РеквизитФормыВЗначение("Объект", Тип("ДокументОбъект.ЗаявленияНаПриемВСпортивноеУчреждение"));
        СтруктураЗаполнения = РеквизитОбъект.ЧерновикЗаявления.Получить();
        ФИОРодителяЧерновик = Строка(СтруктураЗаполнения.ФамилияСпортсмена) + " " + Строка(СтруктураЗаполнения.ИмяСпортсмена) + " " + Строка(СтруктураЗаполнения.ОтчествоСпортсмена);
        ФИОСпортсменаЧерновик = Строка(СтруктураЗаполнения.ФамилияРодителя) + " " + Строка(СтруктураЗаполнения.ИмяРодителя) + " " + Строка(СтруктураЗаполнения.ОтчествоРодителя);
        
    КонецЕсли;

КонецПроцедуры

//{rarus lobash IN-19394 31.08.2021
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Элементы.ДокументСогласиеНаОбработкуПерсональныхДанных.Видимость = Объект.СогласиеНаОбработкуПерсональныхДанныхПолучено;		
	КонецЕсли;
КонецПроцедуры
//}rarus lobash IN-19394 31.08.2021

#КонецОбласти

ВыполняетсяЗакрытие = Ложь;