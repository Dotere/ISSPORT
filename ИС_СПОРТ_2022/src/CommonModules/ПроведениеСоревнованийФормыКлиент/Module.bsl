#Область ОбработчикиРеквизитовФормы

Процедура ЭлементФормыОткрытие(ЭтотОбъект, СтандартнаяОбработка, ИмяРеквизитаФормы, ИмяПроцедурыОповещения) Экспорт
	
	ТекДанные = ЭтотОбъект.Элементы.СоставУчастников.ТекущиеДанные;
	СтандартнаяОбработка = Ложь;
	
	СтруктураОповещения = Новый Структура("НомерСтроки,Спортсмен", ТекДанные.НомерСтроки, ТекДанные.Спортсмен);
	СтруктураОповещения.Вставить(ИмяРеквизитаФормы, ТекДанные[ИмяРеквизитаФормы]);
	
	Оповещение = Новый ОписаниеОповещения(ИмяПроцедурыОповещения,
	ЭтотОбъект,
	СтруктураОповещения);
	
	СписокКоманд = Новый СписокЗначений;
	Если ЗначениеЗаполнено(ТекДанные[ИмяРеквизитаФормы]) Тогда 
		СписокКоманд.Добавить(КодВозвратаДиалога.Да,     "Открыть документ");
	КонецЕсли;
	СписокКоманд.Добавить(КодВозвратаДиалога.Нет,    "Создать документ");
	СписокКоманд.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
	
	ПоказатьВопрос(Оповещение,
	"Выбирите действие",
	СписокКоманд,
	0, 
	?(ЗначениеЗаполнено(ТекДанные[ИмяРеквизитаФормы]),КодВозвратаДиалога.Да, КодВозвратаДиалога.Нет),
	); 
	
КонецПроцедуры                              

Процедура СформироватьЗаголовокФормыДокумента(ЭтотОбъект, ИмяДокумента) Экспорт
	
	Объект = ЭтотОбъект.ЭтаФорма.Объект;
	
	Если Объект.ТипСоревнования = ПредопределенноеЗначение("Перечисление.ТипыСдачиНормативов.Норматив") Тогда
		Если ИмяДокумента = "ЗаявкаНаПроведениеСоревнованияСдачаНормативаОФП" Тогда
			ЭтотОбъект.Заголовок = "Заявка на сдачу нормативов по физической подготовке";
		ИначеЕсли ИмяДокумента = "ПротоколОФПСдачаНормативов" Тогда
			ЭтотОбъект.Заголовок = "Протокол сдачи нормативов по физической подготовке";
		Иначе                                                
			ЭтотОбъект.Заголовок = "Сдача нормативов по физической подготовке";
		КонецЕсли;
		ЭтотОбъект.Автозаголовок = Ложь;
	ИначеЕсли Объект.ТипСоревнования = ПредопределенноеЗначение("Перечисление.ТипыСдачиНормативов.Соревнование") Тогда
		Если ИмяДокумента = "ЗаявкаНаПроведениеСоревнованияСдачаНормативаОФП" Тогда
			ЭтотОбъект.Заголовок = "Заявка на соревнования по общей физической подготовке";
		ИначеЕсли ИмяДокумента = "ПротоколОФПСдачаНормативов" Тогда
			ЭтотОбъект.Заголовок = "Протокол соревнования по общей физической подготовке";	
		Иначе
			ЭтотОбъект.Заголовок = "Соревнование по общей физической подготовке";
		КонецЕсли;
		ЭтотОбъект.Автозаголовок = Ложь;
	Иначе
		ЭтотОбъект.Автозаголовок = Истина;
		ЭтотОбъект.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьЗаголовокФормыСписка(Форма, ИмяДокумента, ТипСоревнования) Экспорт
		
	Если ТипСоревнования = ПредопределенноеЗначение("Перечисление.ТипыСдачиНормативов.Норматив") Тогда
		Если ИмяДокумента = "ЗаявкаНаПроведениеСоревнованияСдачаНормативаОФП" Тогда
			Форма.Заголовок = "Заявки на сдачу нормативов по общей физической подготовке";
		ИначеЕсли ИмяДокумента = "ПротоколОФПСдачаНормативов" Тогда
			Форма.Заголовок = "Протоколы сдачи нормативов по общей физической подготовке";
		Иначе                                                
			Форма.Заголовок = "Сдача нормативов по общей физической подготовке";
		КонецЕсли;
	ИначеЕсли ТипСоревнования = ПредопределенноеЗначение("Перечисление.ТипыСдачиНормативов.Соревнование") Тогда
		Если ИмяДокумента = "ЗаявкаНаПроведениеСоревнованияСдачаНормативаОФП" Тогда
			Форма.Заголовок = "Заявки на соревнования по общей физической подготовке";
		ИначеЕсли ИмяДокумента = "ПротоколОФПСдачаНормативов" Тогда
			Форма.Заголовок = "Протоколы соревнований по общей физической подготовке";	
		Иначе
			Форма.Заголовок = "Соревнования по общей физической подготовке";
		КонецЕсли;
	Иначе
		Если ИмяДокумента = "ЗаявкаНаПроведениеСоревнованияСдачаНормативаОФП" Тогда
			Форма.Заголовок = "Заявки (общая физическая подготовка)";
		ИначеЕсли ИмяДокумента = "ПротоколОФПСдачаНормативов" Тогда
			Форма.Заголовок = "Протоколы (общая физическая подготовка)";	
		Иначе
			Форма.Заголовок = "Состав участников на мероприятия по общей физической подготовке";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандНаФорме

Процедура ВыделитьВсехУчастниковНаФорме(ЭтотОбъект, ИмяТаблицы, ИмяРеквизита) Экспорт
	
	Для Каждого СтрокаНорматив Из ЭтотОбъект[ИмяТаблицы] Цикл
		Для Каждого СтрокаСоставУчастников Из ЭтотОбъект.СоставУчастниковНаФорме Цикл
			УстановитьЗначениеНормативаВСтроке(СтрокаНорматив[ИмяРеквизита], СтрокаСоставУчастников, Истина, СтрокаНорматив.Норматив);
		КонецЦикла;	
	КонецЦикла;	
	
КонецПроцедуры

Процедура УбратьВыделениеВсехУчастниковНаФорме(ЭтотОбъект, ИмяТаблицы, ИмяРеквизита) Экспорт
	
	Для Каждого СтрокаНорматив Из ЭтотОбъект[ИмяТаблицы] Цикл
		Для Каждого СтрокаСоставУчастников Из ЭтотОбъект.СоставУчастниковНаФорме Цикл
			УстановитьЗначениеНормативаВСтроке(СтрокаНорматив[ИмяРеквизита], СтрокаСоставУчастников, Ложь, СтрокаНорматив.Норматив);
		КонецЦикла;	
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаПередПроведением

Процедура ПроверитьСоответствиеВозрастнойГруппыСпортсменаПередЗаписью(ЭтотОбъект, Отказ) Экспорт
	
	МассивСпортсменовНеВключенныхВГруппу = Новый Массив;
	
	Объект = ЭтотОбъект.Объект;
	
	массивУчастников = Новый Массив;
	Для Каждого строкаСоставУчастников Из ЭтотОбъект.СоставУчастниковНаФорме Цикл
		массивУчастников.Добавить(Новый Структура("Спортсмен,МеждународнаяВозрастнаяГруппа", строкаСоставУчастников.Спортсмен, строкаСоставУчастников.МеждународнаяВозрастнаяГруппа));
	КонецЦикла;
	
	СтруктураДляПроверки = Новый Структура("Дата, УчебныйГод, Организация, ВидСпорта, СоставУчастниковДокумента", Объект.Дата, Объект.УчебныйГод, Объект.Организация, Объект.ВидСпорта, массивУчастников);
	
	ПроведениеСоревнованийСлужебныйСервер.ПроверитьСоответствиеВозрастныхГруппСпортсменов(СтруктураДляПроверки, МассивСпортсменовНеВключенныхВГруппу, Отказ);
		
	Если НЕ МассивСпортсменовНеВключенныхВГруппу.Количество()= 0 И НЕ Отказ Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПередЗаписьюЗавершение",
		ЭтотОбъект);
		
		СтрокаИзМассива = "";
		Для Индекс = 0 По МассивСпортсменовНеВключенныхВГруппу.Количество()-1 Цикл
			СтрокаИзМассива = СтрокаИзМассива + МассивСпортсменовНеВключенныхВГруппу[Индекс] + ?(Индекс = МассивСпортсменовНеВключенныхВГруппу.Количество()-1, "",",");
		КонецЦикла;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 %2 младше возрастного порога указанной группы.
		|Продолжить проведение?'"),
		?(МассивСпортсменовНеВключенныхВГруппу.Количество() = 1, "Спортсмен", "Спортсмены"),
		СтрокаИзМассива);
		
		ПоказатьВопрос(Оповещение,
		ТекстСообщения,
		РежимДиалогаВопрос.ДаНет,
		0);
		
		Отказ = Истина;
				
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПронумероватьТаблицуНаФорме(ТаблицаДанных) Экспорт
	
	Для Индекс = 0 По ТаблицаДанных.Количество()-1 Цикл
		ТаблицаДанных[Индекс].НомерСтроки = Индекс + 1;
	КонецЦикла;	
	
КонецПроцедуры	

Процедура УстановитьЗначениеНормативаВСтроке(Гуид, СтрокаСоставУчастников, Значение, Норматив);
	
	СтрокаСоставУчастников["Выбор_" + Гуид] = Значение;
	СтрокаСоставУчастников["Норматив_" + Гуид] = ?(Значение, Норматив, Неопределено);
	
КонецПроцедуры

#КонецОбласти