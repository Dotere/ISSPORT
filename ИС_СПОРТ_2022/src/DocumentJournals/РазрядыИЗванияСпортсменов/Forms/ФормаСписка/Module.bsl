
// {Рарус adilas #16630 -Печатная форма Приказа на присвоение разряда 2021.07.05
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Спортсмен") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,"СсылкаСпортсмен", Параметры.Спортсмен, ВиДсравненияКомпоновкиДанных.Равно);
		ТекущийСпортсмен = Параметры.Спортсмен;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

// {Рарус adilas #16630 -Печатная форма Приказа на присвоение разряда 2021.07.05
#Область ОбработчикиКомандФормы

#Область ПечатьWord

&НаКлиенте
Процедура Вывести(ДанныеПечати, МакетИДанныеОбъекта, ИмяМакета, ЛокальныйКаталогФайловПечати, НаименованиеМакета)
	
	ТипМакета				= МакетИДанныеОбъекта.Макеты.ТипыМакетов[ИмяМакета];
	ДвоичныеДанныеМакетов	= МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	Области					= МакетИДанныеОбъекта.Макеты.ОписаниеОбластей;
	
	Состояние(НСтр("ru='Выполняется формирование печатных форм'"));

	Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета, ИмяМакета);
	Если Макет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗакрытьОкноПечатнойФормы = Ложь;
	
	Для Каждого ОбъектДанных Из ДанныеПечати Цикл
		ДанныеОбъектаМассив     = МакетИДанныеОбъекта.Данные[ОбъектДанных][ИмяМакета];
		Для Каждого ДанныеОбъекта Из  ДанныеОбъектаМассив Цикл
			Попытка	
				ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета, Макет.НастройкиСтраницыМакета);
				Если ПечатнаяФорма = Неопределено Тогда
					УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
					Возврат;                                                             
				КонецЕсли;
				
				Попытка
					Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ОбластьШапка"]);
					УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ОбластьШапка, Ложь);
				Исключение
					ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось подключить область 'ОбластьШапка' по причине: " + ОписаниеОшибки());
				КонецПопытки;
				
				Для каждого СтрокаОбластиСоревнований Из ДанныеОбъекта.МассивСоревнований Цикл	
					Если ТипЗнч(СтрокаОбластиСоревнований) = Тип("Структура") Тогда		
						Попытка	
							Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ОбластьСоревнование"]);
							УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, СтрокаОбластиСоревнований.ОбластьСоревнование, Ложь);	
						Исключение
							ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось подключить область 'ОбластьСоревнование' по причине: " + ОписаниеОшибки());
						КонецПопытки;	
					Иначе			
						Для каждого СтрокаШапкаТаблицы Из СтрокаОбластиСоревнований Цикл	
							Если ТипЗнч(СтрокаШапкаТаблицы) = Тип("Структура") Тогда
								Попытка	
									Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ОбластьШапкаТаблицы"]);
									УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, СтрокаШапкаТаблицы.ОбластьШапкаТаблицы, Ложь);		
								Исключение
									ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось подключить область 'ОбластьШапкаТаблицы' по причине: " + ОписаниеОшибки());
								КонецПопытки;
							Иначе
								Для каждого СтрокаТаблицыСпортсмены Из СтрокаШапкаТаблицы Цикл
									Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ОбластьСтрокаТаблицыСпортсмены"]);
									УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, СтрокаТаблицыСпортсмены, Ложь);
								КонецЦикла;
							КонецЕсли;	
						КонецЦикла;	
					КонецЕсли;
				КонецЦикла;
				
				Попытка
					Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ОбластьПодвал"]);
					УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта.ОбластьПодвал, Ложь);	
				Исключение
					ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось подключить область 'ОбластьПодвал' по причине: " + ОписаниеОшибки());
				КонецПопытки;	
					
				УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);	
			Исключение
				ОбщегоНазначенияКлиент.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗакрытьОкноПечатнойФормы = Истина;
			КонецПопытки;	
		КонецЦикла;	
	КонецЦикла;
	
	УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, ЗакрытьОкноПечатнойФормы);
	УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
	
	Состояние(НСтр("ru='Формирование печатных форм завершено'"));
		
КонецПроцедуры

#КонецОбласти

#Область Печать

&НаКлиенте
Процедура ПечатьПриказ(Команда)
	
	ДанныеПечати = Элементы.Список.ВыделенныеСтроки;
	
	МассивПрисвоениеРазрядов 	= Новый Массив; 
	МассивПодтверждениеРазрядов = Новый Массив;
	
	//Цикл по разделению документов
	Для каждого ОбъектПечати Из ДанныеПечати Цикл
	
		Если ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.ПрисвоениеРазряда") Тогда
			МассивПрисвоениеРазрядов.Добавить(ОбъектПечати); 	         
		ИначеЕсли ТипЗнч(ОбъектПечати) = Тип("ДокументСсылка.ПодтверждениеРазрядаСпортсмена") Тогда 			
			МассивПодтверждениеРазрядов.Добавить(ОбъектПечати);	
		КонецЕсли; 
	
	КонецЦикла;  
	
	#Если Не ВебКлиент Тогда
		Если МассивПрисвоениеРазрядов.Количество() <> 0 Тогда
			МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати("Документ.ПрисвоениеРазряда", "ПФ_DOC_ПриказОПрисвоенииРазрядов", МассивПрисвоениеРазрядов);
			Вывести(МассивПрисвоениеРазрядов, МакетИДанныеОбъекта, "ПФ_DOC_ПриказОПрисвоенииРазрядов", МакетИДанныеОбъекта.ЛокальныйКаталогФайловПечати, "Приказ о присвоении разрядов Word");
		КонецЕсли;
		
		Если МассивПодтверждениеРазрядов.Количество() <> 0 Тогда
			МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати("Документ.ПодтверждениеРазрядаСпортсмена", "ПФ_DOC_ПриказОПодтвержденииРазрядов", МассивПодтверждениеРазрядов);
			Вывести(МассивПодтверждениеРазрядов, МакетИДанныеОбъекта, "ПФ_DOC_ПриказОПодтвержденииРазрядов", МакетИДанныеОбъекта.ЛокальныйКаталогФайловПечати, "Приказ о подтверждении разрядов Word");
		КонецЕсли;
	#Иначе
		Если МассивПрисвоениеРазрядов.Количество() <> 0 Тогда	
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати
			(
			"Документ.ПрисвоениеРазряда",
			"ПФ_MXL_ПриказОПрисвоенииРазрядов",
			МассивПрисвоениеРазрядов,
			Неопределено,
			Неопределено
			);
		КонецЕсли;
		
		Если МассивПодтверждениеРазрядов.Количество() <> 0 Тогда
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати
			(
			"Документ.ПодтверждениеРазрядаСпортсмена",
			"ПФ_MXL_ПриказОПодтвержденииРазрядов",
			МассивПодтверждениеРазрядов,
			Неопределено,
			Неопределено
			);
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
// }Рарус adilas #16630 -Печатная форма Приказа на присвоение разряда 2021.07.05

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Если НЕ Копирование Тогда
		Отказ = Истина;
		Если Параметр = Тип("ДокументСсылка.ПрисвоениеРазряда") Тогда
			ОткрытьФорму("Документ.ПрисвоениеРазряда.Форма.ФормаДокумента",Новый Структура("Спортсмен",ТекущийСпортсмен));
		Иначе
			ОткрытьФорму("Документ.ПодтверждениеРазрядаСпортсмена.Форма.ФормаДокумента",Новый Структура("Спортсмен",ТекущийСпортсмен));
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
// }Рарус adilas #16630 -Печатная форма Приказа на присвоение разряда 2021.07.05