#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// {Рарус adilas #1.0.0.2 -SonarQube 2021.04.23
	// ТекущаяДатаСеанса() вместо ТекущаяДата()
	ДатаСреза = ?(Параметры.Свойство("ДатаДокумента"), Параметры.ДатаДокумента, ТекущаяДатаСеанса());
	// }Рарус adilas #1.0.0.2 -SonarQube 2021.04.23
	Если Параметры.Свойство("СписокОтборов") Тогда
		Для Каждого Отбор Из Параметры.СписокОтборов Цикл
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокЗаявок, Отбор.Ключ, Отбор.Значение, ВидСравненияКомпоновкиДанных.Равно);
		КонецЦикла;	
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокСпортсменов , "Дата" , ДатаСреза , Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокЗаявок      , "Дата" , ДатаСреза , Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗакрыватьФорму = Истина Тогда
        Отказ = Истина;
        ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
    КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиДинамическихСписков

&НаКлиенте
Процедура СписокЗаявокПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элементы.СписокЗаявок.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		ТекущаяЗаявкаСписок = Неопределено;
	Иначе
		ТекущаяЗаявкаСписок = ТекДанные.Ссылка;
	КонецЕсли;
	Если ТекущаяЗаявкаСписок <> ТекущаяЗаявка Тогда
		ТекущаяЗаявка = ТекущаяЗаявкаСписок;
		УстановитьОтборНаДинамическийСписок();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ВыбратьЗаявку(Команда)
	
	текДанные = Элементы.СписокЗаявок.ВыделенныеСтроки;
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ВыбратьЗаявкуНаСервере(текДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСпортсмена(Команда)
	
	текДанные = Элементы.СписокСпортсменов.ВыделенныеСтроки;
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивЗаявок = Новый Массив;
	МассивЗаявок.Добавить(ТекущаяЗаявка);
	ВыбратьЗаявкуНаСервере(МассивЗаявок, текДанные);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьФорму() Экспорт
    ЗакрыватьФорму = Истина;
    Закрыть(СформироватьМассивИзТаблицыДанных());
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборНаДинамическийСписок()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокСпортсменов,"Регистратор");	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокСпортсменов,"Регистратор",ТекущаяЗаявка,ВидСравненияКомпоновкиДанных.Равно);	
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьЗаявкуНаСервере(СписокЗаявок_, СписокСпортсменов_ = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокЗаявок", СписокЗаявок_);
	Запрос.УстановитьПараметр("СписокСпортсменов", СписокСпортсменов_);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявкиНаПроведениеСоревнования.Период КАК Период,
	               |	ЗаявкиНаПроведениеСоревнования.УчебныйГод КАК УчебныйГод,
	               |	ЗаявкиНаПроведениеСоревнования.ВидСпорта КАК ВидСпорта,
	               |	ЗаявкиНаПроведениеСоревнования.Спортсмен КАК Спортсмен,
	               |	ЗаявкиНаПроведениеСоревнования.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
				   |	ЗаявкиНаПроведениеСоревнования.Команда КАК Команда,
	               |	ЗаявкиНаПроведениеСоревнования.Тренер КАК Тренер
	               |ПОМЕСТИТЬ ВТ_СписокСпортсменов
	               |ИЗ
	               |	РегистрСведений.ЗаявкиНаПроведениеСоревнования.СрезПоследних(, Регистратор В (&СписокЗаявок) %1) КАК ЗаявкиНаПроведениеСоревнования
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СписокСпортсменов.Спортсмен КАК Спортсмен,
	               |	ВТ_СписокСпортсменов.ВидСпорта КАК ВидСпорта,
	               |	МАКСИМУМ(ЕСТЬNULL(РегистрацияДопусков.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК Период
	               |ПОМЕСТИТЬ ВТ_МаксимумДопуски
	               |ИЗ
	               |	ВТ_СписокСпортсменов КАК ВТ_СписокСпортсменов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияДопусковКСпортивнымСоревнованиям КАК РегистрацияДопусков
	               |		ПО ВТ_СписокСпортсменов.Спортсмен = РегистрацияДопусков.Спортсмен
	               |			И ВТ_СписокСпортсменов.Период >= РегистрацияДопусков.Период
	               |			И ВТ_СписокСпортсменов.ВидСпорта >= РегистрацияДопусков.ВидСпорта
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_СписокСпортсменов.Спортсмен,
	               |	ВТ_СписокСпортсменов.ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_МаксимумДопуски.Спортсмен КАК Спортсмен,
	               |	ВТ_МаксимумДопуски.ВидСпорта КАК ВидСпорта,
	               |	РегистрацияДопусков.ДействуетДо КАК ДействуетДо
	               |ПОМЕСТИТЬ ВТ_Допуски
	               |ИЗ
	               |	ВТ_МаксимумДопуски КАК ВТ_МаксимумДопуски
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияДопусковКСпортивнымСоревнованиям КАК РегистрацияДопусков
	               |		ПО ВТ_МаксимумДопуски.Спортсмен = РегистрацияДопусков.Спортсмен
	               |			И ВТ_МаксимумДопуски.Период = РегистрацияДопусков.Период
	               |			И ВТ_МаксимумДопуски.ВидСпорта = РегистрацияДопусков.ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Разряды.Спортсмен КАК Спортсмен,
	               |	Разряды.ВидСпорта КАК ВидСпорта,
	               |	МАКСИМУМ(ЕСТЬNULL(Разряды.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК Период
	               |ПОМЕСТИТЬ ВТ_МаксимумРазряды
	               |ИЗ
	               |	ВТ_СписокСпортсменов КАК ВТ_СписокСпортсменов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК Разряды
	               |		ПО ВТ_СписокСпортсменов.Спортсмен = Разряды.Спортсмен
	               |			И ВТ_СписокСпортсменов.ВидСпорта = Разряды.ВидСпорта
	               |			И ВТ_СписокСпортсменов.Период >= Разряды.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Разряды.Спортсмен,
	               |	Разряды.ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_МаксимумРазряды.Спортсмен КАК Спортсмен,
	               |	ВТ_МаксимумРазряды.ВидСпорта КАК ВидСпорта,
	               |	РазрядыСпортсменов.Разряд КАК Разряд
	               |ПОМЕСТИТЬ ВТ_Разряды
	               |ИЗ
	               |	ВТ_МаксимумРазряды КАК ВТ_МаксимумРазряды
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов КАК РазрядыСпортсменов
	               |		ПО ВТ_МаксимумРазряды.Спортсмен = РазрядыСпортсменов.Спортсмен
	               |			И ВТ_МаксимумРазряды.Период = РазрядыСпортсменов.Период
	               |			И ВТ_МаксимумРазряды.ВидСпорта = РазрядыСпортсменов.ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СписокСпортсменов.УчебныйГод КАК УчебныйГод,
	               |	ВТ_СписокСпортсменов.ВидСпорта КАК ВидСпорта,
	               |	ВТ_СписокСпортсменов.Спортсмен КАК Спортсмен,
	               |	МАКСИМУМ(СоставГруппы.Период) КАК Период
	               |ПОМЕСТИТЬ ВТ_МаксимумГруппы
	               |ИЗ
	               |	ВТ_СписокСпортсменов КАК ВТ_СписокСпортсменов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставГруппы КАК СоставГруппы
	               |		ПО ВТ_СписокСпортсменов.Период >= СоставГруппы.Период
	               |			И ВТ_СписокСпортсменов.УчебныйГод = СоставГруппы.УчебныйГод
	               |			И ВТ_СписокСпортсменов.Спортсмен = СоставГруппы.Спортсмен
	               |			И ВТ_СписокСпортсменов.ВидСпорта = СоставГруппы.ВидСпорта
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_СписокСпортсменов.УчебныйГод,
	               |	ВТ_СписокСпортсменов.Спортсмен,
	               |	ВТ_СписокСпортсменов.ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_МаксимумГруппы.Спортсмен КАК Спортсмен,
	               |	ВТ_МаксимумГруппы.ВидСпорта КАК ВидСпорта,
	               |	СоставГруппы.Группа КАК Группа,
	               |	СоставГруппы.Организация КАК СпортивноеУчреждение
	               |ПОМЕСТИТЬ ВТ_Группы
	               |ИЗ
	               |	ВТ_МаксимумГруппы КАК ВТ_МаксимумГруппы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставГруппы КАК СоставГруппы
	               |		ПО ВТ_МаксимумГруппы.УчебныйГод = СоставГруппы.УчебныйГод
	               |			И ВТ_МаксимумГруппы.Спортсмен = СоставГруппы.Спортсмен
	               |			И ВТ_МаксимумГруппы.Период = СоставГруппы.Период
	               |			И (ВТ_МаксимумГруппы.Период < СоставГруппы.ДатаИсключенияИзГруппы
	               |				ИЛИ СоставГруппы.ДатаИсключенияИзГруппы = ДАТАВРЕМЯ(1, 1, 1))
	               |			И ВТ_МаксимумГруппы.ВидСпорта = СоставГруппы.ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СписокСпортсменов.Спортсмен КАК Спортсмен,
	               |	МАКСИМУМ(РегистрацияСтраховыхПолисов.Период) КАК Период,
	               |	РегистрацияСтраховыхПолисов.ВидСпорта КАК ВидСпорта
	               |ПОМЕСТИТЬ ВТ_СтраховыеПолисяМаксимум
				   |ИЗ
	               |	ВТ_СписокСпортсменов КАК ВТ_СписокСпортсменов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияСтраховыхПолисовСпортсменов КАК РегистрацияСтраховыхПолисов
	               |		ПО ВТ_СписокСпортсменов.Спортсмен = РегистрацияСтраховыхПолисов.Спортсмен
	               |			И ВТ_СписокСпортсменов.ВидСпорта = РегистрацияСтраховыхПолисов.ВидСпорта
	               |			И ВТ_СписокСпортсменов.Период >= РегистрацияСтраховыхПолисов.ДатаНачала
				   |	        И (РегистрацияСтраховыхПолисов.ДатаОкончания >= ВТ_СписокСпортсменов.Период
	               |			   ИЛИ РегистрацияСтраховыхПолисов.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))
	               |ГДЕ
	               |	РегистрацияСтраховыхПолисов.Спортсмен В
	               |			(ВЫБРАТЬ
	               |				ВТ_СписокСпортсменов.Спортсмен
	               |			ИЗ
	               |				ВТ_СписокСпортсменов)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_СписокСпортсменов.Спортсмен,
	               |	РегистрацияСтраховыхПолисов.ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СтраховыеПолисяМаксимум.Спортсмен КАК Спортсмен,
	               |	ВТ_СтраховыеПолисяМаксимум.ВидСпорта КАК ВидСпорта,
	               |	РегистрацияСтраховыхПолисовСпортсменов.Регистратор КАК СтраховойПолисРегистратор,
	               |	РегистрацияСтраховыхПолисовСпортсменов.СтраховаяКомпания КАК СтраховаяКомпания,
	               |	РегистрацияСтраховыхПолисовСпортсменов.ДатаНачала КАК ДатаНачала,
	               |	РегистрацияСтраховыхПолисовСпортсменов.ДатаОкончания КАК ДатаОкончания,
	               |	РегистрацияСтраховыхПолисовСпортсменов.НомерСтраховогоПолиса КАК НомерСтраховогоПолиса,
	               |	РегистрацияСтраховыхПолисовСпортсменов.ДатаВыдачиПолиса КАК ДатаВыдачиПолиса
	               |ПОМЕСТИТЬ ВТ_СтраховыеПолисы
	               |ИЗ
	               |	ВТ_СтраховыеПолисяМаксимум КАК ВТ_СтраховыеПолисяМаксимум
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияСтраховыхПолисовСпортсменов КАК РегистрацияСтраховыхПолисовСпортсменов
	               |		ПО ВТ_СтраховыеПолисяМаксимум.Спортсмен = РегистрацияСтраховыхПолисовСпортсменов.Спортсмен
	               |			И ВТ_СтраховыеПолисяМаксимум.Период = РегистрацияСтраховыхПолисовСпортсменов.Период
	               |			И ВТ_СтраховыеПолисяМаксимум.ВидСпорта = РегистрацияСтраховыхПолисовСпортсменов.ВидСпорта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СписокСпортсменов.Спортсмен КАК Спортсмен,
	               |	ВТ_СписокСпортсменов.Период КАК Период,
	               |	ВТ_СписокСпортсменов.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
				   |	ВТ_СписокСпортсменов.Команда КАК Команда,
	               |	ВТ_СписокСпортсменов.Тренер КАК Тренер,
	               |	ВТ_Разряды.Разряд КАК Разряд,
	               |	ВЫБОР
	               |		КОГДА ВТ_Допуски.ДействуетДо ЕСТЬ NULL
	               |			ТОГДА ""НЕ ДОПУЩЕН""
	               |		КОГДА ВТ_Допуски.ДействуетДо <= ВТ_СписокСпортсменов.Период
	               |			ТОГДА ""НЕ ДОПУЩЕН""
	               |		ИНАЧЕ ""ДОПУЩЕН""
	               |	КОНЕЦ КАК Допуск,
	               |	ВТ_Допуски.ДействуетДо КАК ДействуетДо,
	               |	ВТ_Группы.Группа КАК Группа,
	               |	ВТ_Группы.СпортивноеУчреждение КАК СпортивноеУчреждение,
	               |	ЕСТЬNULL(ВТ_СтраховыеПолисы.СтраховойПолисРегистратор, ЗНАЧЕНИЕ(Документ.СтраховойПолисСпортсмена.ПустаяСсылка)) КАК СтраховойПолисРегистратор,
	               |	ВТ_СтраховыеПолисы.СтраховаяКомпания КАК СтраховаяКомпания,
	               |	ВТ_СтраховыеПолисы.ДатаНачала КАК ДатаНачала,
	               |	ВТ_СтраховыеПолисы.ДатаОкончания КАК ДатаОкончания,
	               |	ВТ_СтраховыеПолисы.НомерСтраховогоПолиса КАК НомерСтраховогоПолиса,
	               |	ВТ_СтраховыеПолисы.ДатаВыдачиПолиса КАК ДатаВыдачиПолиса
	               |ИЗ
	               |	ВТ_СписокСпортсменов КАК ВТ_СписокСпортсменов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Разряды КАК ВТ_Разряды
	               |		ПО ВТ_СписокСпортсменов.Спортсмен = ВТ_Разряды.Спортсмен
	               |			И ВТ_СписокСпортсменов.ВидСпорта = ВТ_Разряды.ВидСпорта
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Допуски КАК ВТ_Допуски
	               |		ПО ВТ_СписокСпортсменов.Спортсмен = ВТ_Допуски.Спортсмен
	               |			И ВТ_СписокСпортсменов.ВидСпорта = ВТ_Допуски.ВидСпорта
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Группы КАК ВТ_Группы
	               |		ПО ВТ_СписокСпортсменов.Спортсмен = ВТ_Группы.Спортсмен
	               |			И ВТ_СписокСпортсменов.ВидСпорта = ВТ_Группы.ВидСпорта
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтраховыеПолисы КАК ВТ_СтраховыеПолисы
	               |		ПО ВТ_СписокСпортсменов.Спортсмен = ВТ_СтраховыеПолисы.Спортсмен
	               |			И ВТ_СписокСпортсменов.ВидСпорта = ВТ_СтраховыеПолисы.ВидСпорта";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ?(СписокСпортсменов_ = Неопределено, "", " И Спортсмен в (&СписокСпортсменов)"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		строкаПодбора = ТаблицаПодбора.Добавить();
		ЗаполнитьЗначенияСвойств(строкаПодбора, Выборка);
		// {Рарус dotere #16607 -Проверка на спортсмена из другого учереждения 2021.07.29
		Если Выборка.Спортсмен.СтатусСпортсмена <> Перечисления.СтатусыСпортсменов.СпортсменДругогоСпортивногоУчреждения Тогда
 
			Если строкаПодбора.Допуск = "ДОПУЩЕН" Тогда
				строкаПодбора.Допуск = строкаПодбора.Допуск + " (действует до " + Формат(Выборка.ДействуетДо,"ДЛФ=Д") + ")";
			КонецЕсли;
			Если Выборка.СтраховойПолисРегистратор = Документы.СтраховойПолисСпортсмена.ПустаяСсылка() Тогда
				строкаПодбора.СтраховойПолис = "НЕ ВВЕДЕН";
			ИначеЕсли Выборка.ДатаОкончания <= Выборка.Период Тогда
				строкаПодбора.СтраховойПолис = СокрЛП(Выборка.СтраховаяКомпания) + "; № " + СокрЛП(Выборка.НомерСтраховогоПолиса) + " период действия " 
				   + Формат(Выборка.ДатаНачала, "ДЛФ=Д") + "-" + Формат(Выборка.ДатаОкончания, "ДЛФ=Д") + " ПРОСРОЧЕН";
			Иначе
				строкаПодбора.СтраховойПолис = СокрЛП(Выборка.СтраховаяКомпания) + "; № " + СокрЛП(Выборка.НомерСтраховогоПолиса) + " период действия " 
				   + Формат(Выборка.ДатаНачала, "ДЛФ=Д") + "-" + Формат(Выборка.ДатаОкончания, "ДЛФ=Д");   
			КонецЕсли;
		Иначе
			строкаПодбора.Допуск = "";
		КонецЕсли;
			// }Рарус dotere #16607 -Проверка на спортсмена из другого учереждения 2021.07.29
	КонецЦикла;  
	
КонецПроцедуры	

&НаСервере
Функция СформироватьМассивИзТаблицыДанных()
	
	тзТаблицаПодбора = ТаблицаПодбора.Выгрузить();
	тзТаблицаПодбора.Свернуть("Спортсмен, МеждународнаяВозрастнаяГруппа, Тренер, Допуск, Разряд, Группа, СпортивноеУчреждение, Команда, СтраховойПолис, СтраховойПолисРегистратор");
	
	МассивДанных = Новый Массив;
	Для Каждого стрТаблицаПодбора Из тзТаблицаПодбора Цикл
		структураДанных = Новый Структура("Спортсмен, МеждународнаяВозрастнаяГруппа, Тренер, Допуск, Разряд, Группа, СпортивноеУчреждение, Команда, СтраховойПолис, СтраховойПолисРегистратор");
		ЗаполнитьЗначенияСвойств(структураДанных,стрТаблицаПодбора);
		МассивДанных.Добавить(структураДанных);
	КонецЦикла;
	
	Возврат МассивДанных
	
КонецФункции	

#КонецОбласти