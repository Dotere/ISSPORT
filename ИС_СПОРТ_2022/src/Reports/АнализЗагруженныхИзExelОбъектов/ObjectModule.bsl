
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	
	МассивТипов = Новый Массив;
	//МассивТипов.Добавить(Тип("СправочникСсылка.Команда"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Спортсмены"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ДопускиУчастниковКСпортивнымСоревнованиям"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаявкаНаПроведениеСоревнования"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ПрисвоениеРазряда"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ФормированиеГруппы"));
	
	ТаблицаДанных.Колонки.Добавить("Объект", Новый ОписаниеТипов(МассивТипов));
	
	АдресВременногоХранилища = "";
	Параметр = Настройки.ПараметрыДанных.Элементы.Найти("АдресВременногоХранилища");
    Если Параметр <> Неопределено Тогда
        АдресВременногоХранилища = Параметр.Значение;
    КонецЕсли;
	
	массивОбъектов = ?(ЗначениеЗаполнено(АдресВременногоХранилища),ПолучитьИзВременногоХранилища(АдресВременногоХранилища), Неопределено);
	
	
	Если НЕ массивОбъектов = Неопределено Тогда
		Для Каждого ЗначениеОбъект Из массивОбъектов Цикл
			строкаДанных = ТаблицаДанных.Добавить();
			строкаДанных.Объект = ЗначениеОбъект;
		КонецЦикла;
	КонецЕсли;
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, Новый Структура("ВнешниеДанные", ТаблицаДанных), ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры
