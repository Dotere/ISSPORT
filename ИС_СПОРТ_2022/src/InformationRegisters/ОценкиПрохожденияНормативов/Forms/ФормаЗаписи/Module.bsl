#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ИзменитьВидимостьДоступностьНормативаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИзменитьВидимостьДоступностьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПересчетВМилСек Тогда
		ПроверитьРезультатСтроковыйПриИзменении(Отказ);
		Если Отказ Тогда
			Возврат
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// {Рарус kotana #7380 2020.07.24
	//Добавила условие
	Если ТекущийОбъект.Норматив.ТипРезультата = ПредопределенноеЗначение("Перечисление.ТипыРезультатов.Время") Тогда
		
		ПересчитатьВремяИзСтрокиВМиллесекунды(ТекущийОбъект);
		
	КонецЕсли; 
	// }Рарус kotana #7380 2020.07.24

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовФормы

&НаКлиенте
Процедура РезультатСтроковыйПриИзменении(Элемент)
	
	ПроверитьРезультатСтроковыйПриИзменении(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НормативПриИзменении(Элемент)
	
	Запись.РезультатБулево = Ложь;
	Запись.РезультатЧисло  = 0;
	ИзменитьВидимостьДоступностьНормативаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПопытокПриИзменении(Элемент)
	
	ИзменитьВидимостьДоступностьНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьВидимостьДоступностьНаКлиенте()
	
	Элементы.ТипПодсчетаРезультата.Доступность = Запись.КоличествоПопыток>1;
	
КонецПроцедуры	

&НаСервере
Процедура ИзменитьВидимостьДоступностьНормативаНаСервере()
	
	ТипРезультата   = Запись.Норматив.ТипРезультата;
	ПересчетВМилСек = ТипРезультата = Перечисления.ТипыРезультатов.Время;
	
	Элементы.РезультатБулево.Видимость    = ТипРезультата = Перечисления.ТипыРезультатов.Булево;
	Элементы.РезультатСтроковый.Видимость = ПересчетВМилСек;
	Элементы.РезультатЧисло.Видимость     = ТипРезультата = Перечисления.ТипыРезультатов.Число;
	
	Запись.РезультатБулево = Ложь;
	
	Если ПересчетВМилСек Тогда
		ПересчитатьВремяВМиллесекундахВСтроку();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьВремяВМиллесекундахВСтроку()
	
	РезультатСтроковый = УчетСпортсменовОбщегоНазначенияКлиентСервер.ПересчитатьВремяВМиллесекундахВСтроку(Запись.РезультатЧисло);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРезультатСтроковыйПриИзменении(Отказ)
	
	Отказ = Ложь;
	УчетСпортсменовОбщегоНазначенияКлиентСервер.ПроверитьРезультатСтроковыйПриИзменении(РезультатСтроковый, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьВремяИзСтрокиВМиллесекунды(ТекущийОбъект)
	
	ТекущийОбъект.РезультатЧисло = УчетСпортсменовОбщегоНазначенияКлиентСервер.ПересчитатьВремяИзСтрокиВМиллесекунды(РезультатСтроковый);
	
КонецПроцедуры		

#КонецОбласти





