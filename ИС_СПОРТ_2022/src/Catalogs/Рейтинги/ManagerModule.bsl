#Область Печать

Функция ПолучитьДанныеПечати(знач МассивДокументов, знач МассивИменМакетов) Экспорт
	ДанныеПоВсемОбъектам = Новый Соответствие;
	Для Каждого ОбъектСсылка Из МассивДокументов Цикл
		ДанныеОбъектаПоМакетам = Новый Соответствие;
		Для Каждого ИмяМакета Из МассивИменМакетов Цикл
			ДанныеОбъектаПоМакетам.Вставить(ИмяМакета, ПолучитьДанныеОбъекта(ОбъектСсылка,ИмяМакета));
		КонецЦикла;
		ДанныеПоВсемОбъектам.Вставить(ОбъектСсылка, ДанныеОбъектаПоМакетам);
	КонецЦикла;
	
	ОписаниеОбластей = Новый Соответствие;
	ДвоичныеДанныеМакетов = Новый Соответствие;
	ТипыМакетов = Новый Соответствие;
	                                                             
	Для Каждого ИмяМакета Из МассивИменМакетов Цикл
		Если ИмяМакета = "ПФ_ПоложениеОРейтинге" Тогда
			ДвоичныеДанныеМакетов.Вставить(ИмяМакета, Справочники.Рейтинги.ПолучитьМакет("ПФ_ПоложениеОРейтинге"));
			ТипыМакетов.Вставить(ИмяМакета, "DOC");
		КонецЕсли;
		ОписаниеОбластей.Вставить(ИмяМакета, ПолучитьОписаниеОбластейМакетаОфисногоДокумента(ИмяМакета));
	КонецЦикла;

	
	Возврат Новый Структура("Данные, Макеты",
	ДанныеПоВсемОбъектам,
	Новый Структура("ОписаниеОбластей, ТипыМакетов, ДвоичныеДанныеМакетов",
	ОписаниеОбластей,
	ТипыМакетов,
	ДвоичныеДанныеМакетов));


КонецФункции

Функция ПолучитьОписаниеОбластейМакетаОфисногоДокумента(ИмяМакета)
	
	ОписаниеОбластей = Новый Структура;
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Заголовок"                       , "Общая");
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаТаблицы"                   , "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаТаблицыСоревнований"       , "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "УсловиеРейтинга1"                , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Цель"                            , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "УсловиеРейтинга2"                , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Дисциплина"              		  , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Спонсор"                         , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "УсловиеРейтинга3"                , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ВозрастнаяГруппа"                , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "УсловиеРейтинга4"                , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "УсловиеРейтинга5"                , "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "УсловиеРейтинга6"                , "Общая");
	
	Возврат ОписаниеОбластей;
	
КонецФункции

Функция ПолучитьДанныеОбъекта(ОбъектыПечати,ИмяМакета) Экспорт
	Если ИмяМакета = "ПФ_ПоложениеОРейтинге" Или ИмяМакета = "МакетПоложениеОРейтинге" Тогда
		Возврат ПолучитьДанныеОбъектаПоложениеОРейтинге(ОбъектыПечати);
	КонецЕсли;
КонецФункции

Функция ПолучитьДанныеОбъектаПоложениеОРейтинге(ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивРейтингов",ОбъектыПечати); 
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Рейтинги.Ссылка КАК Ссылка,
	               |	Рейтинги.Наименование КАК РейтингНаименование,
	               |	Рейтинги.Организатор КАК Организатор,
	               |	Рейтинги.ДатаПодсчетаРейтинга КАК ДатаПодсчетаРейтинга,
	               |	Рейтинги.ДатаНачалаРейтинга КАК ДатаНачалаРейтинга,
	               |	Рейтинги.ДатаОкончанияРейтинга КАК ДатаОкончанияРейтинга,
	               |	Рейтинги.ГлавныйСудья КАК ГлавныйСудья,
	               |	Рейтинги.ЗаместительГлавногоСудьи КАК ЗаместительГлавногоСудьи,
	               |	Рейтинги.Секретарь КАК Секретарь,
	               |	РейтингиКонтактнаяИнформация.Представление КАК ТерриторияПроведенияРейтинга,
	               |	Рейтинги.ГлавнаяЦель КАК ГлавнаяЦель,
	               |	ЗаявкиНаПроведениеСоревнованияСрезПоследних.ВидСпорта КАК ВидСпорта
	               |ИЗ
	               |	Справочник.Рейтинги КАК Рейтинги
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Рейтинги.КонтактнаяИнформация КАК РейтингиКонтактнаяИнформация
	               |		ПО Рейтинги.Ссылка = РейтингиКонтактнаяИнформация.Ссылка
	               |			И (РейтингиКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(справочник.видыконтактнойинформации.ГородРейтинги))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявкиНаПроведениеСоревнования.СрезПоследних КАК ЗаявкиНаПроведениеСоревнованияСрезПоследних
	               |		ПО Рейтинги.Ссылка = ЗаявкиНаПроведениеСоревнованияСрезПоследних.Соревнование.Ссылка
	               |ГДЕ
	               |	Рейтинги.Ссылка В(&МассивРейтингов)";
	
	МассивДокументов = Новый Массив;
	
	ВыборкаРейтинг = Запрос.Выполнить().Выбрать();
	Пока ВыборкаРейтинг.Следующий() Цикл
		
		СтруктураДокумента = Новый Структура;
		
		СтруктураШапка = Новый Структура;
		СтруктураШапка.Вставить("РейтингНаименование", ВыборкаРейтинг.РейтингНаименование);
		СтруктураШапка.Вставить("Год", Формат(ВыборкаРейтинг.ДатаНачалаРейтинга,"ДФ=гггг") + "-" + Формат(ВыборкаРейтинг.ДатаОкончанияРейтинга,"ДФ=гггг"));
		СтруктураШапка.Вставить("ГлавнаяЦель", ВыборкаРейтинг.ГлавнаяЦель);
		СтруктураДокумента.Вставить("Заголовок", СтруктураШапка);
				
		МассивСтрок = Новый Массив;
		Для Каждого строкаЦель Из ВыборкаРейтинг.Ссылка.ЦелиРейтинга Цикл
			// {Рарус ivaart IN-18116 исправление ошибки генерации печатной формы 27.07.2021
			// Ошибка обращения к базе данных за методиста
			Попытка
				СтруктураСтрока = Новый Структура("ОписаниеЦели", строкаЦель.Цель.Описание);
				МассивСтрок.Добавить(СтруктураСтрока);
			Исключение
				Продолжить;
			КонецПопытки;
			// }Рарус ivaart IN-18116 исправление ошибки генерации печатной формы 27.07.2021
		КонецЦикла;
		СтруктураДокумента.Вставить("Цель", МассивСтрок);
		
		СтруктураУсловиеРейтинга2 = Новый Структура("ТерриторияПроведенияРейтинга, ЧислоМесяцГод, Год, МесяцГод, Организатор, СписокУчреждений", 
		ВыборкаРейтинг.ТерриторияПроведенияРейтинга,
		Формат(ВыборкаРейтинг.ДатаПодсчетаРейтинга,"ДЛФ=ДД"),
		Формат(ВыборкаРейтинг.ДатаОкончанияРейтинга,"ДФ=гггг"),
		Формат(ВыборкаРейтинг.ДатаНачалаРейтинга,"ДФ=ММММ") + " " +Формат(ВыборкаРейтинг.ДатаОкончанияРейтинга,"ДФ=гггг"),
		ВыборкаРейтинг.Организатор,
		ПолучитьСписокУчрежденийСтрокой(ВыборкаРейтинг.Ссылка));
		СтруктураДокумента.Вставить("УсловиеРейтинга2", СтруктураУсловиеРейтинга2);
		
		СтруктураУсловиеРейтинга6 = Новый Структура("СезонПериод", Формат(ВыборкаРейтинг.ДатаНачалаРейтинга,"ДФ=гггг") + "-" + Формат(ВыборкаРейтинг.ДатаОкончанияРейтинга,"ДФ=гггг"));
		СтруктураДокумента.Вставить("УсловиеРейтинга6", СтруктураУсловиеРейтинга6);
		
		МассивСтрокСпонсор = Новый Массив;
		Для Каждого строкаСпонсор Из ВыборкаРейтинг.Ссылка.СпонсорыРейтинга Цикл
			СтруктураСтрока = Новый Структура("НаименованиеСпонсоры", строкаСпонсор.Спонсор);
			МассивСтрокСпонсор.Добавить(СтруктураСтрока);
		КонецЦикла;
		СтруктураДокумента.Вставить("Спонсоры", МассивСтрокСпонсор);
		
		СтруктураУсловиеРейтинга3 = Новый Структура("ГлавныйСудья, ЗаместительГлавногоСудьи, Секретарь, Организатор, УчебныйГодПериод, ЧислоМесяцГод");
		ЗаполнитьЗначенияСвойств(СтруктураУсловиеРейтинга3, ВыборкаРейтинг);
		СтруктураУсловиеРейтинга3.УчебныйГодПериод = Формат(ВыборкаРейтинг.ДатаНачалаРейтинга,"ДФ=гггг") + "-" + Формат(ВыборкаРейтинг.ДатаОкончанияРейтинга,"ДФ=гггг");
		СтруктураУсловиеРейтинга3.ЧислоМесяцГод = Формат(ВыборкаРейтинг.ДатаНачалаРейтинга, "ДЛФ=ДД");
		СтруктураУсловиеРейтинга3.Вставить("ВидСпорта", ВыборкаРейтинг.ВидСпорта);
		СтруктураДокумента.Вставить("УсловиеРейтинга3", СтруктураУсловиеРейтинга3);
		
		// {Рарус ivaart IN-18116 исправление ошибки генерации печатной формы 27.07.2021
		// печатная форма не генирировалась, потому что в коде использовались несуществующие реквизиты дата начала и дата окончания из структуры выборки
		МассивСтрокВГ = Новый Массив;
		Для Каждого строкаВГ Из ВыборкаРейтинг.Ссылка.МеждународныеВозрастныеГруппы Цикл
			СтруктураСтрока = Новый Структура("ВозрастнаяГруппа", "Девушки " + Формат(ВыборкаРейтинг.ДатаНачалаРейтинга, "ДФ=гггг") + "-" + Формат(ВыборкаРейтинг.ДатаОкончанияРейтинга, "ДФ=гггг") + " г.р. - " + строкаВГ.МеждународнаяВозрастнаяГруппа.Наименование);
			МассивСтрокВГ.Добавить(СтруктураСтрока);
			СтруктураСтрока = Новый Структура("ВозрастнаяГруппа", "Юноши " + Формат(ВыборкаРейтинг.ДатаНачалаРейтинга, "ДФ=гггг") + "-" + Формат(ВыборкаРейтинг.ДатаОкончанияРейтинга, "ДФ=гггг") + " г.р. - " + строкаВГ.МеждународнаяВозрастнаяГруппа.Наименование);
			МассивСтрокВГ.Добавить(СтруктураСтрока);
		КонецЦикла;
		СтруктураДокумента.Вставить("ВозрастныеГруппы", МассивСтрокВГ);
		// }Рарус ivaart IN-18116 исправление ошибки генерации печатной формы 27.07.2021
		
		МассивСтрокСистемаПодсчетаБаллов = Новый Массив;
		Если ВыборкаРейтинг.Ссылка.МеждународныеВозрастныеГруппы.Количество() > 0 Тогда
		    таблицаБаллов = ЗаполнитьТаблицуБаллов(ВыборкаРейтинг.Ссылка.МеждународныеВозрастныеГруппы[0].СистемаПодсчетаБаллов);
			Для Каждого строкаБаллы Из таблицаБаллов Цикл
				струткураБаллы = Новый Структура;
				Для Каждого Колонка Из таблицаБаллов.Колонки Цикл
					струткураБаллы.Вставить(Колонка.Имя,строкаБаллы[Колонка.Имя]); 
				КонецЦикла;
				МассивСтрокСистемаПодсчетаБаллов.Добавить(струткураБаллы);
			КонецЦикла;	
		КонецЕсли;
		СтруктураДокумента.Вставить("СтрокаТаблицы",МассивСтрокСистемаПодсчетаБаллов);
		
		СтруктураУсловиеРейтинга5 = Новый Структура("ЧислоМесяцГод", Формат(ВыборкаРейтинг.Ссылка.ДатаПодсчетаРейтинга, "ДЛФ=Д"));
		СтруктураУсловиеРейтинга5.Вставить("СезонПериод",Формат(ВыборкаРейтинг.ДатаНачалаРейтинга,"ДФ=гггг") + "-" + Формат(ВыборкаРейтинг.ДатаОкончанияРейтинга,"ДФ=гггг"));
		СтруктураДокумента.Вставить("УсловиеРейтинга5",СтруктураУсловиеРейтинга5);
		
		СтруктураДокумента.Вставить("СтрокаТаблицыСоревнований", ПолучитьМассивСоревнований(ВыборкаРейтинг.Ссылка));
		
		СтруктураДокумента.Вставить("Дисциплина", ПолучитьМассивДисциплин(ВыборкаРейтинг.Ссылка));
		
		МассивДокументов.Добавить(СтруктураДокумента);
			
	КонецЦикла;
	
	Возврат МассивДокументов
	
КонецФункции

Функция ПолучитьСписокУчрежденийСтрокой(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала"   , Ссылка.ДатаНачалаРейтинга);
	Запрос.УстановитьПараметр("ДатаОкончания", Ссылка.ДатаОкончанияРейтинга);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СоставГруппы.Группа КАК Команда
	               |ИЗ
	               |	РегистрСведений.СоставГруппы.СрезПоследних(
	               |			&ДатаОкончания,
	               |			УчебныйГод.ДатаНачала >= &ДатаНачала
	               |				И УчебныйГод.ДатаОкончания <= &ДатаОкончания) КАК СоставГруппы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СоставГруппы.Группа";
	
	СтрокаВозврат = "";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаВозврат = СтрокаВозврат + СокрЛП(Выборка.Команда) + ",";
	КонецЦикла;
	
	Возврат Сред(СтрокаВозврат, 1, СтрДлина(СтрокаВозврат) - 1)
	
КонецФункции	

Функция ЗаполнитьТаблицуБаллов(БалльнаяСистемаСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",БалльнаяСистемаСсылка); 
	Запрос.Текст =  "ВЫБРАТЬ ";
	Для Индекс = 1 По 30 Цикл
	   Запрос.Текст  = Запрос.Текст  + Индекс + " КАК Место"  + Индекс + ",
	                |	ВЫБОР
	                |		КОГДА БалльнаяСистема.Место = " +  Индекс + "
	                |			ТОГДА БалльнаяСистема.КоличествоБаллов
	                |		ИНАЧЕ 0
	                |	КОНЕЦ КАК Баллы" + Индекс + ?(Индекс < 30, ",", "");
	КонецЦикла;				
	Запрос.Текст = Запрос.Текст + " ПОМЕСТИТЬ ВТ_Баллы
	                |ИЗ
	                |	Справочник.СистемаПодсчетаБаллов.БалльнаяСистема КАК БалльнаяСистема
	                |ГДЕ
	                |	БалльнаяСистема.Ссылка = &Ссылка
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ";
	Для Индекс = 1 По 30 Цикл				
	   Запрос.Текст  = Запрос.Текст  + " ВТ_Баллы.Место" + Индекс + " КАК Место" + Индекс + ",
	                |	СУММА(ВТ_Баллы.Баллы" + Индекс + ") КАК Баллы" + Индекс + ?(Индекс < 30, ",", "");
    КонецЦикла;	   
	Запрос.Текст  = Запрос.Текст  +" ИЗ
	                |	ВТ_Баллы КАК ВТ_Баллы
	                |
	                |СГРУППИРОВАТЬ ПО ";
	Для Индекс = 1 По 30 Цикл 				
	   Запрос.Текст  = Запрос.Текст  + " ВТ_Баллы.Место" + Индекс + ?(Индекс < 30, ",", "");
    КонецЦикла;
	
	тзДанные  = Запрос.Выполнить().Выгрузить();
	
	тзВозврат = Новый ТаблицаЗначений;
	тзВозврат.Колонки.Добавить("МестоДо10", Новый ОписаниеТипов("Число"));
	тзВозврат.Колонки.Добавить("ОчкиДо10" , Новый ОписаниеТипов("Число"));
	тзВозврат.Колонки.Добавить("МестоДо20", Новый ОписаниеТипов("Число"));
	тзВозврат.Колонки.Добавить("ОчкиДо20" , Новый ОписаниеТипов("Число"));
	тзВозврат.Колонки.Добавить("МестоДо30", Новый ОписаниеТипов("Число"));
	тзВозврат.Колонки.Добавить("ОчкиДо30" , Новый ОписаниеТипов("Число"));
	
	Если  тзДанные.Количество() = 0 Тогда
		Возврат тзВозврат;
	КонецЕсли;	
	
	Для Индекс = 1 По 9 Цикл
		строкаВозврат = тзВозврат.Добавить();
		строкаВозврат.МестоДо10 = тзДанные[0]["Место" + СокрЛП(Индекс)];
		строкаВозврат.ОчкиДо10  = тзДанные[0]["Баллы" + СокрЛП(Индекс)];
		
		строкаВозврат.МестоДо20 = тзДанные[0]["Место2" + СокрЛП(Индекс)];
		строкаВозврат.ОчкиДо20  = тзДанные[0]["Баллы2" + СокрЛП(Индекс)];
		Если Индекс = 1 Тогда
			строкаВозврат.МестоДо30 = тзДанные[0].Место30;
			строкаВозврат.ОчкиДо30  = тзДанные[0].Баллы30;
		КонецЕсли;
		Если Индекс = 9 Тогда
			строкаВозврат = тзВозврат.Добавить();
			строкаВозврат.МестоДо10 = тзДанные[0].Место10;
		    строкаВозврат.ОчкиДо10  = тзДанные[0].Баллы10;
			строкаВозврат.МестоДо20 = тзДанные[0].Место20;
		    строкаВозврат.ОчкиДо20  = тзДанные[0].Баллы20;
		КонецЕсли;
	КонецЦикла;
	
	Возврат тзВозврат;	
	
КонецФункции	

Функция ПолучитьМассивСоревнований(РейтингСсылка)
	// {Рарус ivaart IN-18116 исправление ошибки генерации печатной формы 27.07.2021
	// Запрос использовал несуществующие поля
	МассивСтрок = Новый Массив;
	
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПротоколСоревнования.ДокументОснование КАК ДокументОснование
	               |ПОМЕСТИТЬ ВТ_ПроводимыеСоревнования
	               |ИЗ
	               |	РегистрСведений.ВключениеСоревнованийВРейтинги КАК ВключениеСоревнованийВРейтинги
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПротоколСоревнования КАК ПротоколСоревнования
	               |		ПО ВключениеСоревнованийВРейтинги.Соревнование.Ссылка = ПротоколСоревнования.Соревнование.Ссылка
	               |ГДЕ
	               |	НЕ ПротоколСоревнования.ПометкаУдаления
	               |	И ВключениеСоревнованийВРейтинги.Рейтинг.Ссылка = &Рейтинг
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ПроводимыеСоревнования.ДокументОснование.Соревнование КАК Наименование,
	               |	ВТ_ПроводимыеСоревнования.ДокументОснование.ДатаНачалаСоревнования КАК Дата,
	               |	ВТ_ПроводимыеСоревнования.ДокументОснование.МестоПроведения КАК Место,
	               |	МеждународныеВозрастныеГруппы.МеждународнаяВозрастнаяГруппа КАК МеждународнаяВозрастнаяГруппа,
	               |	ЗаявкиНаПроведениеСоревнованияСрезПоследних.Дисциплина КАК Дисциплина
	               |ИЗ
	               |	ВТ_ПроводимыеСоревнования КАК ВТ_ПроводимыеСоревнования
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Соревнование.МеждународныеВозрастныеГруппы КАК МеждународныеВозрастныеГруппы
	               |		ПО ВТ_ПроводимыеСоревнования.ДокументОснование.Соревнование.Ссылка = МеждународныеВозрастныеГруппы.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявкиНаПроведениеСоревнования.СрезПоследних КАК ЗаявкиНаПроведениеСоревнованияСрезПоследних
	               |		ПО ВТ_ПроводимыеСоревнования.ДокументОснование.Соревнование.Ссылка = ЗаявкиНаПроведениеСоревнованияСрезПоследних.Соревнование.Ссылка
	               |ГДЕ
	               |	НЕ ВТ_ПроводимыеСоревнования.ДокументОснование.Соревнование ЕСТЬ NULL
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_ПроводимыеСоревнования.ДокументОснование.ДатаНачалаСоревнования,
	               |	ВТ_ПроводимыеСоревнования.ДокументОснование.МестоПроведения,
	               |	ВТ_ПроводимыеСоревнования.ДокументОснование.Соревнование,
	               |	ЗаявкиНаПроведениеСоревнованияСрезПоследних.Дисциплина,
	               |	МеждународныеВозрастныеГруппы.МеждународнаяВозрастнаяГруппа
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата
	               |ИТОГИ ПО
	               |	Наименование";

	Запрос.УстановитьПараметр("Рейтинг",РейтингСсылка);
	ВыборкаСоревнование = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	НомерСтроки = 1;
	Пока ВыборкаСоревнование.Следующий() Цикл
		СтруктураДанных = Новый Структура("НомерСтроки, Наименование, Дата, Место, ВозрастныеГруппы, СпортивнаяДисциплина", НомерСтроки, ВыборкаСоревнование.Наименование);
		Выборка = ВыборкаСоревнование.Выбрать();
		ВозрастныеГруппы = "";
		Пока Выборка.Следующий() Цикл
			СтруктураДанных.Дата  = Формат(Выборка.Дата,"ДЛФ=Д");
			СтруктураДанных.Место = Выборка.Место;
			ВозрастныеГруппы = ВозрастныеГруппы + ?(СтрНайти(ВозрастныеГруппы, Выборка.МеждународнаяВозрастнаяГруппа)<>0,"",СокрЛП(Выборка.МеждународнаяВозрастнаяГруппа) + ", ");
		КонецЦикла;
		СтруктураДанных.ВозрастныеГруппы = Сред(ВозрастныеГруппы, 1, СтрДлина(ВозрастныеГруппы)-2);
		СтруктураДанных.НомерСтроки = НомерСтроки;
		СтруктураДанных.СпортивнаяДисциплина = ВыборкаСоревнование.Дисциплина;
		НомерСтроки = НомерСтроки + 1;
		МассивСтрок.Добавить(СтруктураДанных);
	КонецЦикла;	
	// }Рарус ivaart IN-18116 исправление ошибки генерации печатной формы 27.07.2021
	
	Возврат МассивСтрок
	
КонецФункции

// {Рарус ivaart IN-18116 Доработка печатной формы 17.08.2021
// Получение спортивных дисциплин соревнования
Функция ПолучитьМассивДисциплин(РейтингСсылка)
	МассивСтрок = Новый Массив;
	
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВключениеСоревнованийВРейтинги.Соревнование КАК Соревнование
	               |ПОМЕСТИТЬ ВТ_ПроводимыеСоревнования
	               |ИЗ
	               |	РегистрСведений.ВключениеСоревнованийВРейтинги КАК ВключениеСоревнованийВРейтинги
	               |ГДЕ
	               |	ВключениеСоревнованийВРейтинги.Рейтинг.Ссылка = &Рейтинг
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаявкиНаПроведениеСоревнованияСрезПоследних.Дисциплина КАК Дисциплина
	               |ИЗ
	               |	ВТ_ПроводимыеСоревнования КАК ВТ_ПроводимыеСоревнования
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявкиНаПроведениеСоревнования.СрезПоследних КАК ЗаявкиНаПроведениеСоревнованияСрезПоследних
	               |		ПО ВТ_ПроводимыеСоревнования.Соревнование.Ссылка = ЗаявкиНаПроведениеСоревнованияСрезПоследних.Соревнование.Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаявкиНаПроведениеСоревнованияСрезПоследних.Дисциплина";

	Запрос.УстановитьПараметр("Рейтинг",РейтингСсылка);
	ВыборкаДисциплина = Запрос.Выполнить().Выбрать();
	НомерСтроки = 1;
	Пока ВыборкаДисциплина.Следующий() Цикл
		Если не ПустаяСтрока(ВыборкаДисциплина.Дисциплина) тогда
			СтруктураДанных = Новый Структура("НомерСтроки, Дисциплина", НомерСтроки, ВыборкаДисциплина.Дисциплина);
			НомерСтроки = НомерСтроки + 1;
			МассивСтрок.Добавить(СтруктураДанных);
		КонецЕсли;
	КонецЦикла;	
	
	Если МассивСтрок.Количество() = 0 тогда
		МассивСтрок.Добавить(Новый Структура("НомерСтроки, Дисциплина", НомерСтроки, " - "));
	КонецЕсли;
	
	Возврат МассивСтрок
	
КонецФункции
// {Рарус ivaart IN-18116 Доработка печатной формы 17.08.2021
#КонецОбласти

















