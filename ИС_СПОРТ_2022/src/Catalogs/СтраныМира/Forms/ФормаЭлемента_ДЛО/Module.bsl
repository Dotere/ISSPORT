
//==================================================================================================
#Область СобытияФормы
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Предопределенный Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Справочник.СтраныМира.Изменение", Объект.Ссылка, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Отказ = ЕстьДругойЭлементСТакимиСтранами();
	
КонецПроцедуры

#КонецОбласти


//==================================================================================================
#Область СобытияЭлементовФормы
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	НаименованиеПриИзменении_НаСервере();
	
КонецПроцедуры

#КонецОбласти


//==================================================================================================
#Область СобытияИмяТабличнойЧасти
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОЙ ЧАСТИ

&НаКлиенте
Процедура СтраныПриИзменении(Элемент)
	
	НаименованиеПереформироватьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраныПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Страны_ИзмененСостав();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраныПослеУдаления(Элемент)
	
	НаименованиеПереформироватьНаСервере();
	
КонецПроцедуры

#КонецОбласти


//==================================================================================================
#Область СобытияОбщегоНазначения
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//--------------------------------------------------------------------------------------------------
#Область СобытияОбщегоНазначенияСервер


&НаСервере
Процедура НаименованиеПриИзменении_НаСервере()
	
	стрНаим = СокрЛП(Объект.Наименование);
	Пока СтрДлина(стрНаим) > 0 Цикл
		Поз = Найти(стрНаим, "/");
		Если Поз = 0 Тогда
			Поз = Найти(стрНаим, "\");
		КонецЕсли;
		Если Поз = 0 Тогда
			Поз = Найти(стрНаим, ",");
		КонецЕсли;
		Если Поз = 0 Тогда
			Поз = Найти(стрНаим, ";");
		КонецЕсли;
		
		Если Поз = 0 Тогда
			ИщемНаименование 	= стрНаим;
			стрНаим				= "";
		Иначе
			ИщемНаименование 	= Лев(стрНаим, Поз - 1);
			стрНаим				= Сред(стрНаим, Поз + 1);
		КонецЕсли;
		
		ИщемНаименование = СокрЛП(ИщемНаименование);
		
		Страна = Справочники.СтраныМира.НайтиПоНаименованию(ИщемНаименование,Истина);
		Если Страна.Пустая() Тогда
			Сообщить("Не найдена страна: " + ИщемНаименование);
		Иначе
			строкиТЧ = Объект.Страны.НайтиСтроки(Новый Структура("Страна", Страна));
			Если строкиТЧ.Количество() = 0 Тогда
				строкаТЧ = Объект.Страны.Добавить();
				строкаТЧ.Страна	= Страна;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	НаименованиеПереформироватьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура НаименованиеПереформироватьНаСервере()
	
	стрНаим = "";
	Для каждого стрТЧ Из Объект.Страны Цикл
		стрНаим = стрНаим + ?(стрНаим="","","/") + стрТЧ.Страна;
	КонецЦикла;
	
	Объект.Наименование			= стрНаим;
	Объект.НаименованиеПолное	= стрНаим;
	
КонецПроцедуры

&НаСервере
Процедура Страны_ИзмененСостав()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтраныМираСтраны.Страна
	|ИЗ
	|	Справочник.СтраныМира.Страны КАК СтраныМираСтраны
	|ГДЕ
	|	СтраныМираСтраны.Ссылка В (&Страны)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Страна";
	Запрос.УстановитьПараметр("Страны", Объект.Страны.Выгрузить().ВыгрузитьКолонку("Страна"));
	Объект.Страны.Загрузить(Запрос.Выполнить().Выгрузить());
	
	НаименованиеПереформироватьНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ЕстьДругойЭлементСТакимиСтранами()

	Отказ 		= Ложь;
	СтраныИзТЧ	= Объект.Страны.Выгрузить().ВыгрузитьКолонку("Страна");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЧ.Ссылка КАК Страна,
	|	СУММА(1) КАК СтранВсего,
	|	СУММА(ВЫБОР
	|			КОГДА ТЧ.Страна В (&Страны)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СтранВСписке
	|ПОМЕСТИТЬ ВТ_0
	|ИЗ
	|	Справочник.СтраныМира.Страны КАК ТЧ
	|ГДЕ
	|	ТЧ.Ссылка <> &Страна
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_0.Страна,
	|	ВТ_0.СтранВсего,
	|	ВТ_0.СтранВСписке
	|ИЗ
	|	ВТ_0 КАК ВТ_0
	|ГДЕ
	|	ВТ_0.СтранВСписке = ВТ_0.СтранВсего
	|	И ВТ_0.СтранВсего = &СтранВсего";
	Запрос.УстановитьПараметр("Страна"		, Объект.Ссылка);
	Запрос.УстановитьПараметр("Страны"		, СтраныИзТЧ);
	Запрос.УстановитьПараметр("СтранВсего"	, СтраныИзТЧ.Количество());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В " + Выборка.Страна + " такой же набор стран!",Выборка.Страна,,,Отказ);
	КонецЦикла;
	
	Возврат Отказ;

КонецФункции // ()

#КонецОбласти

//--------------------------------------------------------------------------------------------------
#Область СобытияОбщегоНазначенияКлиент


#КонецОбласти

#КонецОбласти


//==================================================================================================

