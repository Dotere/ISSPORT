#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьГиперссылкиКНастройкамРегистраОценкиПрохожденияНормативов();
	
	Если РольДоступна("Методист") Тогда
		ТекущаяОрганизация = ПараметрыСеанса.ТекущаяОрганизация;
		Элементы.ДобавитьНастройку.Видимость = ТаблицаНастроекНорматива.Количество()= 0; 
	КонецЕсли; 
	
	Если Параметры.Свойство("Владелец") Тогда
		Объект.Владелец = Параметры.Владелец;
	ИначеЕсли Объект.Ссылка.Пустая() Тогда
		Объект.Владелец = ПараметрыСеанса.ТекущаяОрганизация;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДобавлениеНастройкиВРегистрОценкиПрохожденияНормативов" Тогда
		ДобавитьНастройкуПродолжить(Параметр);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовШапки

&НаКлиенте
Процедура НастройкаНормативаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	гуид = Сред(Элемент.Имя,СтрНайти(Элемент.Имя,"_")+1);
	
	строкиТаблицыНастроек = ТаблицаНастроекНорматива.НайтиСтроки(Новый Структура("УникальныйИдентификатор",гуид));
	Если строкиТаблицыНастроек.Количество() = 0 Тогда
		Возврат
	Иначе
		
		ОткрытьФорму("Обработка.НастройкаРегистраОценкиПрохожденияНормативов.Форма.ФормаНастройкиНормативаФП", 
		  Новый Структура("Организация, Норматив, ВидСпорта", строкиТаблицыНастроек[0].Организация, Объект.Ссылка,строкиТаблицыНастроек[0].ВидСпорта),
		  ЭтаФорма,,,,,
		  РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ДобавитьНастройку(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьНастройкуЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.НастройкаРегистраОценкиПрохожденияНормативов.Форма.ФормаНастройкиНормативаФП", 
		  Новый Структура("Организация, Норматив", ТекущаяОрганизация, Объект.Ссылка),
		  ЭтаФорма,,,,
		  Оповещение,
		  РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьГиперссылкиКНастройкамРегистраОценкиПрохожденияНормативов()
	
	ОграниченныеПраваНаПросмотр = РольДоступна("Методист");
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ОценкиПрохожденияНормативов.Организация КАК Организация,
	                |	ОценкиПрохожденияНормативов.ВидСпорта КАК ВидСпорта
	                |ИЗ
	                |	РегистрСведений.ОценкиПрохожденияНормативов КАК ОценкиПрохожденияНормативов
	                |ГДЕ
	                |	%1
	                |	ОценкиПрохожденияНормативов.Норматив = &Норматив
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ОценкиПрохожденияНормативов.Организация,
	                |	ОценкиПрохожденияНормативов.ВидСпорта";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ?(ОграниченныеПраваНаПросмотр, "ОценкиПрохожденияНормативов.Организация = &Организация И ОценкиПрохожденияНормативов.ВидСпорта В (&ВидСпорта) И ","")); 
	ТекущаяОрганизация = ПараметрыСеанса.ТекущаяОрганизация;
	Запрос.УстановитьПараметр("Организация", ТекущаяОрганизация);
	Если ОграниченныеПраваНаПросмотр Тогда
		ЗапросВидовСпорта = Новый Запрос;
		ЗапросВидовСпорта.Текст = "ВЫБРАТЬ
		                          |	ВидыСпорта.Ссылка КАК Ссылка
		                          |ИЗ
		                          |	Справочник.ВидыСпорта КАК ВидыСпорта
		                          |ГДЕ
		                          |	ВидыСпорта.Владелец = &Владелец";
		ЗапросВидовСпорта.УстановитьПараметр("Владелец",ТекущаяОрганизация);
		Запрос.УстановитьПараметр("ВидСпорта", ЗапросВидовСпорта.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Норматив"   , Объект.Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	Иначе
		
		НовыеРеквизиты = Новый Массив;
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			строкаТаблицаНастроекНорматива = ТаблицаНастроекНорматива.Добавить();
			ЗаполнитьЗначенияСвойств(строкаТаблицаНастроекНорматива,Выборка);
			строкаТаблицаНастроекНорматива.УникальныйИдентификатор = СтрЗаменить(Новый УникальныйИдентификатор(),"-","_");
			
			НовыеРеквизиты.Добавить(Новый РеквизитФормы("НастройкаНорматива_" + строкаТаблицаНастроекНорматива.УникальныйИдентификатор, Новый ОписаниеТипов("Строка"))); 
			
		КонецЦикла;
		
		ИзменитьРеквизиты(НовыеРеквизиты);
		
		Для Каждого строкаТаблицаНастроекНорматива Из ТаблицаНастроекНорматива Цикл
			
			гуид = строкаТаблицаНастроекНорматива.УникальныйИдентификатор;
			
			ДобавитьНовыйЭлементНаФорму(гуид, строкаТаблицаНастроекНорматива.Организация, строкаТаблицаНастроекНорматива.ВидСпорта);
			
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ДобавитьНастройкуЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	Иначе
		ДобавитьНастройкуПродолжить(Результат);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНастройкуПродолжить(Результат)
	
	строкиТаблицаНастроекНорматива = ТаблицаНастроекНорматива.НайтиСтроки(Новый Структура("Организация, ВидСпорта",Результат.Организация, Результат.ВидСпорта));
	Если строкиТаблицаНастроекНорматива.Количество() = 0 Тогда
		ДобавитьНовуюГиперссылкуНаФорму(Результат);
		ЭтаФорма.Прочитать();
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьНовуюГиперссылкуНаФорму(СтруктураДанных)
	
	НовыеРеквизиты = Новый Массив;
	
	строкаТаблицаНастроекНорматива = ТаблицаНастроекНорматива.Добавить();
	ЗаполнитьЗначенияСвойств(строкаТаблицаНастроекНорматива,СтруктураДанных);
	строкаТаблицаНастроекНорматива.УникальныйИдентификатор = СтрЗаменить(Новый УникальныйИдентификатор(),"-","_");
	
	НовыеРеквизиты.Добавить(Новый РеквизитФормы("НастройкаНорматива_" + строкаТаблицаНастроекНорматива.УникальныйИдентификатор, Новый ОписаниеТипов("Строка")));
	
	ИзменитьРеквизиты(НовыеРеквизиты);
	
	гуид = строкаТаблицаНастроекНорматива.УникальныйИдентификатор;
	
	ДобавитьНовыйЭлементНаФорму(гуид, СтруктураДанных.Организация, СтруктураДанных.ВидСпорта);
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьНовыйЭлементНаФорму(гуид, Организация, ВидСпорта)
	
	НовыйЭлемент                    = Элементы.Добавить("НастройкаНорматива_" + гуид, Тип("ПолеФормы"), Элементы.НормативныеТребования); 
	НовыйЭлемент.Вид                = ВидПоляФормы.ПолеНадписи; 
	НовыйЭлемент.ПутьКДанным        = "НастройкаНорматива_" + гуид;
	НовыйЭлемент.Видимость          = Истина;
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.Гиперссылка        = Истина;
	НовыйЭлемент.УстановитьДействие("Нажатие", "НастройкаНормативаНажатие");
	
	ЭтотОбъект["НастройкаНорматива_" + гуид] = СокрЛП(Организация) + "(вид спорта: " + СокрЛП(ВидСпорта) + ")";
	
КонецПроцедуры	

#КонецОбласти













