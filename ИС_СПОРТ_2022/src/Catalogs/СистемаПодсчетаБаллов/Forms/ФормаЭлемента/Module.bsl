
&НаКлиенте
Процедура ЗаполнитьМеста(Команда)
	
	Если НЕ Объект.БалльнаяСистема.Количество()= 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьМестаОтвет",
		ЭтотОбъект);	
		
		ПоказатьВопрос(Оповещение,
		"Табличная часть будет очищена.
		|Продолжить?",
		РежимДиалогаВопрос.ДаНет,
		0);
		
		Возврат;
		
	КонецЕсли;	
	
	ЗаполнитьМестаПродолжить();
	
КонецПроцедуры
	
&НаКлиенте
Процедура ЗаполнитьМестаОтвет(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.БалльнаяСистема.Очистить();
		ЗаполнитьМестаПродолжить();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМестаПродолжить()
	
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьМестаВыборВариантаЗаполнения",
      ЭтотОбъект);
	  
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.Да     , "Диапазон");
	СписокКнопок.Добавить(КодВозвратаДиалога.Нет    , "Загрузка из файла");
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена , "Отмена");
 
    ПоказатьВопрос(Оповещение,
        "Выберите метод заполнения системы",
        СписокКнопок,
        0);
		
КонецПроцедуры
	
&НаКлиенте
Процедура ЗаполнитьМестаВыборВариантаЗаполнения(Результат, ДопПараметры) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыОткрытия.Вставить("ЗаполнятьПоДиапозону", Истина);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ПараметрыОткрытия.Вставить("ЗаполнятьПоДиапозону", Ложь);
	Иначе
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнениеТабличнойЧасти",
      ЭтотОбъект);
	
	ОткрытьФорму("Справочник.СистемаПодсчетаБаллов.Форма.ФормаВыбораДиапозона",ПараметрыОткрытия,ЭтаФорма,,,,Оповещение); 
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнениеТабличнойЧасти(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтруктураЗаполненияТЧ Из Результат.БалльнаяСистема Цикл
		строкаОбъект = Объект.БалльнаяСистема.Добавить();
		ЗаполнитьЗначенияСвойств(строкаОбъект,СтруктураЗаполненияТЧ);
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// {Рарус dotere #22850 -Скрывает поле организации у методиста 2022.01.17
	Если ПараметрыСеанса.ТекущаяОрганизация.Пустая() Тогда
		Элементы.Владелец.Видимость = Истина;
	Иначе
		Элементы.Владелец.Видимость = Ложь;
		Объект.Владелец = ПараметрыСеанса.ТекущаяОрганизация;
	КонецЕсли;
	// }Рарус dotere #22850 -Скрывает поле организации у методиста 2022.01.17
КонецПроцедуры

















