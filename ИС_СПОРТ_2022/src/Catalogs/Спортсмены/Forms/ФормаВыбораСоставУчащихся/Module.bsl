
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.Свойство("ТолькоВыбор") Тогда
		Элементы.СписокПодобрать.Видимость         = Ложь;
		Элементы.ФормаВыбрать.КнопкаПоУмолчанию    = Истина;
		ВыборЗначения = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоПодбор") Тогда
		Элементы.ФормаВыбрать.Видимость            = Ложь;
		Элементы.СписокПодобрать.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДатаСреза"  , Параметры.ДатаСреза);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Организация", Параметры.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ВыборЗначения Тогда
		Оповестить("ПодборСпискаСпортсменов", Новый Структура("СписокСпортсменов",СписокСпортсменов)); 	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийДинамическогоСпискаСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыборЗначения Тогда
	   ВыбратьЗначениеИЗакрыть();
    Иначе
	   ПодобратьЗначение();
	КонецЕсли;   
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьЗначениеИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	
	ПодобратьЗначение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьЗначениеИЗакрыть()
	
	ТекСпортсмен = Неопределено;
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекДанные = Неопределено Тогда
		ТекСпортсмен = ТекДанные.Ссылка;
	КонецЕсли;
	
	ЭтаФорма.Закрыть(Новый Структура("Спортсмен", ТекСпортсмен));
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьЗначение()
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекДанные = Неопределено Тогда
		СписокСпортсменов.Добавить(ТекДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти