#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЛичнаяКарточкаСпортсмена") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
																	"ЛичнаяКарточкаСпортсмена", // Идентфикатор
																	"Личная карточка спортсмена", // Название (представление) 
								  СформироватьЛичнуюКарточкуСпортсмена(МассивОбъектов)); // Функция которая получает макет и его заполняет
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьЛичнуюКарточкуСпортсмена(МассивОбъектов) Экспорт
	
	ПакетДанныеПоСпортсмену = ДанныеДляЛичнойКарточкиСпортсмена(МассивОбъектов);
	
	ПутьКМакету = "Справочник.Спортсмены.ЛичнаяКарточкаСпортсмена";
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету);
	
	ПФР_ПФЗ = ПакетДанныеПоСпортсмену[1].Выгрузить();
	таблицаСоревнований = ПакетДанныеПоСпортсмену[5].Выгрузить();
	
	ТабДок = Новый ТабличныйДокумент;
	
	ВыборкаПоСпортсмену = ПакетДанныеПоСпортсмену[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоСпортсмену.Следующий() Цикл
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		
		Выборка = ВыборкаПоСпортсмену.Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбластьШапка.Параметры.Заполнить(Выборка);
			МестоУчебы = СокрЛП(Выборка.УчебноеУчреждение) + ?(ЗначениеЗаполнено(Выборка.УчебноеУчреждение), ", ", "");
			МестоУчебы = МестоУчебы + ?(ЗначениеЗаполнено(Выборка.Класс), СокрЛП(ВЫборка.Класс) + " класс, ", "");
			МестоУчебы = МестоУчебы + ?(ЗначениеЗаполнено(Выборка.Факультет), СокрЛП(Выборка.Факультет) + ", ","");
			МестоУчебы = МестоУчебы + ?(ЗначениеЗаполнено(Выборка.Курс), СокрЛП(Выборка.Курс) + " курс",""); 
			ОбластьШапка.Параметры.МестоУчебы = ?(Прав(МестоУчебы,2) = ", ", Сред(МестоУчебы,1,СтрДлина(МестоУчебы)-2),МестоУчебы);
		КонецЦикла;
		
		ТабДок.Вывести(ОбластьШапка);
		
		массивПФР_ПФЗ = ПФР_ПФЗ.НайтиСтроки(Новый Структура("Спортсмен",ВыборкаПоСпортсмену.Спортсмен));
		
		ОбластьПФР = Макет.ПолучитьОбласть("ПФР");
		
		массивКолонокПФР = Новый Массив;
		массивКолонокПФР.Добавить("ПФР");
		массивКолонокПФР.Добавить("Рост");
		массивКолонокПФР.Добавить("Вес");
		массивКолонокПФР.Добавить("МедОсмотр");
		массивКолонокПФР.Добавить("Заключение");
		
		Для Индекс = 0 По массивПФР_ПФЗ.Количество()-1 Цикл
			Если Индекс = 5 Тогда
				Прервать;
			КонецЕсли;
			Для Каждого колонкаПФР Из массивКолонокПФР Цикл
				ОбластьПФР.Параметры[колонкаПФР + "_" + СокрЛП(Индекс+1)] = массивПФР_ПФЗ[Индекс][колонкаПФР];
			КонецЦикла;	
		КонецЦикла;	
		
		ТабДок.Вывести(ОбластьПФР);
		
		ОбластьПФЗ = Макет.ПолучитьОбласть("ПФЗ");
		
		массивКолонокПФЗ = Новый Массив;
		массивКолонокПФЗ.Добавить("Быстрота");
		массивКолонокПФЗ.Добавить("Выносливость");
		массивКолонокПФЗ.Добавить("Реакция");
		массивКолонокПФЗ.Добавить("Сила");
		
		Для Индекс = 0 По массивПФР_ПФЗ.Количество()-1 Цикл
			Если Индекс = 5 Тогда
				Прервать;
			КонецЕсли;
			Для Каждого колонкаПФЗ Из массивКолонокПФЗ Цикл
				ОбластьПФЗ.Параметры[колонкаПФЗ + "_" + СокрЛП(Индекс+1)] = массивПФР_ПФЗ[Индекс][колонкаПФЗ];
			КонецЦикла;	
		КонецЦикла;	
		
		ТабДок.Вывести(ОбластьПФЗ);
		
		ШапкаТаблицыСоревнований = Макет.ПолучитьОбласть("ШапкаТаблицыСоревнований");
		ТабДок.Вывести(ШапкаТаблицыСоревнований);
		
		массивСоревнований = таблицаСоревнований.НайтиСтроки(Новый Структура("Спортсмен",ВыборкаПоСпортсмену.Спортсмен));
		Для Индекс = 0 По массивСоревнований.Количество()-1 Цикл
			ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТаблицыСоревнований");
			ОбластьСтрока.Параметры.Заполнить(массивСоревнований[Индекс]);
			ОбластьСтрока.Параметры.НомерПоПорядку = Индекс + 1;
			// {Рарус adilas #8756 -Исправить вывод отчёта, преобразовать результат в чч.мм.сс.мм 2020.08.18
			// 
			РезультатВМиллисекундах = ОбластьСтрока.Параметры.РезультатЧисло;
			Если РезультатВМиллисекундах <> Неопределено Тогда 
				ОбластьСтрока.Параметры.РезультатЧисло = УчетСпортсменовОбщегоНазначенияКлиентСервер.ПересчитатьВремяВМиллесекундахВСтроку(РезультатВМиллисекундах);
			Иначе
				Продолжить;
			КонецЕсли;
			// }Рарус adilas #8756 -Исправить вывод отчёта, преобразовать результат в чч.мм.сс.мм 2020.08.18
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	
	ТабДок.КлючПараметровПечати	= ПутьКМакету;
	ТабДок.ОриентацияСтраницы 	= ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб 			= Истина;
	
	Возврат ТабДок;
	
КонецФункции

Функция ДанныеДляЛичнойКарточкиСпортсмена(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Спортсмены.Ссылка КАК Спортсмен,
	               |	ФизическиеЛица.Фамилия КАК Фамилия,
	               |	ФизическиеЛица.Имя КАК Имя,
	               |	ФизическиеЛица.Отчество КАК Отчество,
	               |	ФизическиеЛица.ДатаРождения КАК ДатаРождения,
	               |	АдресПроживанияФизЛица.Представление КАК АдресПроживания,
	               |	ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних.ВидСпорта КАК ВидСпорта,
	               |	ГруппаСпортсменов.Группа КАК Группа,
	               |	ГруппаСпортсменов.ТренерСборной КАК ТренерСборной,
	               |	УчебныеУчрежденияСпортсменовСрезПоследних.Организация КАК УчебноеУчреждение,
	               |	УчебныеУчрежденияСпортсменовСрезПоследних.Класс КАК Класс,
	               |	УчебныеУчрежденияСпортсменовСрезПоследних.Курс КАК Курс,
	               |	УчебныеУчрежденияСпортсменовСрезПоследних.Факультет КАК Факультет
	               |ИЗ
	               |	Справочник.Спортсмены КАК Спортсмены
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |		ПО Спортсмены.ФизическоеЛицо = ФизическиеЛица.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК АдресПроживанияФизЛица
	               |		ПО Спортсмены.ФизическоеЛицо = АдресПроживанияФизЛица.Ссылка
	               |			И (АдресПроживанияФизЛица.Вид = ЗНАЧЕНИЕ(справочник.видыконтактнойинформации.АдресМестаПроживанияФизическиеЛица))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчебныеУчрежденияСпортсменов.СрезПоследних КАК УчебныеУчрежденияСпортсменовСрезПоследних
	               |		ПО (УчебныеУчрежденияСпортсменовСрезПоследних.Спортсмен = Спортсмены.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставУчащихсяСпортивногоУчреждения.СрезПоследних(, ДатаОкончанияОбучения = ДАТАВРЕМЯ(1, 1, 1)) КАК СоставУчащихсяСпортивногоУчрежденияСрезПоследних
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭтапыСпортивнойПодготовкиСпортсменов.СрезПоследних КАК ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних
	               |			ПО СоставУчащихсяСпортивногоУчрежденияСрезПоследних.Спортсмен = ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних.Спортсмен
	               |				И СоставУчащихсяСпортивногоУчрежденияСрезПоследних.ВидСпорта = ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних.ВидСпорта
	               |				И СоставУчащихсяСпортивногоУчрежденияСрезПоследних.ДатаНачалаОбучения < ЭтапыСпортивнойПодготовкиСпортсменовСрезПоследних.Период
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставГруппы.СрезПоследних(
	               |					&Дата,
	               |					ДатаИсключенияИзГруппы >= &Дата
	               |						ИЛИ ДатаИсключенияИзГруппы = ДАТАВРЕМЯ(1, 1, 1)) КАК ГруппаСпортсменов
	               |			ПО СоставУчащихсяСпортивногоУчрежденияСрезПоследних.Спортсмен = ГруппаСпортсменов.Спортсмен
	               |				И СоставУчащихсяСпортивногоУчрежденияСрезПоследних.ВидСпорта = ГруппаСпортсменов.ВидСпорта
	               |				И СоставУчащихсяСпортивногоУчрежденияСрезПоследних.ДатаНачалаОбучения < ГруппаСпортсменов.Период
	               |		ПО СоставУчащихсяСпортивногоУчрежденияСрезПоследних.Спортсмен = Спортсмены.Ссылка
	               |ГДЕ
	               |	Спортсмены.Ссылка В(&МассивОбъектов)
	               |ИТОГИ ПО
	               |	Спортсмен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Спортсмен КАК Спортсмен,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Период КАК Период,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.ПоказательФизическогоРазвития КАК ПФР,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Рост КАК Рост,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Вес КАК Вес,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Быстрота КАК Быстрота,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Выносливость КАК Выносливость,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Сила КАК Сила,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Реакция КАК Реакция,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.ДатаМедОбследования КАК МедОсмотр,
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.ЗаключениеВрача КАК Заключение
	               |ИЗ
	               |	РегистрСведений.ПоказателиФизическогоЗдоровьяСпортсмена КАК ПоказателиФизическогоЗдоровьяСпортсмена
	               |ГДЕ
	               |	ПоказателиФизическогоЗдоровьяСпортсмена.Спортсмен В(&МассивОбъектов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ИтоговыеРезультатыСоревнования.Спортсмен КАК Спортсмен,
	               |	ИтоговыеРезультатыСоревнования.Соревнование КАК Соревнование,
	               |	ИтоговыеРезультатыСоревнования.РезультатЧисло КАК Результат,
	               |	УчастникиСоревнований.ДатаНачалаСоревнования КАК ДатаНачалаСоревнования,
	               |	ИтоговыеРезультатыСоревнования.Организация КАК Организация,
	               |	ИтоговыеРезультатыСоревнования.ВидСпорта КАК ВидСпорта
	               |ПОМЕСТИТЬ ВТ_Результаты
	               |ИЗ
	               |	РегистрСведений.ИтоговыеРезультатыСоревнования КАК ИтоговыеРезультатыСоревнования
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиСоревнований КАК УчастникиСоревнований
	               |		ПО ИтоговыеРезультатыСоревнования.Спортсмен = УчастникиСоревнований.Спортсмен
	               |			И ИтоговыеРезультатыСоревнования.Организация = УчастникиСоревнований.Организация
	               |			И ИтоговыеРезультатыСоревнования.УчебныйГод = УчастникиСоревнований.УчебныйГод
	               |			И ИтоговыеРезультатыСоревнования.Соревнование = УчастникиСоревнований.Соревнование
	               |			И ИтоговыеРезультатыСоревнования.ВидСпорта = УчастникиСоревнований.ВидСпорта
	               |ГДЕ
	               |	ИтоговыеРезультатыСоревнования.Спортсмен В(&МассивОбъектов)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Спортсмен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	МАКСИМУМ(ПодтвержденныеРазрядыСпортсменовСрезПоследних.ДатаПрисвоения) КАК ДатаПрисвоения,
	               |	МАКСИМУМ(ПодтвержденныеРазрядыСпортсменовСрезПоследних.ДатаПодтверждения) КАК ДатаПодтверждения,
	               |	ВТ_Результаты.Спортсмен КАК Спортсмен,
	               |	ВТ_Результаты.Соревнование КАК Соревнование,
	               |	ВТ_Результаты.Результат КАК Результат,
	               |	ВТ_Результаты.ДатаНачалаСоревнования КАК ДатаНачалаСоревнования,
	               |	ВТ_Результаты.Организация КАК Организация,
	               |	ВТ_Результаты.ВидСпорта КАК ВидСпорта
	               |ПОМЕСТИТЬ ВТ_Разряды
	               |ИЗ
	               |	ВТ_Результаты КАК ВТ_Результаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов.СрезПоследних(, ) КАК ПодтвержденныеРазрядыСпортсменовСрезПоследних
	               |		ПО ВТ_Результаты.Спортсмен = ПодтвержденныеРазрядыСпортсменовСрезПоследних.Спортсмен
	               |			И ВТ_Результаты.Организация = ПодтвержденныеРазрядыСпортсменовСрезПоследних.Организация
	               |			И ВТ_Результаты.ВидСпорта = ПодтвержденныеРазрядыСпортсменовСрезПоследних.ВидСпорта
	               |ГДЕ
	               |	(ПодтвержденныеРазрядыСпортсменовСрезПоследних.ДатаПрисвоения = ВТ_Результаты.ДатаНачалаСоревнования
	               |			ИЛИ ПодтвержденныеРазрядыСпортсменовСрезПоследних.ДатаПодтверждения = ВТ_Результаты.ДатаНачалаСоревнования
	               |				И ПодтвержденныеРазрядыСпортсменовСрезПоследних.ДатаПрисвоения <> ДАТАВРЕМЯ(1, 1, 1)
	               |				И ПодтвержденныеРазрядыСпортсменовСрезПоследних.ДатаПодтверждения <> ДАТАВРЕМЯ(1, 1, 1))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Результаты.Спортсмен,
	               |	ВТ_Результаты.Соревнование,
	               |	ВТ_Результаты.Результат,
	               |	ВТ_Результаты.ДатаНачалаСоревнования,
	               |	ВТ_Результаты.Организация,
	               |	ВТ_Результаты.ВидСпорта
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Спортсмен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПодтвержденныеРазрядыСпортсменовСрезПоследних.Разряд.Наименование КАК Разряд,
	               |	ВТ_Разряды.Спортсмен КАК Спортсмен,
	               |	ВТ_Разряды.Соревнование КАК Соревнование,
	               |	ВТ_Разряды.Результат КАК Результат,
	               |	ВТ_Разряды.ДатаНачалаСоревнования КАК ДатаНачалаСоревнования,
	               |	ВТ_Разряды.Организация КАК Организация,
	               |	ВТ_Разряды.ВидСпорта КАК ВидСпорта
	               |ПОМЕСТИТЬ ВТ_РезультатыСРазрядами
	               |ИЗ
	               |	ВТ_Разряды КАК ВТ_Разряды
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодтвержденныеРазрядыСпортсменов.СрезПоследних КАК ПодтвержденныеРазрядыСпортсменовСрезПоследних
	               |		ПО ВТ_Разряды.ДатаПрисвоения = ПодтвержденныеРазрядыСпортсменовСрезПоследних.ДатаПрисвоения
	               |			И ВТ_Разряды.ДатаПодтверждения = ПодтвержденныеРазрядыСпортсменовСрезПоследних.ДатаПодтверждения
	               |			И ВТ_Разряды.Спортсмен = ПодтвержденныеРазрядыСпортсменовСрезПоследних.Спортсмен
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Спортсмен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Результаты.Спортсмен КАК Спортсмен,
	               |	ВТ_Результаты.Соревнование КАК Соревнование,
	               |	ВТ_Результаты.Результат КАК РезультатЧисло,
	               |	ГОД(ВТ_Результаты.ДатаНачалаСоревнования) КАК Год,
	               |	ВТ_Результаты.Организация КАК Организация,
	               |	ВТ_Результаты.ВидСпорта КАК ВидСпорта,
	               |	ВТ_РезультатыСРазрядами.Разряд КАК Разряд
	               |ИЗ
	               |	ВТ_Результаты КАК ВТ_Результаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РезультатыСРазрядами КАК ВТ_РезультатыСРазрядами
	               |		ПО ВТ_Результаты.Спортсмен = ВТ_РезультатыСРазрядами.Спортсмен
	               |			И ВТ_Результаты.Соревнование = ВТ_РезультатыСРазрядами.Соревнование
	               |			И ВТ_Результаты.ДатаНачалаСоревнования = ВТ_РезультатыСРазрядами.ДатаНачалаСоревнования
	               |			И ВТ_Результаты.Результат = ВТ_РезультатыСРазрядами.Результат
	               |			И ВТ_Результаты.Организация = ВТ_РезультатыСРазрядами.Организация
	               |			И ВТ_Результаты.ВидСпорта = ВТ_РезультатыСРазрядами.ВидСпорта";
	Возврат Запрос.ВыполнитьПакет()
	
КонецФункции	

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)

    СтандартнаяОбработка = Ложь;
	// {Рарус adilas #8836 -Поиск по значению 2020.08.20
	//
	Поля.Добавить("ФизическоеЛицо");   
	// }Рарус adilas #8836 -Поиск по значению 2020.08.20  
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	УстановитьПривилегированныйРежим (Истина);
	// {Рарус adilas #8836 -Поиск по значению 2020.08.20
	//
	ФамилияСпортсмена        	= Данные.ФизическоеЛицо.Фамилия;
	ИмяСпортсмена				= Данные.ФизическоеЛицо.Имя;
	
	Представление 				= ФамилияСпортсмена + " " + ИмяСпортсмена;
	// }Рарус adilas #8836 -Поиск по значению 2020.08.20   
	УстановитьПривилегированныйРежим (Ложь);
	
КонецПроцедуры

#КонецОбласти