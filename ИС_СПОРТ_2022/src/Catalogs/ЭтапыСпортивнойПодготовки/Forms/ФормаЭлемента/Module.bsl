#Область ОбработчикиСобытийРеквизитовФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидимостьНастроекЭтапов = НЕ Объект.Родитель.Пустая();
	Элементы.НастройкиЭтапов.Видимость = ВидимостьНастроекЭтапов;
	
	Если ВидимостьНастроекЭтапов Тогда
		ЗаполнитьНастройкиЭтапов();
	КонецЕсли;
	
	Элементы.ГодаОбученияПоВидамСпорта.Видимость = НЕ ВидимостьНастроекЭтапов;
	
	ТекущаяОрганизация = ПараметрыСеанса.ТекущаяОрганизация;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ТаблицаГодовОбучения, "Этап", Объект.Ссылка, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовШапки

&НаКлиенте
Процедура ДекорацияНажатие(Элемент)
	
	Индекс = Число(Сред(Элемент.Имя, Найти(Элемент.Имя, "_") + 1));
	ПараметрыФормы = Новый Структура("Этап, Организация, ВидСпорта", 
	  Объект.Ссылка, ТаблицаСоответствияОрганизацииИДекорации[Индекс-1].Организация, ТаблицаСоответствияОрганизацииИДекорации[Индекс-1].ВидСпорта);
	  
	ОткрытьФорму("Обработка.НастройкаРегистраОценкиПрохожденияНормативов.Форма.ФормаНастройкиПоЭтапу", 
	   ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);  
	
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиСобытийТабличныхЧастей

&НаКлиенте
Процедура ТаблицаГодовОбученияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ОткрытьФорму("РегистрСведений.НастройкаКоличестваПодэтапов.ФормаЗаписи", Новый Структура("Организация, Этап", ТекущаяОрганизация, Объект.Ссылка), ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкиЭтапов()
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	Организации.Ссылка КАК Организация,
	                |	ВидыСпорта.Ссылка КАК ВидСпорта
	                |ИЗ
	                |	Справочник.Организации КАК Организации
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыСпорта КАК ВидыСпорта
	                |		ПО Организации.Ссылка = ВидыСпорта.Владелец
					// {Рарус adilas #19058 -Ведомость приема КПН. Настройка этапов. 2021.08.16
					|ГДЕ
	                |	ВидыСпорта.ЭтоГруппа = ЛОЖЬ";
					// }Рарус adilas #19058 -Ведомость приема КПН. Настройка этапов. 2021.08.16
	                
	Выборка = Запрос.Выполнить().Выбрать();
	Индекс = 1;
	Пока Выборка.Следующий() Цикл
		строкаТаблицыСоответствия = ТаблицаСоответствияОрганизацииИДекорации.Добавить();
		ЗаполнитьЗначенияСвойств(строкаТаблицыСоответствия,Выборка);
		
		Элемент1 = Элементы.Добавить("Декорация_" + СокрЛП(Индекс), Тип("ДекорацияФормы"), Элементы.НастройкиЭтапов);
	    Элемент1.Заголовок = СокрЛП(Выборка.Организация) + "/" + СокрЛП(Выборка.ВидСпорта);
		Элемент1.Гиперссылка = Истина;
		Элемент1.УстановитьДействие("Нажатие", "ДекорацияНажатие");
		
		Индекс = Индекс + 1;
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти
