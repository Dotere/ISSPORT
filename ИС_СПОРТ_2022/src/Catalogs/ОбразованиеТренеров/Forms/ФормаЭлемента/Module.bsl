
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ВидОбразования = "1";
		Объект.ОсновноеОбразование = Истина;
	Иначе
		ВидОбразования = ?(Объект.ОсновноеОбразование, "1", "2");
	КонецЕсли;
	
	// {Рарус adilas #9708 -Заполнять поле "Тренер" при создании квалификационной категории 2020.09.25
	//
	Если Параметры.Свойство("Тренер") Тогда
		Объект.Владелец = Параметры.Тренер;
	КонецЕсли;
	// }Рарус adilas #9708 -Заполнять поле "Тренер" при создании квалификационной категории 2020.09.25
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.Наименование = СокрЛП(Объект.Владелец) + "/" + ?(Объект.ОсновноеОбразование, "Основное", "Дополнительное");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитов

&НаКлиенте
Процедура ВидОбразованияПереключательПриИзменении(Элемент)
	
	Объект.ОсновноеОбразование = ВидОбразования = "1";
	Если ВидОбразования = "1" Тогда
		Объект.ВидОбучения = "";
	Иначе
		Объект.ФизкультурноеОбразование = Ложь;
	КонецЕсли;	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОбученияПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность();
	Если НЕ Объект.ВидОбучения = ПредопределенноеЗначение("Перечисление.ВидыОбучения.ПовышениеКвалификации") Тогда
		Объект.Квалификация = "";
		Объект.СрокДействияКвалификации = Дата(1,1,1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	Элементы.ГруппаОсновноеОбразование.Видимость = Объект.ОсновноеОбразование;
	Элементы.ГруппаДополнительноеОбразование.Видимость = НЕ Объект.ОсновноеОбразование;
	Элементы.ГруппаКвалификация.Видимость = Объект.ВидОбучения = ПредопределенноеЗначение("Перечисление.ВидыОбучения.ПовышениеКвалификации");
	
КонецПроцедуры	

#КонецОбласти