#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", Справочники.ВидыСпорта.ПустаяСсылка(), , , Истина);
    ТекОрганизация = ПараметрыСеанса.ТекущаяОрганизация;
    ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокВидовСпорта, "Организация", ТекОрганизация, , , Не ТекОрганизация.Пустая());
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокВидовСпорта

&НаКлиенте
Процедура СписокВидовСпортаПриАктивизацииСтроки(Элемент)
    
    ТекДанные = Элементы.СписокВидовСпорта.ТекущиеДанные;
    
    Если ТекДанные = Неопределено Тогда
        
        УстановитьОтборПодчиненногоСписка();
        
    Иначе
        
        ВидСпорта = ТекДанные.ВидСпорта;
        
        УстановитьОтборПодчиненногоСписка(ВидСпорта);
        
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// {Рарус adilas #18949 -Спортивные дисциплины 2021.07.30
&НаКлиенте
Процедура ФормаПодобратьИзРеестраСпортивныхДисциплин(Команда)
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("РежимВыбора", Истина);
	ПараметрыВыбора.Вставить("ЗакрыватьПриВыборе", Истина);
	
    Обработчик = Новый ОписаниеОповещения("ОбработкаВыбранногоЭлементаРеестрСпортивныхДисциплин", ЭтаФорма);
    
    ОткрытьФорму("Справочник.РеестрСпортивныхДисциплин.ФормаВыбора", ПараметрыВыбора, ЭтаФорма, , , , Обработчик, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПодчиненногоСписка(ВидСпорта = Неопределено)
    
    ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", ВидСпорта, , , Истина);
    
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбранногоЭлементаРеестрСпортивныхДисциплин(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Наименование");
		ПараметрыЗаполнения.Вставить("ДисциплинаПоРеестру");
		ПараметрыЗаполнения.Вставить("Владелец");
		ЗаполнитьПараметры(ПараметрыЗаполнения, Результат);
		ОткрытьФорму("Справочник.СпортивныеДисциплины.Форма.ФормаЭлемента", ПараметрыЗаполнения, ЭтаФорма);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметры(ПараметрыЗаполнения, Источник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ВидыСпорта.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыСпорта КАК ВидыСпорта
		|ГДЕ
		|	ВидыСпорта.ПометкаУдаления = ЛОЖЬ
		|	И ВидыСпорта.ВидСпортаПоРеестру = &ВидСпортаПоРеестру";
	
	Запрос.УстановитьПараметр("ВидСпортаПоРеестру", Источник.Владелец.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Владелец = ВыборкаДетальныеЗаписи.Ссылка;	
			
		КонецЦикла;
		
	КонецЕсли;	
		
	ПараметрыЗаполнения.Владелец = Владелец;
	ПараметрыЗаполнения.Наименование = Источник.Наименование;
	ПараметрыЗаполнения.ДисциплинаПоРеестру = Источник.Ссылка;
	
КонецПроцедуры
// }Рарус adilas #18949 -Спортивные дисциплины 2021.07.30

#КонецОбласти